	DO (1) NEXT
	PLEASE BEHOLD AN INTERCAL PORT OF ADVENTURES

	PLEASE INITIALIZE
	PLEASE CHOOSE PUKE BINARY I/O OR C-INTERCAL BINARY I/O
	       BY ABSTAINING FROM (50000) FOR PUKE BINARY I/O OR
	       REINSTATING (50000) FOR C-INTERCAL BINARY I/O
(1)	DO FORGET #1
	DON'T REINSTATE (50000)
	DO (50000) NEXT DO NOTE INITIALIZE I/O
	DO (51000) NEXT DO NOTE INITIALIZE LINE INPUT
	DO (60000) NEXT DO NOTE INITIALIZE DATABASE
	DO (61000) NEXT DO NOTE INITIALIZE STATE
	DO (2) NEXT
	PLEASE STOP

	PLEASE WELCOME PLAYER
(2)	DO FORGET #1
	DO .1 <- #65
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- #2
	DO (57101) NEXT DO NOTE YESNO
	DO (3) NEXT
	DO (5) NEXT
	PLEASE STOP

	PLEASE MARK HINT #3 HAS HINTED AND BUMP LAMP LIFETIME UP TO #1000
(3)	DO (4) NEXT
	DO FORGET #1
	DO .1 <- #1
	DO (55601) NEXT DO NOTE RSPEAK
	DO,60006SUB#13#2<-#10  DO,60006SUB#13#3<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO ,61000SUB#5 <- #1000 DO NOTE LIMIT <- #1000
	DO ,61004SUB#3#1 <- #1 DO NOTE HINTED(3) <- #1
	DO (5) NEXT
	PLEASE STOP
(4)	DO RESUME .1
	PLEASE STOP

	PLEASE NOTE CAN'T LEAVE CAVE ONCE IT'S CLOSING (EXCEPT BY MAIN OFFICE).
(5)	DO FORGET #1
	DO .1 <- ,61000SUB#19 DO NOTE .1 <- CLOSNG
	DO .2 <- #0
	DO (11) NEXT DO NOTE IF NOT CLOSNG
	DO .1 <- ,61000SUB#2 DO NOTE .1 <- NEWLOC
	DO .2 <- #0
	DO (11) NEXT DO NOTE IF NEWLOC EQ #0
	DO .2 <- #9
	DO (40004) NEXT DO NOTE .3 <- NEWLOC LT #9
	DO .1 <- .3
	DO .2 <- #0
	DO (11) NEXT DO NOTE IF NEWLOC GE #9
	DO .1 <- #130
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,61000SUB#2 <- ,61000SUB#1 DO NOTE NEWLOC <- LOC
	DO .1 <- ,61000SUB#25 DO NOTE .1 <- PANIC
	DO .2 <- #0
	DO .3 <- #15
	DO .4 <- ,61000SUB#17 DO NOTE .4 <- CLOCK2
	DO (40021) NEXT
	DO ,61000SUB#17 <- .1 DO NOTE IF NOT PANIC, CLOCK2 <- #15
	DO ,61000SUB#25 <- #1 DO NOTE PANIC <- #1
	DO .2 <- .1
	DO (11) NEXT
	PLEASE STOP

	PLEASE SEE IF A DWARF HAS SEEN HIM AND HAS COME FROM WHERE HE WANTS TO
	       GO.  IF SO, THE DWARF'S BLOCKING HIS WAY.  IF COMING FROM PLACE
	       FORBIDDEN TO PIRATE (DWARVES ROOTED IN PLACE) LET HIM GET OUT
	       (AND ATTACKED).
(11)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ,61000SUB#2 DO NOTE .1 <- NEWLOC
	DO .2 <- ,61000SUB#1 DO NOTE .2 <- LOC
	DO (21) NEXT DO NOTE IF NEWLOC EQ LOC
	DO (30151) NEXT DO NOTE .1 <- FORCED(LOC).
	DO .2 <- #1
	DO (21) NEXT DO NOTE IF FORCED(LOC).
	DO .1 <- ,61000SUB#1
	DO .2 <- #8
	DO (30152) NEXT DO NOTE .1 <- LOC.FORBIDDEN TO PIRATE
	DO .2 <- #1
	DO (21) NEXT DO NOTE IF LOC.FORBIDDEN TO PIRATE
	DO .4 <- #1
	DO (12) NEXT
	PLEASE STOP
	PLEASE FOR .4 IN #1 .. #5
(12)	DO FORGET #1
	DO (13) NEXT
	DO .1 <- .4
	DO .2 <- #5
	DO (21) NEXT
	DO (1020) NEXT
	DO .4 <- .1
	DO (12) NEXT
	PLEASE STOP
(13)	DO .1 <- ,61002SUB.4#2 DO NOTE .1 <- ODLOC(.4).
	DO .2 <- ,61000SUB#2
	DO (40011) NEXT DO NOTE .1=.2 ODLOC(.4) EQ NEWLOC
	DO .1 <- ,61002SUB.4#3 DO NOTE .1 <- DSEEN(.4).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 DSEEN
	DO ,61000SUB#2 <- ,61000SUB#1 DO NOTE NEWLOC <- LOC
	DO .1 <- #2
	DO (55601) NEXT DO NOTE RSPEAK
	DO .2 <- .1
	DO (21) NEXT
	PLEASE STOP

	PLEASE FIRST OFF, DON'T LET THE DWARVES FOLLOW HIM INTO A PIT OR A
	       WALL.  ACTIVATE THE WHOLE MESS THE FIRST TIME HE GETS AS FAR AS
	       THE HALL OF MISTS (LOC 15).  IF NEWLOC IS FORBIDDEN TO PIRATE
	       (IN PARTICULAR, IF IT'S BEYOND THE TROLL BRIDGE), BYPASS DWARF
	       STUFF.  THAT WAY PIRATE CAN'T STEAL RETURN TOLL, AND DWARVES
	       CAN'T MEET THE BEAR.  ALSO MEANS DWARVES WON'T FOLLOW HIM INTO
	       DEAD END IN MAZE, BUT C'EST LA VIE.  THEY'LL WAIT FOR HIM
	       OUTSIDE THE DEAD END.
(21)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO ,61000SUB#1 <- ,61000SUB#2 DO NOTE LOC <- NEWLOC
	DO (30021) NEXT DO NOTE LOCATIONS OF OBJECTS IN MULTIPLE LOCATIONS
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #0
	DO (100) NEXT DO NOTE IF LOC EQ #0
	DO (30151) NEXT DO NOTE .1 <- FORCED(LOC).
	DO .2 <- #1
	DO (100) NEXT DO NOTE IF FORCED(LOC).
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #8
	DO (30152) NEXT DO NOTE .1 <- LOC.FORBIDDEN TO PIRATE
	DO .2 <- #1
	DO (100) NEXT DO NOTE IF 
	DO .1 <- ,61000SUB#10 DO NOTE .1 <- DFLAG
	DO .2 <- #0
	DO (22) NEXT DO NOTE IF DFLAG EQ #0
	DO (31) NEXT
	PLEASE STOP
(22)	DO (40011) NEXT DO NOTE .1=.2 DFLAG EQ #0
	DO FORGET #1
	DO .1 <- #15
	DO .2 <- ,61000SUB#1 DO NOTE .2 <- LOC
	DO (40004) NEXT DO NOTE .3 <- #15 LT LOC
	DO ,61000SUB#10 <- .3 DO NOTE IF LOC GE #15, DFLAG <- #1
	DO (101) NEXT
	PLEASE STOP

	PLEASE WHEN WE ENCOUNTER THE FIRST DWARF, WE KILL 0, 1, OR 2 OF THE 5
	       DWARVES.  IF ANY OF THE SURVIVORS IS AT LOC, REPLACE HIM WITH
	       THE ALTERNATE.
(31)	DO FORGET #1
	DO .1 <- ,61000SUB#10 DO NOTE .1 <- DFLAG
	DO .2 <- #1
	DO (40003) NEXT DO NOTE .3 <- .1 NE .2
	DO .1 <- .3
	DO .2 <- #1
	DO (41) NEXT DO NOTE IF DFLAG NE #1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #15
	DO (40004) NEXT DO NOTE .3 <- LOC LT #15
	DO .1 <- .3
	DO .2 <- #1
	DO (100) NEXT DO NOTE IF LOC LT #15
	DO %95 (101) NEXT
	DO NOTE MAYBE KILL RAND0M DWARF
	DO ,61000SUB#10 <- #2 DO NOTE DFLAG <- #2
	DO (1900) NEXT
	DO .2 <- #5
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2
	DO .1 <- .3
	DO (1020) NEXT
	DO %50 ,61002SUB.1#1 <- #0 DO NOTE DLOC(.1) <- #0
	DO NOTE MAYBE KILL ANOTHER RAND0M DWARF
	DO (1900) NEXT
	DO .2 <- #5
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2
	DO .1 <- .3
	DO (1020) NEXT
	DO %50 ,61002SUB.1#1 <- #0 DO NOTE DLOC(.1) <- #0
	DO .1 <- #3
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- #28 DO .2 <- ,61000SUB#1 DO (30001) NEXT DO NOTE MOVE AXE TO LOC
	DO NOTE MOVE DWARVES AT LOC TO #18
	DO .3 <- #1 DO (32) NEXT
	DO .3 <- #2 DO (32) NEXT
	DO .3 <- #3 DO (32) NEXT
	DO .3 <- #4 DO (32) NEXT
	DO .3 <- #5 DO (32) NEXT
	DO NOT [DWARF 1.LOC] ,61002SUB#1#1
	DO NOT [DWARF 2.LOC] ,61002SUB#2#1
	DO NOT [DWARF 3.LOC] ,61002SUB#3#1
	DO NOT [DWARF 4.LOC] ,61002SUB#4#1
	DO NOT [DWARF 5.LOC] ,61002SUB#5#1
	DO NOT [DWARF 6.LOC] ,61002SUB#6#1
	DO (101) NEXT
	PLEASE STOP
(32)	DO .1 <- ,61002SUB.3#1 DO NOTE .1 <- DLOC(.3).
	DO (40011) NEXT DO NOTE .1=.2 DLOC(.3) EQ LOC
	DO ,61002SUB.3#1 <- #18 DO NOTE DLOC(.3) <- #18
	DO RESUME #1
	PLEASE STOP

	PLEASE THINGS ARE IN FULL SWING.  MOVE EACH DWARF AT RANDOM, EXCEPT IF
	       HE'S SEEN US HE STICKS WITH US.  DWARVES NEVER GO TO LOCS <15.
	       IF WANDERING AT RANDOM, THEY DON'T BACK UP UNLESS THERE'S NO
	       ALTERNATIVE.  IF THEY DON'T HAVE TO MOVE, THEY ATTACK.  AND, OF
	       COURSE, DEAD DWARVES DON'T DO MUCH OF ANYTHING.
(41)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .5 <- #0 DO NOTE DTOTAL
	DO .6 <- #0 DO NOTE ATTACK
	DO .7 <- #0 DO NOTE STICK
	DO .8 <- #1
	DO (42) NEXT
	PLEASE STOP
	PLEASE FOR .8 IN #1 .. #6
(42)	DO FORGET #1
	DO .1 <- ,61002SUB.8#1 DO NOTE .1 <- DLOC(.8).
	DO .2 <- #0
	DO (51) NEXT DO NOTE IF DEAD
	DO .9 <- ,60013SUB.1 DO NOTE .9 <- KEY(DLOC(.8)).
	DO .10 <- #1
	DO .1 <- .9
	DO .2 <- #0
	DO (47) NEXT DO NOTE IF KEY(DLOC(.8)) EQ #0
	DO (43) NEXT
	PLEASE STOP
(43)	DO FORGET #1
	DO .1 <- ,60003SUB.9#1 DO NOTE .1 <- TRAVEL(.9).SRC
	DO .2 <- ,61002SUB.8#1 DO NOTE .2 <- DLOC(.8).
	DO (40003) NEXT DO NOTE .3 <- .1 NE .2
	DO .1 <- .3
	DO .2 <- #1
	DO (47) NEXT DO NOTE IF TRAVEL(.9).SRC NE DLOC(.8).
	DO ,61005SUB.10 <- ',60003SUB.9#2'~#255 DO NOTE TK(.10) <- TRAVEL.DEST
	DO .1 <- ',60003SUB.9#2'~#65280 DO NOTE .1 <- TRAVEL.DEST.TAG
	DO .2 <- #1
	DO (40003) NEXT DO NOTE .3 <- .1 NE .2
	DO .1 <- .3
	DO .2 <- #1
	DO (45) NEXT DO NOTE IF TRAVEL(.9).DEST.TAG NE #1
	DO .1 <- ',60003SUB.9#2'~#255 DO NOTE .1 <- TRAVEL.DEST
	DO .2 <- #15
	DO (40004) NEXT DO NOTE .3 <- .1 LT .2
	DO .1 <- .3
	DO .2 <- #1
	DO (45) NEXT DO NOTE IF TRAVEL(.9).DEST LT #15
	DO .1 <- ',60003SUB.9#2'~#255 DO NOTE .1 <- TRAVEL.DEST
	DO .2 <- ,61002SUB.8#2 DO NOTE .2 <- ODLOC(.8).
	DO (45) NEXT DO NOTE IF TRAVEL(.9).DEST EQ ODLOC
	DO (44) NEXT DO NOTE IF .10 GT #1 AND TK(.10) EQ TK(.10-#1).
	DO .1 <- ',60003SUB.9#2'~#255 DO NOTE .1 <- TRAVEL.DEST
	DO .2 <- ,61002SUB.8#1 DO NOTE .2 <- DLOC(.8).
	DO (45) NEXT DO NOTE IF TRAVEL(.9).DEST EQ DLOC
	DO (30151) NEXT DO NOTE .1 <- FORCED(TRAVEL.DEST).
	DO .2 <- #1
	DO (45) NEXT DO NOTE IF FORCED(TRAVEL(.9).DEST).
	DO .1 <- .8
	DO .2 <- #6
	DO (40002) NEXT DO NOTE .3 <- .8 EQ PIRATE
	DO .4 <- .3
	DO .3 <- #1
	DO .1 <- ',60009SUB"',60003SUB.9#2'~#255"'~#8 DO NOTE .1 <- COND(TRAVEL.DEST).NOPIRATE
	DO .2 <- #1
	DO (40002) NEXT DO NOTE .3 <- .8 EQ TRAVEL.DEST.NOPIRATE
	DO .1 <- .3
	DO .2 <- .4
	DO (40006) NEXT DO NOTE .3 <- (.8 EQ PIRATE) AND COND(TRAVEL.DEST).NOPIRATE 
	DO .1 <- .3
	DO .2 <- #1
	DO (45) NEXT DO NOTE IF PIRATE AND PIRATE OFF LIMITS FLAG
	DO .1 <- ',60003SUB.9#3'~#65280 DO NOTE .1 <- TRAVEL.DEST.TAG
	DO .2 <- #2
	DO (40002) NEXT DO NOTE .3 <- TRAVEL.COND.TAG EQ #2 (FORBIDDEN TO DWARVES).
	DO .1 <- .3
	DO .2 <- #1
	DO (45) NEXT DO NOTE IF TRAVEL IS FORBIDDEN TO DWARVES
	DO .1 <- .10
	DO (1020) NEXT
	DO .10 <- .1 DO NOTE .10 <- .10+#1
	DO (46) NEXT
	PLEASE STOP
(44)	DO .1 <- .10
	DO .2 <- #1
	DO (40003) NEXT DO NOTE .3 <- .10 NE #1
	DO .1 <- .3
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 .10 NE #1
	DO .1 <- .10
	DO .2 <- #1
	DO (1010) NEXT
	DO .1 <- ,61005SUB.3 DO NOTE .1 <- TK(.10-#1).
	DO .2 <- ,61005SUB.10 DO NOTE .2 <- TK(.10).
	DO (40011) NEXT DO NOTE .1=.2 TK(.10-#1) EQ TK(.10).
	DO FORGET #1
	DO (46) NEXT
	PLEASE STOP
(45)	DO (40011) NEXT DO NOTE .1=.2
	DO NOT STOP, FALLTHROUGH
(46)	DO FORGET #1
	DO .1 <- .9
	DO (1020) NEXT
	DO .9 <- .1
	DO (43) NEXT
	PLEASE STOP
(47)	DO (40011) NEXT DO NOTE .1=.2 LAST .9
	DO FORGET #1
	DO ,61005SUB.10 <- ,61002SUB.8#2 DO NOTE TK(.10) <- ODLOC(.8).
	DO .1 <- .10
	DO .2 <- #1
	DO (40003) NEXT DO NOTE .3 <- .10 NE #1
	DO .2 <- .3
	DO (1010) NEXT DO NOTE IF .10 EQ #1, .3 <- #1, ELSE .3 <- .10-#1
	DO (1900) NEXT
	DO .2 <- .3
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2, I.E. .3 <- RAND(.3)
	DO .1 <- .3
	DO (1020) NEXT DO NOTE .1 <- RAND+#1
	DO ,61002SUB.8#2 <- ,61002SUB.8#1 DO NOTE ODLOC(.8) <- DLOC(.8).
	DO ,61002SUB.8#1 <- ,61005SUB.1 DO NOTE DLOC(.8) <- TK(RAND).

	DO .1 <- #15
	DO .2 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO (40004) NEXT DO NOTE .3 <- LOC GE #15
	DO .1 <- ,61002SUB.8#3 DO NOTE .1 <- DSEEN(.8).
	DO .2 <- #1
	DO .4 <- #0
	DO (40021) NEXT DO NOTE .1 <- (LOC GE #15) AND DSEEN(.8).
	DO .4 <- .1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- ,61002SUB.8#1 DO NOTE .2 <- DLOC(.8).
	DO .3 <- #1
	DO (40021) NEXT DO NOTE .1 <- ((LOC GE #15) AND DSEEN(.8)) OR DLOC(.8) EQ LOC 
	DO .4 <- .1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- ,61002SUB.8#2 DO NOTE .2 <- ODLOC(.8).
	DO .3 <- #1
	DO (40021) NEXT DO NOTE .1 <- ((LOC GE #15) AND DSEEN(.8)) OR DLOC(.8) EQ LOC  OR ODLOC(.8) EQ LOC
	DO ,61002SUB.8#3 <- .1 DO NOTE DSEEN(.8) <- ((LOC GE #15) AND DSEEN(.8)) OR DLOC(.8) EQ LOC OR ODLOC(.8) EQ LOC
	DO .2 <- #0
	DO (51) NEXT DO NOTE IF NOT DSEEN(.8)
	DO ,61002SUB.8#1 <- ,61000SUB#1 DO NOTE DLOC(.8) <- LOC
	DO .1 <- .8
	DO .2 <- #6
	DO (71) NEXT DO NOTE IF PIRATE
	DO (61) NEXT DO NOTE DWARF
	PLEASE STOP

(51)	DO (40011) NEXT DO NOTE .1=.2 NEXT .8
	DO NOT STOP, FALLTHROUGH
(52)	DO FORGET #1
	DO .1 <- .8
	DO .2 <- #6
	DO (90) NEXT DO NOTE IF LAST .8
	DO (1020) NEXT
	DO .8 <- .1
	DO (42) NEXT DO NOTE NEXT .8
	PLEASE STOP

	PLEASE THIS THREATENING LITTLE DWARF IS IN THE ROOM WITH HIM!
(61)	DO FORGET #1
	DO .1 <- .5
	DO (1020) NEXT
	DO .5 <- .1 DO NOTE DTOTAL <- DTOTAL + #1
	DO .1 <- ,61002SUB.8#1 DO NOTE .1 <- DLOC(.8).
	DO .2 <- ,61002SUB.8#2 DO NOTE .2 <- ODLOC(.8).
	DO (40003) NEXT DO NOTE .3 <- DLOC(.8) NE ODLOC(.8).
	DO .1 <- .3
	DO .2 <- #1
	DO (51) NEXT DO NOTE IF DLOC(.8) NE ODLOC(.8), NEXT .8
	DO .1 <- .6
	DO (1020) NEXT
	DO .6 <- .1 DO NOTE ATTACK <- ATTACK + #1
	DO .1 <- ,61000SUB#7 DO NOTE .1 <- KNFLOC	
	DO .2 <- #65535
	DO .3 <- #65535
	DO .4 <- ,61000SUB#1 DO NOTE .4 <- LOC
	DO (40021) NEXT DO NOTE IF KNFLOC NE #65535, KNFLOC <- LOC
	DO .1 <- ,61000SUB#10 DO NOTE .1 <- DFLAG
	DO .2 <- #0 DO (51) NEXT DO NOTE IF DFLAG EQ #0
	DO .2 <- #1 DO (51) NEXT DO NOTE IF DFLAG EQ #1
	DO .2 <- #2 DO (51) NEXT DO NOTE IF DFLAG EQ #2
	DO (1010) NEXT
	DO .1 <- .3
	DO .2 <- #95
	DO (1030) NEXT
	DO .1 <- .3
	DO .2 <- #100
	DO (1040) NEXT
	DO .4 <- .3 DO NOTE .4 <- (DFLAG - #2)*#95/#100
	DO (1900) NEXT
	DO .2 <- #1000
	DO (40001) NEXT DO NOTE .3 <- RAND(#1000).
	DO .1 <- .3
	DO .2 <- .4
	DO (40004) NEXT DO NOTE .3 <- RAND(#1000) LT (DFLAG - #2)*#95/#100
	DO .1 <- .3
	DO .2 <- #0
	DO (51) NEXT DO NOTE IF .3 <- RAND(#1000) GE (DFLAG - #2)*#95/#100
	DO .1 <- .7
	DO (1020) NEXT
	DO .7 <- .1 DO NOTE STICK <- STICK + #1
	DO (52) NEXT DO NOTE NEXT .8
	PLEASE STOP

	PLEASE THE PIRATE'S SPOTTED HIM.  HE LEAVES HIM ALONE ONCE WE'VE FOUND
	       CHEST.  COUNTS IF A TREASURE IS HERE.  IF NOT, AND TALLY=TALLY2
	       PLUS ONE FOR AN UNSEEN CHEST, LET THE PIRATE BE SPOTTED.  USE
	       PLACE(MESSAG) TO DETERMINE IF PIRATE'S BEEN SEEN, SINCE
	       PLACE(CHEST)=0 COULD MEAN HE THREW IT TO TROLL.
(71)	DO (40011) NEXT DO NOTE .1=.2 PIRATE
	DO FORGET #1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #114 DO NOTE .2 <- CHLOC
	DO (90) NEXT DO NOTE IF LOC EQ CHLOC
	DO .1 <- ,61003SUB#55#5 DO NOTE .1 <- CHEST.PROPNEG
	DO .2 <- #0
	DO (90) NEXT DO NOTE IF NOT CHEST.PROPNEG
	DO .8 <- #50
	DO .9 <- #0
	DO (72) NEXT
	PLEASE STOP
	PLEASE FOR .8 IN #50 .. #64
(72)	DO FORGET #1
	DO (73) NEXT DO NOTE IF TOTING(.8) AND (.8 NE PYRAMID OR LOC NE PLOVER) AND (.8 NE EMERALD OR LOC NE DARK ROOM)
	DO .1 <- .8 DO (30111) NEXT DO NOTE .1 <- HERE(.8).
	DO .2 <- #1
	DO .3 <- #1
	DO .4 <- .9
	DO (40021) NEXT DO NOTE IF HERE(.8), .1 <- #1, ELSE .1 <- .9
	DO .9 <- .1 DO NOTE IF HERE(.8), .9 <- #1
	DO .1 <- .8
	DO .2 <- #64
	DO (75) NEXT DO NOTE IF .8 EQ #64
	DO (1020) NEXT
	DO .8 <- .1
	DO (72) NEXT
	PLEASE STOP
(73)	DO .1 <- .8
	DO (30101) NEXT DO NOTE .1 <- TOTING(.8).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 TOTING(.8).
	DO .1 <- .8
	DO .2 <- #60
	DO (40002) NEXT DO NOTE .3 <- .8 EQ PYRAMID
	DO .4 <- #0
	DO .1 <- ,61000SUB#1
	DO .2 <- #100
	DO (40021) NEXT DO NOTE .3 <- .8 EQ PYRAMID AND LOC EQ PLOVER
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 .8 NE PYRAMID OR LOC NE PLOVER
	DO .1 <- .8
	DO .2 <- #59
	DO (40002) NEXT DO NOTE .3 <- .8 EQ EMERALD
	DO .4 <- #0
	DO .1 <- ,61000SUB#1
	DO .2 <- #101
	DO (40021) NEXT DO NOTE .3 <- .8 EQ EMERALD AND LOC EQ DARK ROOM
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 .8 NE EMERALD OR LOC NE DARK ROOM
	DO FORGET #1
	DO .1 <- #128
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- ,61003SUB#36#1 DO NOTE .1 <- MESSAG.LOC
	DO .2 <- #0
	DO (74) NEXT DO NOTE IF MESSAG.LOC EQ #0, MOVE CHEST TO CHLOC
	DO .1 <- #36
	DO .2 <- #140
	DO (30001) NEXT DO NOTE MOVE MESSAGE(#36) TO CHLOC2(#140).
	DO .8 <- #50
	DO (81) NEXT
	PLEASE STOP
(74)	DO (40011) NEXT DO NOTE .1=.2 MESSAG.LOC EQ #0
	DO .1 <- #55
	DO .2 <- #114
	DO (30001) NEXT DO NOTE MOVE CHEST TO CHLOC
	DO RESUME #1
	PLEASE STOP
(75)	DO (40011) NEXT DO NOTE .1=.2 END ITERATION FROM (72) OVER TREASURES
	DO FORGET #1
	DO (76) NEXT DO NOTE 
	DO .1 <- ,61002SUB#6#1 DO NOTE .1 <- DLOC(#6)
	DO .2 <- ,61002SUB#6#2 DO NOTE .2 <- ODLOC(#6)
	DO (90) NEXT DO NOTE IF DLOC(#6) EQ ODLOC(#6)
	DO .1 <- #127
	DO %20 (55601) NEXT DO NOTE RSPEAK
	DO (91) NEXT
	PLEASE STOP
(76)	DO .1 <- ,61000SUB#30 DO NOTE .1 <- TALLY2
	DO (1020) NEXT
	DO .2 <- ,61000SUB#29 DO NOTE .2 <- TALLY
	DO (40011) NEXT DO NOTE .1=.2 TALLY EQ TALLY2 + #1
	DO .1 <- .9
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 NO TREASURES HERE
	DO .1 <- #2
	DO (30101) NEXT DO NOTE .1 <- HERE(LAMP).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 HERE(LAMP).
	DO .1 <- ,61003SUB#2#4 DO NOTE .1 <- LAMP.PROP
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 LAMP.PROP EQ #1
	DO FORGET #1
	DO .1 <- #186
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- #55
	DO .2 <- #114
	DO (30001) NEXT DO NOTE MOVE CHEST TO CHLOC
	DO .1 <- #36
	DO .2 <- #140
	DO (30001) NEXT DO NOTE MOVE MESSAGE(#36) TO CHLOC2(#140).
	DO ,61002SUB#6#1 <- #114 DO NOTE DLOC(#6) <- CHLOC
	DO ,61002SUB#6#2 <- #114 DO NOTE ODLOC(#6) <- CHLOC
	DO ,61002SUB#6#3 <- #0 DO NOTE DSEEN(#6) <- #0
	DO (91) NEXT
	PLEASE STOP
	PLEASE FOR .8 IN #50 .. #64
(81)	DO FORGET #1
	DO (82) NEXT DO NOTE HANDLE CURRENT TREASURE
	DO .1 <- .8
	DO .2 <- #64
	DO (83) NEXT DO NOTE IF LAST TREASURE
	DO (1020) NEXT
	DO .8 <- .1
	DO (81) NEXT
	PLEASE STOP
(82)	DO .1 <- .8
	DO .2 <- #60
	DO (40002) NEXT DO NOTE .3 <- .8 EQ PYRAMID
	DO .4 <- #0
	DO .1 <- ,61000SUB#1
	DO .2 <- #100
	DO (40021) NEXT DO NOTE .3 <- .8 EQ PYRAMID AND LOC EQ PLOVER
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 .8 NE PYRAMID OR LOC NE PLOVER
	DO .1 <- .8
	DO .2 <- #59
	DO (40002) NEXT DO NOTE .3 <- .8 EQ EMERALD
	DO .4 <- #0
	DO .1 <- ,61000SUB#1
	DO .2 <- #101
	DO (40021) NEXT DO NOTE .3 <- .8 EQ EMERALD AND LOC EQ DARK ROOM
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 .8 NE EMERALD OR LOC NE DARK ROOM
	DO .1 <- ,60007SUB.8#2 DO NOTE .1 <- TREASURE.FIXD
	DO .2 <- #65535
	DO (40002) NEXT DO NOTE .3 <- TREASURE.FIXD EQ #65535
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 TREASURE.FIXD NE #65535
	DO .1 <- .8
	DO (30101) NEXT DO NOTE .1 <- HERE(TREASURE).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 HERE(TREASURE).
	DO .1 <- .8
	DO .2 <- #114
	DO (30001) NEXT DO NOTE MOVE TREASURE TO CHLOC
	DO RESUME #1
	PLEASE STOP
(83)	DO (40011) NEXT DO NOTE .1=.2 LAST TREASURE
	DO FORGET #1
	DO ,61002SUB#6#1 <- #114 DO NOTE DLOC(#6) <- CHLOC
	DO ,61002SUB#6#2 <- #114 DO NOTE ODLOC(#6) <- CHLOC
	DO ,61002SUB#6#3 <- #0 DO NOTE DSEEN(#6) <- #0
	DO (91) NEXT DO NOTE LAST DWARF
	PLEASE STOP

	PLEASE NOW WE KNOW WHAT'S HAPPENING.  LET'S TELL THE POOR SUCKER ABOUT
	       IT.
(90)	DO (40011) NEXT DO NOTE .1=.2
	DO NOT STOP, FALLTHROUGH
(91)	DO FORGET #1
	DO NOT [LOC] ,61000SUB#1
	DO NOT [DWARF 1.LOC] ,61002SUB#1#1
	DO NOT [DWARF 2.LOC] ,61002SUB#2#1
	DO NOT [DWARF 3.LOC] ,61002SUB#3#1
	DO NOT [DWARF 4.LOC] ,61002SUB#4#1
	DO NOT [DWARF 5.LOC] ,61002SUB#5#1
	DO NOT [DWARF 6.LOC] ,61002SUB#6#1
	DO NOT [DTOTAL] .5
	DO NOT [ATTACK] .6
	DO NOT [STICK] .7
	DO .1 <- .5 DO NOTE .1 <- DTOTAL
	DO .2 <- #0
	DO (100) NEXT DO NOTE IF DTOTAL EQ #0
	DO .2 <- #1
	DO (92) NEXT DO NOTE IF DTOTAL EQ #1
	DO,60006SUB#13#2<-#84  DO,60006SUB#13#3<-#72  DO,60006SUB#13#4<-#69
	DO,60006SUB#13#5<-#82  DO,60006SUB#13#6<-#69  DO,60006SUB#13#7<-#32
	DO,60006SUB#13#8<-#65  DO,60006SUB#13#9<-#82  DO,60006SUB#13#10<-#69
	DO,60006SUB#13#11<-#32 DO,60006SUB#13#12<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE: "THERE ARE "
	DO (57201) NEXT DO NOTE FORMAT INTEGER: I1 DTOTAL
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#84  DO,60006SUB#13#4<-#72
	DO,60006SUB#13#5<-#82  DO,60006SUB#13#6<-#69  DO,60006SUB#13#7<-#65
	DO,60006SUB#13#8<-#84  DO,60006SUB#13#9<-#69  DO,60006SUB#13#10<-#78
	DO,60006SUB#13#11<-#73 DO,60006SUB#13#12<-#78 DO,60006SUB#13#13<-#71
	DO,60006SUB#13#14<-#32 DO,60006SUB#13#15<-#76 DO,60006SUB#13#16<-#73
	DO,60006SUB#13#17<-#84 DO,60006SUB#13#18<-#84 DO,60006SUB#13#19<-#76
	DO,60006SUB#13#20<-#69 DO,60006SUB#13#21<-#32 DO,60006SUB#13#22<-#68
	DO,60006SUB#13#23<-#87 DO,60006SUB#13#24<-#65 DO,60006SUB#13#25<-#82
	DO,60006SUB#13#26<-#86 DO,60006SUB#13#27<-#69 DO,60006SUB#13#28<-#83
	DO,60006SUB#13#29<-#32 DO,60006SUB#13#30<-#73 DO,60006SUB#13#31<-#78
	DO,60006SUB#13#32<-#32 DO,60006SUB#13#33<-#84 DO,60006SUB#13#34<-#72
	DO,60006SUB#13#35<-#69 DO,60006SUB#13#36<-#32 DO,60006SUB#13#37<-#82
	DO,60006SUB#13#38<-#79 DO,60006SUB#13#39<-#79 DO,60006SUB#13#40<-#77
	DO,60006SUB#13#41<-#32 DO,60006SUB#13#42<-#87 DO,60006SUB#13#43<-#73
	DO,60006SUB#13#44<-#84 DO,60006SUB#13#45<-#72 DO,60006SUB#13#46<-#32
	DO,60006SUB#13#47<-#89 DO,60006SUB#13#48<-#79 DO,60006SUB#13#49<-#85
	DO,60006SUB#13#50<-#46 DO,60006SUB#13#51<-#10 DO,60006SUB#13#52<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE: " THREATENING LITTLE DWARVES IN THE ROOM WITH YOU."
	DO (93) NEXT
	PLEASE STOP
(92)	DO (40011) NEXT DO NOTE .1=.2 DTOTAL EQ #1
	DO FORGET #1
	DO .1 <- #4
	DO (55601) NEXT DO NOTE RSPEAK
	DO (93) NEXT
	PLEASE STOP
(93)	DO FORGET #1
	DO .1 <- .6 DO NOTE .1 <- ATTACK
	DO .2 <- #0
	DO (100) NEXT DO NOTE IF ATTACK EQ #0
	DO .1 <- ,61000SUB#10 DO NOTE .1 <- DFLAG
	DO .2 <- #2
	DO .3 <- #3
	DO .4 <- ,61000SUB#10 DO NOTE .4 <- DFLAG
	DO (40021) NEXT DO NOTE IF DFLAG EQ #2, DFLAG <- #3
	DO .1 <- .6 DO NOTE .1 <- ATTACK
	DO .2 <- #1
	DO (94) NEXT DO NOTE IF ATTACK EQ #1
	DO (57201) NEXT DO NOTE FORMAT INTEGER: I1 ATTACK
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#79  DO,60006SUB#13#4<-#70
	DO,60006SUB#13#5<-#32  DO,60006SUB#13#6<-#84  DO,60006SUB#13#7<-#72
	DO,60006SUB#13#8<-#69  DO,60006SUB#13#9<-#77  DO,60006SUB#13#10<-#32
	DO,60006SUB#13#11<-#84 DO,60006SUB#13#12<-#72 DO,60006SUB#13#13<-#82
	DO,60006SUB#13#14<-#79 DO,60006SUB#13#15<-#87 DO,60006SUB#13#16<-#32
	DO,60006SUB#13#17<-#75 DO,60006SUB#13#18<-#78 DO,60006SUB#13#19<-#73
	DO,60006SUB#13#20<-#86 DO,60006SUB#13#21<-#69 DO,60006SUB#13#22<-#83
	DO,60006SUB#13#23<-#32 DO,60006SUB#13#24<-#65 DO,60006SUB#13#25<-#84
	DO,60006SUB#13#26<-#32 DO,60006SUB#13#27<-#89 DO,60006SUB#13#28<-#79
	DO,60006SUB#13#29<-#85 DO,60006SUB#13#30<-#33 DO,60006SUB#13#31<-#10
	DO,60006SUB#13#32<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE: " OF THEM THROW KNIVES AT YOU!"
	DO .3 <- #6 DO NOTE MULTIPLE KNIVES MISS/HIT MESSAGE
	DO (95) NEXT
	PLEASE STOP
(94)	DO (40011) NEXT DO NOTE .1=.2 ATTACK EQ #1
	DO FORGET #1
	DO .1 <- #5
	DO (55601) NEXT DO NOTE RSPEAK
	DO .3 <- #52 DO NOTE ONE KNIFE MISS/HIT MESSAGE
	DO (95) NEXT
	PLEASE STOP
(95)	DO FORGET #1
	DO .1 <- .7 DO NOTE .1 <- STICK
	DO .2 <- #0
	DO (99) NEXT DO NOTE IF STICK EQ #0
	DO .2 <- #1
	DO (96) NEXT DO NOTE IF STICK EQ #0
	DO (57201) NEXT DO NOTE FORMAT INTEGER: I1 STICK
	DO (57201) NEXT DO NOTE FORMAT INTEGER: I1 ATTACK
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#79  DO,60006SUB#13#4<-#70
	DO,60006SUB#13#5<-#32  DO,60006SUB#13#6<-#84  DO,60006SUB#13#7<-#72
	DO,60006SUB#13#8<-#69  DO,60006SUB#13#9<-#77  DO,60006SUB#13#10<-#32
	DO,60006SUB#13#11<-#71 DO,60006SUB#13#12<-#69 DO,60006SUB#13#13<-#84
	DO,60006SUB#13#14<-#32 DO,60006SUB#13#15<-#89 DO,60006SUB#13#16<-#79
	DO,60006SUB#13#17<-#85 DO,60006SUB#13#18<-#33 DO,60006SUB#13#19<-#10
	DO,60006SUB#13#20<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE: " OF THEM GET YOU!"
	DO ,61000SUB#18 <- ,61000SUB#1 DO NOTE OLDLC2 <- LOC
	DO (10001) NEXT
	PLEASE STOP
(96)	DO (40011) NEXT DO NOTE .1=.2 STICK EQ #1
	DO FORGET #1
	DO .1 <- .3
	DO (1020) NEXT
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,61000SUB#18 <- ,61000SUB#1 DO NOTE OLDLC2 <- LOC
	DO (10001) NEXT
	PLEASE STOP
(99)	DO (40011) NEXT DO NOTE .1=.2 STICK EQ #0
	DO FORGET #1
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO (101) NEXT
	PLEASE STOP

	PLEASE DESCRIBE THE CURRENT LOCATION AND (MAYBE) GET NEXT COMMAND.
(100)	DO (40011) NEXT DO NOTE .1=.2
	DO NOT STOP, FALLTHROUGH
(101)	DO FORGET #1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #0
	DO (102) NEXT
	DO (103) NEXT
	PLEASE STOP
(102)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (10001) NEXT
	PLEASE STOP

	PLEASE IF NOT FORCED(LOC) AND DARK(),
	          IF WZDARK, %35 GOTO "YOU FELL INTO A PIT"
	          IF TOTING(BEAR), RSPEAK(#141)
	          RSPEAK(#16)
	          IF LOC=#33 AND NOT CLOSNG, RSPEAK(#8).
	          GOTO (241).
(103)	DO FORGET #1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO (30151) NEXT DO NOTE .1 <- FORCED(LOC).
	DO .2 <- '∀.1¢#1'~#1 DO NOTE .2 <- NOT FORCED(LOC).
	DO (30161) NEXT DO NOTE .1 <- DARK().
	DO .1 <- '&.1¢.2' DO NOTE .1 <- NOT FORCED(LOC) AND DARK().
	DO .2 <- #1
	DO (104) NEXT
	DO (111) NEXT
	PLEASE STOP
(104)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE IF WZDARK %35 GOTO "YOU FELL INTO A PIT"
	DO .1 <- ,61000SUB#20 DO NOTE .1 <- WZDARK
	DO .2 <- #1
	DO (105) NEXT
	DO (106) NEXT
	PLEASE STOP
(105)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO %65 (106) NEXT
	DO .1 <- #23
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,61000SUB#18 <- ,61000SUB#1 DO NOTE OLDLC2 <- LOC
	DO (10001) NEXT
	PLEASE STOP
(106)	DO FORGET #1
	DO (121) NEXT
	DO .1 <- #16
	DO (55601) NEXT DO NOTE RSPEAK
	DO (131) NEXT
	DO (241) NEXT
	PLEASE STOP

	PLEASE IF TOTING(BEAR), RSPEAK(#141)
	       IF ABB(LOC) MOD ABBNUM = 0, SPEAK(LTEXT(LOC))
	       ELSE SPEAK(STEXT(LOC))
	       IF LOC=#33 AND NOT CLOSNG, RSPEAK(#8).
	       IF FORCED(LOC), GOTO (501) WITH MOTION VERB <- #1
(111)	DO FORGET #1
	DO (121) NEXT
	DO .1 <- ,61001SUB",61000SUB#1"#2 DO NOTE .1 <- ABB(LOC).
	DO .2 <- ,61000SUB#9 DO NOTE .2 <- ABBNUM
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2
	DO .1 <- .3
	DO .2 <- #0
	DO (112) NEXT
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO (55201) NEXT DO NOTE SPEAK(STEXT(LOC)).
	DO (113) NEXT
	PLEASE STOP
(112)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO (55101) NEXT DO NOTE SPEAK(LTEXT(LOC)).
	DO (113) NEXT
	PLEASE STOP
(113)	DO FORGET #1
	DO (131) NEXT
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO (30151) NEXT DO NOTE .1 <- FORCED(LOC).
	DO .2 <- #1
	DO (114) NEXT
	DO (201) NEXT
	PLEASE STOP
(114) 	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #1
	DO (501) NEXT
	PLEASE STOP

	PLEASE IF TOTING #35 (BEAR), RSPEAK(#141).
(121)	DO .1 <- #35
	DO (30101) NEXT DO NOTE .1 <- TOTING(BEAR).
	DO .2 <- #1
	DO (122) NEXT
	DO RESUME #1
	PLEASE STOP
(122)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #141
	DO (55601) NEXT DO NOTE RSPEAK
	DO RESUME #1
	PLEASE STOP

	PLEASE IF LOC=#33 AND NOT CLOSNG, %25 RSPEAK(#8).
(131)	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #33
	DO (132) NEXT
	DO RESUME #1
	PLEASE STOP
(132)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ,61000SUB#19 DO NOTE .1 <- CLOSNG
	DO .2 <- #0
	DO (133) NEXT
	DO RESUME #1
	PLEASE STOP
(133)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #8
	DO %25 (55601) NEXT DO NOTE RSPEAK
	DO RESUME #1
	PLEASE STOP

	PLEASE PRINT OUT DESCRIPTIONS OF OBJECTS AT THIS LOCATION.  IF NOT
	       CLOSING AND PROPERTY VALUE IS NEGATIVE, TALLY OFF ANOTHER
	       TREASURE.  RUG IS SPECIAL CASE; ONCE SEEN, ITS PROP IS 1 (DRAGON
	       ON IT) TILL DRAGON IS KILLED.  SIMILARLY FOR CHAIN; PROP IS
	       INITIALLY 1 (LOCKED TO BEAR).  THESE HACKS ARE BECAUSE PROP=0 IS
	       NEEDED TO GET FULL SCORE.
(201)	DO FORGET #1
	DO .1 <- ,61001SUB",61000SUB#1"#2 DO NOTE .1 <- ABB(LOC).
	DO (1020) NEXT
	DO ,61001SUB",61000SUB#1"#2 <- .1 DO NOTE ABB(LOC) <- .1
	DO NOTE ITERATE OVER OBJECTS AT LOC
	DO NOT [LOC] ,61000SUB#1
	DO .4 <- ,61001SUB',61000SUB#1'#1 DO NOTE .4 <- LOC.FIRSTOBJ
	DO (202) NEXT
	PLEASE STOP
(202)	DO FORGET #1
	DO NOT [IOBJ] .4
	DO .1 <- .4
	DO .2 <- #0
	DO (204) NEXT DO NOTE CHECK IF LAST OBJ
	DO .2 <- #7
	DO (208) NEXT DO NOTE CHECK IF OBJ=STEPS (AND TOTING NUGGET).
	DO .1 <- ,61003SUB.4#5 DO NOTE .1 <- OBJ.PROPNEGATIVE
	DO .2 <- #1
	DO (206) NEXT DO NOTE CHECK OBJ.PROPNEGATIVE
	DO .1 <- ,61003SUB.4#4 DO NOTE .1 <- OBJ.PROP
	DO (1020) NEXT
	DO (1020) NEXT
	DO .2 <- .1
	DO .1 <- .4
	DO (55501) NEXT DO NOTE PSPEAK
	DO (203) NEXT
	PLEASE STOP
(203)	DO FORGET #1
	DO .4 <- ,61003SUB.4#2 DO NOTE OBJ <- OBJ.NEXTOBJ
	DO (202) NEXT
	PLEASE STOP
(204)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (241) NEXT
	PLEASE STOP
(206)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- ,61000SUB#26 DO NOTE .1 <- CLOSED
	DO .2 <- #1
	DO (209) NEXT DO NOTE SKIP UNSEEN TREASURES IF CLOSED
	DO ,61003SUB.4#4 <- #0 DO NOTE OBJ.PROP <- #0
	DO ,61003SUB.4#5 <- #0 DO NOTE OBJ.PROPNEGATIVE <- #0
	DO .1 <- .4 DO .2 <- #62 DO (207) NEXT DO NOTE IF OBJ EQ RUG, OBJ.PROP <- #1
	DO .1 <- .4 DO .2 <- #64 DO (207) NEXT DO NOTE IF OBJ EQ CHAIN, OBJ.PROP <- #1
	DO .1 <- ,61000SUB#29 DO NOTE .1 <- TALLY
	DO .2 <- #1
	DO (1010) NEXT DO NOTE .3 <- TALLY-#1
	DO ,61000SUB#29 <- .3 DO NOTE TALLY <- TALLY-#1
	DO NOTE IF REMAINING TREASURES TOO ELUSIVE, ZAP HIS LAMP.
	DO .1 <- .3
	DO .2 <- #0
	DO (40002) NEXT DO NOTE .3 <- TALLY EQ #0
	DO .1 <- .3
	DO (40011) NEXT DO NOTE .1=.2 TALLY NE #0
	DO .1 <- ,61000SUB#29 DO NOTE .1 <- TALLY
	DO .2 <- ,61000SUB#30 DO NOTE .2 <- TALLY2
	DO (40011) NEXT DO NOTE .1=.2 TALLY EQ TALLY2
	DO .1 <- ,61000SUB#5 DO NOTE .1 <- LIMIT
	DO .2 <- #35
	DO (40012) NEXT DO NOTE .1>=.2 LIMIT GE #35
	DO ,61000SUB#5 <- #35 DO NOTE LIMIT <- #35
	DO RESUME #1
	PLEASE STOP
(207)	DO (40011) NEXT DO NOTE .1=.2
	DO ,61003SUB.4#4 <- #1 DO NOTE OBJ.PROP <- #1
	DO RESUME #1
	PLEASE STOP
(208)	DO (40011) NEXT DO NOTE .1=.2 OBJ EQ STEPS
	DO .1 <- #50 DO (30101) NEXT DO NOTE .1 <- TOTING(NUGGET).
	DO .2 <- #1
	DO (209) NEXT DO NOTE SKIP STEPS IF TOTING(NUGGET).
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #14 DO .3 <- #2 DO (210) NEXT
	DO .2 <- #15 DO .3 <- #3 DO (210) NEXT
	DO BUG STEPS
	PLEASE STOP
(209)	DO (40011) NEXT DO NOTE .1=.2 SKIP OBJECT
	DO FORGET #2
	DO (203) NEXT
	PLEASE STOP
(210)	DO (40011) NEXT DO NOTE .1=.2, LOC EQ #14 OR #15, STEPS
	DO .1 <- #7
	DO .2 <- .3
	DO (55501) NEXT DO NOTE PSPEAK
	DO FORGET #2
	DO (203) NEXT
	PLEASE STOP

(240)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP

(241)	DO FORGET #1
	DO ,61000SUB#21 <- #0 DO NOTE VERB <- #0
	DO ,61000SUB#23 <- ,61000SUB#22 DO NOTE OLD_OBJ <- OBJ
	DO ,61000SUB#22 <- #0 DO NOTE OBJ <- #0
	DO (251) NEXT
	PLEASE STOP

	PLEASE CHECK IF THIS LOC IS ELIGIBLE FOR ANY HINTS.  IF BEEN HERE LONG
	       ENOUGH, BRANCH TO HELP SECTION (ON LATER PAGE).  HINTS ALL COME
	       BACK HERE EVENTUALLY TO FINISH THE LOOP.  IGNORE "HINTS" < 4
	       (SPECIAL STUFF, SEE DATABASE NOTES).
(251)	DO FORGET #1
	DO .5 <- #4 DO (252) NEXT
	DO .5 <- #5 DO (252) NEXT
	DO .5 <- #6 DO (252) NEXT
	DO .5 <- #7 DO (252) NEXT
	DO .5 <- #8 DO (252) NEXT
	DO .5 <- #9 DO (252) NEXT
	DO (301) NEXT
	PLEASE STOP
(252)	DO .1 <- ,61004SUB.5#1 DO NOTE .1 <- HINTED
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 NOT HINTED
	DO .1 <- ",60009SUB',61000SUB#1'"~",60011SUB.5#5" DO NOTE .1 <- LOC.HINT
	DO .2 <- #1
	DO .3 <- ,61004SUB.5#2 DO NOTE .3 <- HINTLC
	DO .4 <- #0
	DO (40021) NEXT DO NOTE IF LOC.HINT, .1 <- HINTLC, ELSE .1 <- #0
	DO ,61004SUB.5#2 <- .1 DO NOTE IF NOT LOC.HINT, HINTLC <- #0
	DO .1 <- ",60009SUB',61000SUB#1'"~",60011SUB.5#5" DO NOTE .1 <- LOC.HINT
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 LOC.HINT
	DO .1 <- ,61004SUB.5#2 DO NOTE .1 <- HINTLC
	DO (1020) NEXT
	DO ,61004SUB.5#2 <- .1 DO NOTE HINTLC <- HINTLC + #1
	DO .2 <- ,60011SUB.5#1
	DO (40012) NEXT DO NOTE .1>=.2 HINTLC GE HINT TURNS
	DO .1 <- .5 DO .2 <- #4	DO (254) NEXT DO NOTE CAVE
	DO .1 <- .5 DO .2 <- #5	DO (255) NEXT DO NOTE BIRD
	DO .1 <- .5 DO .2 <- #6	DO (256) NEXT DO NOTE SNAKE
	DO .1 <- .5 DO .2 <- #7	DO (257) NEXT DO NOTE MAZE
	DO .1 <- .5 DO .2 <- #8	DO (258) NEXT DO NOTE DARK
	DO .1 <- .5 DO .2 <- #9	DO (259) NEXT DO NOTE WITT
	DO FORGET #1
	DO (301) NEXT
	PLEASE STOP
(254)	DO (40011) NEXT DO NOTE .1=.2 CAVE HINT
	DO ,61004SUB.5#2 <- #0 DO NOTE HINTLC <- #0
	DO .1 <- ,61003SUB#3#4 DO NOTE .1 <- GRATE.PROP
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 GRATE.PROP #EQ #0
	DO .1 <- #1 DO (30111) NEXT DO NOTE .1 <- HERE(KEYS).
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 NOT HERE(KEYS).
	DO FORGET #1
	DO (261) NEXT
	PLEASE STOP
(255)	DO (40011) NEXT DO NOTE .1=.2 BIRD HINT
	DO .1 <- #8 DO (30111) NEXT DO NOTE .1 <- HERE(BIRD).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 HERE(BIRD).
	DO .1 <- #5 DO (30101) NEXT DO NOTE .1 <- TOTING(ROD).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 TOTING(ROD).
	DO .1 <- ,61000SUB#23 DO NOTE .1 <- OLD_OBJ
	DO .2 <- #8
	DO (40011) NEXT DO NOTE .1=.2 OLD_OBJ EQ BIRD
	DO FORGET #1
	DO ,61004SUB.5#2 <- #0 DO NOTE HINTLC <- #0
	DO (261) NEXT
	PLEASE STOP
(256)	DO (40011) NEXT DO NOTE .1=.2 SNAKE HINT
	DO ,61004SUB.5#2 <- #0 DO NOTE HINTLC <- #0
	DO .1 <- #11 DO (30111) NEXT DO NOTE .1 <- HERE(SNAKE).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 HERE(SNAKE).
	DO .1 <- #8 DO (30111) NEXT DO NOTE .1 <- HERE(BIRD).
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 NOT HERE(BIRD).
	DO FORGET #1
	DO (261) NEXT
	PLEASE STOP
(257)	DO (40011) NEXT DO NOTE .1=.2 MAZE HINT
	DO ,61004SUB.5#2 <- #0 DO NOTE HINTLC <- #0
	DO .1 <- ,61001SUB',61000SUB#1'#1 DO NOTE .1 <- LOC.FIRSTOBJ
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 LOC.FIRSTOBJ EQ #0
	DO .1 <- ,61001SUB',61000SUB#3'#1 DO NOTE .1 <- OLDLOC.FIRSTOBJ
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 OLDLOC.FIRSTOBJ EQ #0
	DO .1 <- ,61001SUB',61000SUB#18'#1 DO NOTE .1 <- OLDLC2.FIRSTOBJ
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 OLDLC2.FIRSTOBJ EQ #0
	DO (30171) NEXT DO NOTE .1 <- HOLDNG
	DO .2 <- #2
	DO (40012) NEXT DO NOTE .1>=.2 HOLDNG GE #2
	DO FORGET #1
	DO (261) NEXT
	PLEASE STOP
(258)	DO (40011) NEXT DO NOTE .1=.2 DARK HINT
	DO ,61004SUB.5#2 <- #0 DO NOTE HINTLC <- #0
	DO .1 <- ,61003SUB#59#5 DO NOTE .1 <- EMERALD.PROPNEG
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 NOT EMERALD.PROPNEG
	DO .1 <- ,61003SUB#60#4 DO NOTE .1 <- PYRAMID.PROPNEG
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 PYRAMID.PROPNEG
	DO FORGET #1
	DO (261) NEXT
	PLEASE STOP
(259)	DO (40011) NEXT DO NOTE .1=.2 WITT HINT
	DO ,61004SUB.5#2 <- #0 DO NOTE HINTLC <- #0
	DO FORGET #1
	DO (261) NEXT
	PLEASE STOP
(261)	DO FORGET #1
	DO .1 <- ,60011SUB.5#3 DO NOTE .1 <- HINTQUESTION
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- #2
	DO (57101) NEXT DO NOTE YESNO
	DO .2 <- #2
	DO (269) NEXT DO NOTE IF NO
	DO,60006SUB#13#2<-#73  DO,60006SUB#13#3<-#32  DO,60006SUB#13#4<-#65
	DO,60006SUB#13#5<-#77  DO,60006SUB#13#6<-#32  DO,60006SUB#13#7<-#80
	DO,60006SUB#13#8<-#82  DO,60006SUB#13#9<-#69  DO,60006SUB#13#10<-#80
	DO,60006SUB#13#11<-#65 DO,60006SUB#13#12<-#82 DO,60006SUB#13#13<-#69
	DO,60006SUB#13#14<-#68 DO,60006SUB#13#15<-#32 DO,60006SUB#13#16<-#84
	DO,60006SUB#13#17<-#79 DO,60006SUB#13#18<-#32 DO,60006SUB#13#19<-#71
	DO,60006SUB#13#20<-#73 DO,60006SUB#13#21<-#86 DO,60006SUB#13#22<-#69
	DO,60006SUB#13#23<-#32 DO,60006SUB#13#24<-#89 DO,60006SUB#13#25<-#79
	DO,60006SUB#13#26<-#85 DO,60006SUB#13#27<-#32 DO,60006SUB#13#28<-#65
	DO,60006SUB#13#29<-#32 DO,60006SUB#13#30<-#72 DO,60006SUB#13#31<-#73
	DO,60006SUB#13#32<-#78 DO,60006SUB#13#33<-#84 DO,60006SUB#13#34<-#44
	DO,60006SUB#13#35<-#32 DO,60006SUB#13#36<-#66 DO,60006SUB#13#37<-#85
	DO,60006SUB#13#38<-#84 DO,60006SUB#13#39<-#32 DO,60006SUB#13#40<-#73
	DO,60006SUB#13#41<-#84 DO,60006SUB#13#42<-#32 DO,60006SUB#13#43<-#87
	DO,60006SUB#13#44<-#73 DO,60006SUB#13#45<-#76 DO,60006SUB#13#46<-#76
	DO,60006SUB#13#47<-#32 DO,60006SUB#13#48<-#67 DO,60006SUB#13#49<-#79
	DO,60006SUB#13#50<-#83 DO,60006SUB#13#51<-#84 DO,60006SUB#13#52<-#32
	DO,60006SUB#13#53<-#89 DO,60006SUB#13#54<-#79 DO,60006SUB#13#55<-#85
	DO,60006SUB#13#56<-#32 DO,60006SUB#13#57<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .1 <- ,60011SUB.5#2 DO NOTE .1 <- HINT.POINTS
	DO .2 <- #10
	DO (57201) NEXT DO NOTE FORMAT INTEGER
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#80  DO,60006SUB#13#4<-#79
	DO,60006SUB#13#5<-#73  DO,60006SUB#13#6<-#78  DO,60006SUB#13#7<-#84
	DO,60006SUB#13#8<-#83  DO,60006SUB#13#9<-#46  DO,60006SUB#13#10<-#10
	DO,60006SUB#13#11<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .1 <- #2
	DO (57101) NEXT DO NOTE YESNO
	DO .2 <- #2
	DO (269) NEXT DO NOTE IF NO
	DO .1 <- ,60011SUB.5#4 DO NOTE .1 <- HINT.TEXT
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,61004SUB.5#1 <- #1 DO NOTE HINTED <- #1
	DO .1 <- #30
	DO .2 <- ,61000SUB#5 DO NOTE .2 <- LIMIT
	DO (262) NEXT DO NOTE IF LIMIT GT #30
	DO .2 <- ,60011SUB.5#2 DO NOTE .2 <- HINT.POINTS
	DO (1030) NEXT DO NOTE .3 <- #30 * HINT.POINTS
	DO .1 <- ,61000SUB#5 DO NOTE .1 <- LIMIT
	DO .2 <- .3
	DO (1000) NEXT DO NOTE .3 <- LIMIT + #30 * HINT.POINTS
	DO ,61000SUB#5 <- .3 DO NOTE LIMIT <- LIMIT + #30 * HINT.POINTS
	DO (301) NEXT
	PLEASE STOP
(262)	DO (40012) NEXT DO NOTE .1>=.2
	DO FORGET #1
	DO (301) NEXT
(269)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #54
	DO (301) NEXT
	DO (55601) NEXT DO NOTE RSPEAK
	PLEASE STOP

	PLEASE KICK THE RANDOM NUMBER GENERATOR JUST TO ADD VARIETY TO THE
	       CHASE.  ALSO, IF CLOSING TIME, CHECK FOR ANY OBJECTS BEING TOTED
	       WITH PROP < 0 AND SET THE PROP TO -1-PROP.  THIS WAY OBJECTS
	       WON'T BE DESCRIBED UNTIL THEY'VE BEEN PICKED UP AND PUT DOWN
	       SEPARATE FROM THEIR RESPECTIVE PILES.  DON'T TICK CLOCK1 UNLESS
	       WELL INTO CAVE (AND NOT AT Y2).
(301)	DO FORGET #1
	DO .1 <- ,61000SUB#26 DO NOTE .1 <- CLOSED
	DO .2 <- #0
	DO (308) NEXT
	DO .1 <- #15
	DO (30101) NEXT DO NOTE .1 <- TOTING(OYSTER).
	DO .1 <- "&',61003SUB#15#5'¢.1"~#1 DO NOTE .1 <- OYSTER.PROPNEG AND TOTING(OYSTER).
	DO (302) NEXT
	DO .1 <- #15
	DO .2 <- #1
	DO (55501) NEXT DO NOTE PSPEAK(OYSTER,1).
	DO .1 <- #1
	DO (303) NEXT
	PLEASE STOP
(302)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #1
	DO (303) NEXT
	PLEASE STOP
(303)	DO FORGET #1
	DO .3 <- .1
	DO (30101) NEXT DO NOTE .1 <- TOTING(.1).
	DO .2 <- #1
	DO (304) NEXT
	DO .1 <- .3
	DO .2 <- #64
	DO (308) NEXT
	DO (1020) NEXT
	DO (303) NEXT
	PLEASE STOP
(304)	DO (40011) NEXT DO NOTE .1=.2
	DO ,61003SUB.3#5 <- #0 DO NOTE OBJ.PROPNEG <- #0
	DO RESUME #1
	PLEASE STOP
(308)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (30161) NEXT DO NOTE .1 <- DARK().
	DO ,61000SUB#20 <- .1 DO NOTE WZDARK <- DARK
	DO,60006SUB#13#2<-#62  DO,60006SUB#13#3<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (51001) NEXT DO NOTE CALL GETIN
	DO (60010) NEXT
	DO (309) NEXT
	PLEASE STOP
(309)	DO FORGET #1
	DO ,51001SUB#3#4 <- #1 DO NOTE CURRENT WORD INDEX INDEX <- #1
	DO (310) NEXT DO NOTE CHECK FOR EOF
	DO (311) NEXT
	PLEASE STOP
(310)	DO (57108) NEXT
	DO FORGET #1
	DO NOTE AT EOF
	DO ,61000SUB#27 <- #1 DO NOTE GAVEUP <- #1
	DO (10051) NEXT
	PLEASE STOP

	PLEASE EVERY INPUT, CHECK "FOOBAR" FLAG.  IF ZERO, NOTHING'S GOING ON.
	       IF POS, MAKE NEG.  IF NEG, HE SKIPPED A WORD, SO MAKE IT ZERO.
(311)	DO FORGET #1
	DO .1 <- ',61000SUB#14'~#32768 DO NOTE .1 <- FOOBAR NEG FLAG
	DO .2 <- #0
	DO .3 <- '#32768¢",61000SUB#14"'~'#32768¢#32767' DO NOTE .3 <- FOOBAR BOR NEG FLAG
	DO .4 <- #0
	DO (40021) NEXT DO NOTE IF FOOBAR NEG FLAG EQ #0, .1 <- FOOBAR BOR NEG FLAG, ELSE .1 <- #0
	DO ,61000SUB#14 <- .1 DO NOTE FOOBAR <- #1
	DO NOTE TURNS <- TURNS + #1
	DO .1 <- ,61000SUB#4 DO NOTE .1 <- TURNS
	DO (1020) NEXT
	DO ,61000SUB#4 <- .1 DO NOTE TURNS <- .1
	DO NOT [TURNS] .1
	DO NOTE IF VERB EQ SAY,
	            IF WD2 EQ '', VERB <- #0
	            ELSE GOTO (10101).
	DO .1 <- ,61000SUB#21 DO NOTE .1 <- VERB
	DO .2 <- #3
	DO (312) NEXT
	DO (314) NEXT
	PLEASE STOP
(312)	DO (40011) NEXT DO NOTE .1=.2 VERB EQ SAY
	DO FORGET #1
	DO .1 <- ,51001SUB#2#1 DO NOTE .1 <- WD2
	DO .2 <- #0
	DO (313) NEXT
	DO ,61000SUB#21 <- #0 DO NOTE VERB <- #0
	DO (314) NEXT
	PLEASE STOP
(313)	DO (40011) NEXT DO NOTE WD2 EQ #0
	DO FORGET #1
	DO (10101) NEXT
	PLEASE STOP
(314)	DO FORGET #1
	DO (321) NEXT DO NOTE TICK CLOCK1
	DO (322) NEXT DO NOTE TICK CLOCK2 WHEN CLOCK1 EQ #0

	DO NOTE ... IF(LIMIT.LE.30+PROP(LAMP).AND.HERE(BATTERIES).AND.PROP(BATTERIES).EQ.0.AND.HERE(LAMP)) GOSUB AUTO-REPLACE BATTERIES
	DO NOTE ... IF(PROP(LAMP).EQ.1)LIMIT=LIMIT-1
	DO NOTE ... IF(LIMIT.EQ.0) GOSUB LAMP GOES DEAD
	DO NOTE ... IF(LIMIT.LT.0.AND.LOC.LE.8) RSPEAK(185) GOTO GIVE UP
	DO NOTE ... IF(LIMIT.LE.30) GOSUB OTHER CASES OF LAMP DYING 

	DO (391) NEXT
	PLEASE STOP

	PLEASE TICK CLOCK1
(321)	DO .1 <- ,61000SUB#29 DO NOTE .1 <- TALLY
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 TALLY EQ #0
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #15
	DO (40021) NEXT DO NOTE .1>=.2 LOC GE #15
	DO .2 <- #33
	DO (40003) NEXT DO NOTE .3 <- LOC NE #33
	DO .1 <- .3
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 LOC NE #33
	DO .1 <- ,61000SUB#16 DO NOTE .1 <- CLOCK1
	DO .2 <- #1
	DO (40012) NEXT DO NOTE .1>=.2 CLOCK1 GE #1
	DO (1010) NEXT
	DO ,61000SUB#16 <- .3 DO NOTE CLOCK1 <- CLOCK1 - #1
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 CLOCK1 EQ #0
	DO FORGET #1
	DO NOTE WHEN THE FIRST WARNING COMES, WE LOCK THE GRATE, DESTROY THE
	       BRIDGE, KILL ALL THE DWARVES (AND THE PIRATE), REMOVE THE TROLL
	       AND BEAR (UNLESS DEAD), AND SET "CLOSNG" TO TRUE.  LEAVE THE
	       DRAGON; TOO MUCH TROUBLE TO MOVE IT.  FROM NOW UNTIL CLOCK2 RUNS
	       OUT, HE CANNOT UNLOCK THE GRATE, MOVE TO ANY LOCATION OUTSIDE
	       THE CAVE (LOC<9), OR CREATE THE BRIDGE.  NOR CAN HE BE
	       RESURRECTED IF HE DIES.  NOTE THAT THE SNAKE IS ALREADY GONE,
	       SINCE HE GOT TO THE TREASURE ACCESSIBLE ONLY VIA THE HALL OF THE
	       MT. KING.  ALSO, HE'S BEEN IN GIANT ROOM (TO GET EGGS), SO WE
	       CAN REFER TO IT.  ALSO ALSO, HE'S GOTTEN THE PEARL, SO WE KNOW
	       THE BIVALVE IS AN OYSTER.  *AND*, THE DWARVES MUST HAVE BEEN
	       ACTIVATED, SINCE WE'VE FOUND CHEST.
	DO ,61003SUB#3#4 <- #0 DO NOTE GRATE.PROP <- #0
	DO ,61003SUB#12#4 <- #0 DO NOTE FISSURE.PROP <- #0
	DO ,61002SUB#1#1 <- #0 DO NOTE DLOC(#1) <- #0
	DO ,61002SUB#1#3 <- #0 DO NOTE DSEEN(#1) <- #0
	DO ,61002SUB#2#1 <- #0 DO NOTE DLOC(#2) <- #0
	DO ,61002SUB#2#3 <- #0 DO NOTE DSEEN(#2) <- #0
	DO ,61002SUB#3#1 <- #0 DO NOTE DLOC(#3) <- #0
	DO ,61002SUB#3#3 <- #0 DO NOTE DSEEN(#3) <- #0
	DO ,61002SUB#4#1 <- #0 DO NOTE DLOC(#4) <- #0
	DO ,61002SUB#4#3 <- #0 DO NOTE DSEEN(#4) <- #0
	DO ,61002SUB#5#1 <- #0 DO NOTE DLOC(#5) <- #0
	DO ,61002SUB#5#3 <- #0 DO NOTE DSEEN(#5) <- #0
	DO ,61002SUB#6#1 <- #0 DO NOTE DLOC(#6) <- #0
	DO ,61002SUB#6#3 <- #0 DO NOTE DSEEN(#6) <- #0
	DO .1 <- #33 DO .2 <- #0 DO (30001) NEXT DO NOTE MOVE TROLL TO #0
	DO ,60007SUB#33#1 <- #65535 DO NOTE TROLL.PLAC <- #0
	DO ,60007SUB#33#2 <- #65535 DO NOTE TROLL.FIXD <- #0
	DO .1 <- #34 DO .2 <- #117 DO (30001) NEXT DO NOTE MOVE TROLL2 TO LOC
	DO ,60007SUB#34#1 <- #122 DO NOTE TROLL2.PLAC <- #117
	DO ,60007SUB#34#2 <- #122 DO NOTE TROLL2.FIXD <- #122
	DO .1 <- #32
	DO .2 <- ,61003SUB#32#1
	DO (30001) NEXT DO NOTE JUGGLE CHASM: MOVE CHASM TO CHASM.LOC
	DO .1 <- ,61003SUB#35#4 DO NOTE .1 <- BEAR.PROP
	DO .2 <- #3
	DO .3 <- ,61003SUB#35#1 DO NOTE .3 <- BEAR.LOC
	DO .4 <- #0
	DO (40002) NEXT DO NOTE IF BEAR.PROP EQ #3, .1 <- BEAR.LOC, ELSE .1 <- #0
	DO .2 <- .1
	DO .1 <- #35
	DO (30001) NEXT DO NOTE IF BEAR.PROP EQ #3, MOVE BEAR TO #0
	DO ,61003SUB#64#4 <- #0 DO NOTE CHAIN.PROP <- #0
	DO ,60007SUB#64#2 <- #0 DO NOTE CHAIN.FIXD <- #0
	DO ,61003SUB#28#4 <- #0 DO NOTE AXE.PROP <- #0
	DO ,60007SUB#28#2 <- #0 DO NOTE AXE.FIXD <- #0
	DO .1 <- #129	
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,61000SUB#19 <- #1 DO NOTE CLOSNG <- #1
	DO (391) NEXT
	PLEASE STOP
	
	PLEASE TICK CLOCK2 WHEN CLOCK1 EQ #0
(322)	DO .1 <- ,61000SUB#16 DO NOTE .1 <- CLOCK1
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 CLOCK1 EQ #0
	DO .1 <- ,61000SUB#17 DO NOTE .1 <- CLOCK2
	DO .2 <- #1
	DO (40012) NEXT DO NOTE .1>=.2 CLOCK2 GE #1
	DO (1010) NEXT
	DO ,61000SUB#17 <- .3 DO NOTE CLOCK2 <- CLOCK2 - #1
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 CLOCK2 EQ #0
	DO FORGET #1
	DO NOTE ONCE HE'S PANICKED, AND CLOCK2 HAS RUN OUT, WE COME HERE TO
	       SET UP THE STORAGE ROOM.  THE ROOM HAS TWO LOCS, HARDWIRED AS
	       115 (NE) AND 116 (SW).  AT THE NE END, WE PLACE EMPTY BOTTLES, A
	       NURSERY OF PLANTS, A BED OF OYSTERS, A PILE OF LAMPS, RODS WITH
	       STARS, SLEEPING DWARVES, AND HIM.  AT THE SW END WE PLACE GRATE
	       OVER TREASURES, SNAKE PIT, COVEY OF CAGED BIRDS, MORE RODS, AND
	       PILLOWS.  A MIRROR STRETCHES ACROSS ONE WALL.  MANY OF THE
	       OBJECTS COME FROM KNOWN LOCATIONS AND/OR STATES (E.G. THE SNAKE
	       IS KNOWN TO HAVE BEEN DESTROYED AND NEEDN'T BE CARRIED AWAY FROM
	       ITS OLD "PLACE"), MAKING THE VARIOUS OBJECTS BE HANDLED
	       DIFFERENTLY.  WE ALSO DROP ALL OTHER OBJECTS HE MIGHT BE
	       CARRYING (LEST HE HAVE SOME WHICH COULD CAUSE TROUBLE, SUCH AS
	       THE KEYS).  WE DESCRIBE THE FLASH OF LIGHT AND TRUNDLE BACK.
	DO .2 <- #115
	DO ,61003SUB#20#4 <- #1 DO NOTE BOTTLE.PROP <- #1
	DO ,61003SUB#20#5 <- #1 DO NOTE BOTTLE.PROPNEG <- #1
	DO .1 <- #20
	DO (30001) NEXT DO NOTE MOVE BOTTLE TO #115
	DO ,61003SUB#24#4 <- #0 DO NOTE PLANT.PROP <- #0
	DO ,61003SUB#24#5 <- #1 DO NOTE PLANT.PROPNEG <- #1
	DO .1 <- #24
	DO (30001) NEXT DO NOTE MOVE PLANT TO #115
	DO ,61003SUB#15#4 <- #0 DO NOTE OYSTER.PROP <- #0
	DO ,61003SUB#15#5 <- #1 DO NOTE OYSTER.PROPNEG <- #1
	DO .1 <- #15
	DO (30001) NEXT DO NOTE MOVE OYSTER TO #115
	DO ,61003SUB#2#4 <- #0 DO NOTE LAMP.PROP <- #0
	DO ,61003SUB#2#5 <- #1 DO NOTE LAMP.PROPNEG <- #1
	DO .1 <- #2
	DO (30001) NEXT DO NOTE MOVE LAMP TO #115
	DO ,61003SUB#5#4 <- #0 DO NOTE ROD.PROP <- #0
	DO ,61003SUB#5#5 <- #1 DO NOTE ROD.PROPNEG <- #1
	DO .1 <- #5
	DO (30001) NEXT DO NOTE MOVE ROD TO #115
	DO ,61003SUB#17#4 <- #0 DO NOTE DWARF.PROP <- #0
	DO ,61003SUB#17#5 <- #1 DO NOTE DWARF.PROPNEG <- #1
	DO .1 <- #17
	DO (30001) NEXT DO NOTE MOVE DWARF TO #115
	DO ,61000SUB#1 <- #115 DO NOTE LOC <- #115
	DO ,61000SUB#2 <- #115 DO NOTE NEWLOC <- #115
	DO ,61000SUB#3 <- #115 DO NOTE OLDLOC <- #115
	DO NOTE LEAVE THE GRATE WITH NORMAL (NON-NEGATIVE) PROPERTY.
	DO .2 <- #116
	DO .1 <- #3
	DO (30001) NEXT DO NOTE MOVE GRATE TO #116
	DO ,61003SUB#11#4 <- #1 DO NOTE SNAKE.PROP <- #1
	DO ,61003SUB#11#5 <- #1 DO NOTE SNAKE.PROPNEG <- #1
	DO .1 <- #11
	DO (30001) NEXT DO NOTE MOVE SNAKE TO #116
	DO ,61003SUB#8#4 <- #1 DO NOTE BIRD.PROP <- #1
	DO ,61003SUB#8#5 <- #1 DO NOTE BIRD.PROPNEG <- #1
	DO .1 <- #8
	DO (30001) NEXT DO NOTE MOVE BIRD TO #116
	DO ,61003SUB#4#4 <- #0 DO NOTE CAGE.PROP <- #0
	DO ,61003SUB#4#5 <- #1 DO NOTE CAGE.PROPNEG <- #1
	DO .1 <- #4
	DO (30001) NEXT DO NOTE MOVE CAGE TO #116
	DO ,61003SUB#6#4 <- #0 DO NOTE ROD2.PROP <- #0
	DO ,61003SUB#6#5 <- #1 DO NOTE ROD2.PROPNEG <- #1
	DO .1 <- #6
	DO (30001) NEXT DO NOTE MOVE ROD2 TO #116
	DO ,61003SUB#10#4 <- #0 DO NOTE PILLOW.PROP <- #0
	DO ,61003SUB#10#5 <- #1 DO NOTE PILLOW.PROPNEG <- #1
	DO .1 <- #10
	DO (30001) NEXT DO NOTE MOVE PILLOW TO #116

	DO ,61003SUB#23#4 <- #0 DO NOTE MIRROR.PROP <- #0
	DO ,61003SUB#23#5 <- #1 DO NOTE MIRROR.PROPNEG <- #1
	DO .1 <- #23
	DO .2 <- #115
	DO (30001) NEXT DO NOTE MOVE MIRROR TO #115
	DO ,60007SUB#23#2 <- #116 DO NOTE MIRROR.LOC2 <- #116

	DO .5 <- #1
	DO (323) NEXT
	PLEASE STOP
	PLEASE FOR .5 IN #1 .. #64
(323)	DO FORGET #1
	DO .1 <- ,61003SUB.5#1 DO NOTE .1 <- OBJ.LOC
	DO .2 <- #65535
	DO .3 <- #0
	DO .4 <- .1
	DO (40021) NEXT DO NOTE IF OBJ.LOC EQ #65535, .1 <- #0, ELSE .1 <- OBJ.LOC
	DO ,61003SUB.5#1 <- .1 DO NOTE IF OBJ.LOC EQ #65535, OBJ.LOC <- #0
	DO .1 <- .5
	DO .2 <- #64
	DO (324) NEXT
	DO (1020) NEXT
	DO .5 <- .1
	DO (323) NEXT
	PLEASE STOP
(324)	DO (40011) NEXT DO NOTE .1=.2 END (323) LOOP
	DO FORGET #1
	DO .1 <- #132
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,61000SUB#26 <- #1 DO NOTE CLOSED <- #1
	DO (5) NEXT
	PLEASE STOP

(390)	DO (40011) NEXT DO NOTE .1=.2
	DO NOT STOP, FALLTHROUGH
(391)	DO FORGET #1
	DO NOTE ... IF(.1 = 'ENTER' AND .2 = 'STREA' OR .2 = 'WATER' AND LIQLOC(LOC).EQ.WATER) RSPEAK(70).
	DO NOTE ... IF(.1 = 'ENTER' AND .2 = 'STREA' OR .2 = 'WATER') RSPEAK(43).
	DO NOTE ... OIL/WATER D0OR/PLANT -> POUR

	DO (400) NEXT
	PLEASE STOP

	PLEASE IF CURRENT WORD INDEX IS WEST,
	           IWEST <- IWEST + #1
	           IF IWEST EQ #10, RSPEAK(#17)
	       DISPATCH ON CURRENT WORD.TAG
(400)	DO FORGET #1
	DO .1 <- ,51001SUB#3#4 DO NOTE .1 <- CURRENT WORD INDEX INDEX
	DO .2 <- #11
	DO (1000) NEXT
	DO .1 <- ,51001SUB#3.1 DO NOTE .1 <- CURRENT WORD INDEX
	DO .2 <- #64 DO NOTE .2 <- WEST
	DO (401) NEXT
	DO (403) NEXT
(401)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ,61000SUB#6 DO NOTE .1 <- IWEST
	DO (1020) NEXT
	DO ,61000SUB#6 <- .1 DO NOTE IWEST <- .1
	DO NOTE IF(IWEST.EQ.10)RSPEAK(17).
	DO .2 <- #10
	DO (402) NEXT
	DO (403) NEXT
	PLEASE STOP
(402)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #17
	DO (55601) NEXT DO NOTE RSPEAK
	DO (403) NEXT
	PLEASE STOP
(403)	DO FORGET #1
	DO .1 <- ,51001SUB#3#4 DO NOTE .1 <- CURRENT WORD INDEX INDEX
	DO .2 <- #11
	DO (1000) NEXT
	DO .1 <- ,51001SUB#3.1 DO NOTE .1 <- CURRENT WORD INDEX
	DO .2 <- #0
	DO NOTE IF(.1 = 0) SORRY, I D0N'T KNOW THE WORD, GOTO (251).
	DO (404) NEXT
	DO (405) NEXT
	PLEASE STOP
(404)	DO (40011) NEXT DO NOTE .1=.2
	DO NOTE GEE, I D0N'T UNDERSTAND.
	DO FORGET #1
	DO .1 <- ,51001SUB#3#4 DO NOTE .1 <- CURRENT WORD INDEX INDEX
	DO,60006SUB#13#2<-#83  DO,60006SUB#13#3<-#79  DO,60006SUB#13#4<-#82
	DO,60006SUB#13#5<-#82  DO,60006SUB#13#6<-#89  DO,60006SUB#13#7<-#44
	DO,60006SUB#13#8<-#32  DO,60006SUB#13#9<-#73  DO,60006SUB#13#10<-#32
	DO,60006SUB#13#11<-#68 DO,60006SUB#13#12<-#79 DO,60006SUB#13#13<-#78
	DO,60006SUB#13#14<-#39 DO,60006SUB#13#15<-#84 DO,60006SUB#13#16<-#32
	DO,60006SUB#13#17<-#75 DO,60006SUB#13#18<-#78 DO,60006SUB#13#19<-#79
	DO,60006SUB#13#20<-#87 DO,60006SUB#13#21<-#32 DO,60006SUB#13#22<-#84
	DO,60006SUB#13#23<-#72 DO,60006SUB#13#24<-#69 DO,60006SUB#13#25<-#32
	DO,60006SUB#13#26<-#87 DO,60006SUB#13#27<-#79 DO,60006SUB#13#28<-#82
	DO,60006SUB#13#29<-#68 DO,60006SUB#13#30<-#32 DO,60006SUB#13#31<-#34
	DO,60006SUB#13#32<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (51501) NEXT DO NOTE READ OUT INPUT BUFFER
	DO,60006SUB#13#2<-#34  DO,60006SUB#13#3<-#46  DO,60006SUB#13#4<-#10
	DO,60006SUB#13#5<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (251) NEXT
	PLEASE STOP

	PLEASE .1 HAS CURRENT WORD INDEX
(405)	DO FORGET #1
	DO .5 <- ,60004SUB.1#3 DO NOTE .5 <- VOCAB(CURRENT WORD).
	DO .1 <- .5~#65280 DO NOTE .1 <- VOCAB(CURRENT WORD).TAG
	DO .2 <- #1
	DO (406) NEXT
	DO .2 <- #2
	DO (407) NEXT
	DO .2 <- #3
	DO (408) NEXT
	DO .2 <- #4 DO .3 <- .1 <- ',60004SUB.3#3'~#255
	DO (240) NEXT
	DO BUG INVALID VOCAB ENTRY
(406)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE MOTION VERB
	DO .1 <- .5~#255 DO NOTE VOCAB(VERB).VALUE
	DO (501) NEXT
	PLEASE STOP
(407)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE OBJECT
	DO (701) NEXT
	PLEASE STOP
(408)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE ACTION VERB
	DO (601) NEXT
	PLEASE STOP

	PLEASE FIGURE OUT THE NEW LOCATION
	       GIVEN THE CURRENT LOCATION IN "LOC", AND A MOTION VERB NUMBER
	       IN "K", PUT THE NEW LOCATION IN "NEWLOC".  THE CURRENT LOC IS
	       SAVED IN "OLDLOC" IN CASE HE WANTS TO RETREAT.  THE CURRENT
	       OLDLOC IS SAVED IN OLDLC2, IN CASE HE DIES.  (IF HE DOES,
	       NEWLOC WILL BE LIMBO, AND OLDLOC WILL BE WHAT KILLED HIM, SO WE
	       NEED OLDLC2, WHICH IS THE LAST PLACE HE WAS SAFE.)
	       .1 HAS MOTION VERB VALUE
(501)	DO FORGET #1
	DO .5 <- .1 DO NOTE SAVE MOTION VERB VALUE
	DO ,61000SUB#2 <- ,61000SUB#1 DO NOTE NEWLOC <- LOC
	DO .2 <- #21 DO NOTE CHECK FOR NOWHERE
	DO (502) NEXT
	DO (503) NEXT
	PLEASE STOP
(502)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (5) NEXT
	PLEASE STOP
(503)	DO FORGET #1
	DO .2 <- #8 DO NOTE CHECK FOR GO BACK
	DO (504) NEXT
	DO (505) NEXT
	PLEASE STOP
(504)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO [... IMPLEMENT GO BACK]
	DO (5) NEXT
	PLEASE STOP
(505)	DO FORGET #1
	DO .2 <- #57 DO NOTE CHECK FOR LOOK
	DO (506) NEXT
	DO (507) NEXT
	PLEASE STOP
(506)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (10301) NEXT
	PLEASE STOP
(507)	DO FORGET #1
	DO .2 <- #67 DO NOTE CHECK FOR CAVE
	DO (508) NEXT
	DO (509) NEXT
	PLEASE STOP
(508)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (10111) NEXT
	PLEASE STOP
(509)	DO FORGET #1
	DO ,61000SUB#18 <- ,61000SUB#3 DO NOTE OLDLC2 <- OLDLOC
	DO ,61000SUB#3 <- ,61000SUB#1 DO NOTE OLDLOC <- LOC
	DO .6 <- ,60013SUB',61000SUB#1' DO NOTE .6 <- TRAVELSTARTINDEX(LOC).
	DO NOT [LOC] ,61000SUB#1
	DO NOT [TRAVELSTARTINDEX] .6
	DO .7 <- #4
	DO (510) NEXT
	PLEASE STOP
	PLEASE FOR .6 IN TRAVELSTARTINDEX(LOC) .. WHILE TRAVEL(.6,#1).EQ.LOC
	PLEASE FOR .7 IN #4 .. LEN(TRAVEL(.6))
(510)	DO FORGET #1
	DO NOT [VERB] .5
	DO NOT [TRAVEL INDEX] .6
	DO NOT [TRAVEL INDEX VERB INDEX] .7
	DO NOT [TRAVEL INDEX VERB] ,60003SUB.6.7
	DO NOT [TRAVEL SRC] ,60003SUB.6#1
	DO NOT [TRAVEL DEST TAG] ",60003SUB.6#2"~#65280
	DO NOT [TRAVEL DEST LOC] ",60003SUB.6#2"~#255
	DO NOT [TRAVEL COND] ,60003SUB.6#3
	DO NOT [LOC] ,61000SUB#1
	DO .1 <- ,60003SUB.6.7
	DO .2 <- #0
	DO (511) NEXT
	DO (513) NEXT
	PLEASE STOP
(511)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE NO APPLICABLE MOTION, NEXT .6
	DO .1 <- .6
	DO (1020) NEXT
	DO .6 <- .1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- ,60003SUB.6#1 DO NOTE .2 <- TRAVEL(.6,#1) (TRAVEL SOURCE).
	DO (512) NEXT
	DO NOTE TRAVEL(.6,#1).NE.LOC
	DO (591) NEXT
(512)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE NEXT .6
	DO .7 <- #4
	DO (510) NEXT
	PLEASE STOP
(513)	DO FORGET #1
	DO .1 <- .5 DO NOTE .1 <- TRAVEL VERB
	DO .2 <- ,60003SUB.6.7 DO NOTE .2 <- TRAVEL(.6,.7).
	DO (515) NEXT
	DO (514) NEXT
	PLEASE STOP
(514)	DO FORGET #1
	DO NOTE NEXT .7
	DO .1 <- .7
	DO (1020) NEXT
	DO .7 <- .1
	DO (510) NEXT
	PLEASE STOP
(515)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE FOUND MATCHING TRAVEL VERB
	DO NOT [VERB] .5
	DO NOT [TRAVEL INDEX] .6
	DO .1 <- ',60003SUB.6#3'~#65280 DO NOTE .1 <- TRAVEL COND TAG
	DO NOT [TRAVEL COND TAG] .1
	DO .2 <- #1 DO (525) NEXT
	DO .2 <- #2 DO (525) NEXT
	DO .2 <- #3 DO (521) NEXT
	DO .2 <- #4 DO (522) NEXT
	DO .2 <- #5 DO (523) NEXT
	DO .2 <- #6 DO .3 <- #0 DO (524) NEXT
	DO .2 <- #7 DO .3 <- #1 DO (524) NEXT
	DO .2 <- #8 DO .3 <- #2 DO (524) NEXT
	DO .2 <- #9 DO .3 <- #3 DO (524) NEXT
	DO .2 <- #10 DO .3 <- #4 DO (524) NEXT
	DO BUG IN TRAVEL TABLE
	PLEASE STOP
(521)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (1900) NEXT
	DO .2 <- #100
	DO (40001) NEXT DO NOTE .3 <- RAND MOD #100
	DO .1 <- .3
	DO .2 <- ',60003SUB.6#3'~#255 DO NOTE .2 <- TRAVEL COND VALUE
	DO (40004) NEXT DO NOTE .3 <- RAND MOD #100 LT TRAVEL COND VALUE
	DO .1 <- .3
	DO .2 <- #1
	DO (525) NEXT
	DO (514) NEXT
	PLEASE STOP
(522)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ',60003SUB.6#3'~#255 DO NOTE .1 <- TRAVEL COND OBJ
	DO (30101) NEXT DO NOTE .1 <- TOTING(.1).
	DO .2 <- #1
	DO (525) NEXT
	DO (514) NEXT
	PLEASE STOP
(523)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ',60003SUB.6#3'~#255 DO NOTE .1 <- TRAVEL COND OBJ
	DO (30111) NEXT DO NOTE .1 <- HERE(OBJ).
	DO .2 <- #1
	DO (525) NEXT
	DO (514) NEXT
	PLEASE STOP
(524)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ,61003SUB"',60003SUB.6#3'~#255"#4 DO NOTE .1 <- TRAVEL COND OBJ.PROP
	DO .2 <- .3
	DO (40003) NEXT DO NOTE .3 <- TRAVEL COND OBJ.PROP NE TRAVEL COND TAG
	DO .1 <- .3
	DO .2 <- #1
	DO (525) NEXT
	DO (514) NEXT
	PLEASE STOP
(525)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ',60003SUB.6#2'~#65280 DO NOTE .1 <- TRAVEL DEST TAG
	DO .2 <- #1 DO (526) NEXT
	DO .2 <- #2 DO (527) NEXT
	DO .2 <- #3 DO .3 <- ',60003SUB.6#2'~#255 DO (240) NEXT
	DO BUG TRAVEL TABLE
	PLEASE STOP
(526)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO ,61000SUB#2 <- ',60003SUB.6#2'~#255 DO NOTE NEWLOC <- TRAVEL(.6,#2) (TRAVEL DEST).
	DO NOT [NEWLOC AFTER MOVE] ,61000SUB#2
	DO (5) NEXT
	PLEASE STOP
(527)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ',60003SUB.6#2'~#255 DO NOTE .1 <- TRAVEL DEST VALUE
	DO .2 <- #1 DO (531) NEXT DO NOTE PLOVER-ALCOVE
	DO .2 <- #2 DO (541) NEXT DO NOTE PLOVER TRANSPORT
	DO .2 <- #3 DO (551) NEXT DO NOTE TROLL BRIDGE
	DO BUG IN TRAVEL TABLE
	PLEASE STOP

	PLEASE TRAVEL 301.  PLOVER-ALCOVE PASSAGE.  CAN CARRY ONLY EMERALD.
	       NOTE: TRAVEL TABLE MUST INCLUDE "USELESS" ENTRIES GOING THROUGH
	       PASSAGE, WHICH CAN NEVER BE USED FOR ACTUAL MOTION, BUT CAN BE
	       SPOTTED BY "GO BACK".
(531)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #199
	DO .2 <- ,61000SUB#1 DO NOTE .2 <- LOC
	DO (1010) NEXT
	DO ,61000SUB#2 <- .3 DO NOTE NEWLOC <- #99 + #100 - LOC
	DO .4 <- #1
	DO (532) NEXT
	PLEASE STOP
	PLEASE FOR .4 IN #1 .. #64
(532)	DO FORGET #1
	DO .1 <- ,61003SUB.4#1 DO NOTE .1 <- OBJ.LOC
	DO .2 <- #65535
	DO (533) NEXT
	DO .1 <- .4
	DO .2 <- #64
	DO (534) NEXT
	DO (1020) NEXT
	DO .4 <- .1
	DO (532) NEXT
(533)	DO (40011) NEXT DO NOTE .1=.2 OBJ.LOC EQ #65535
	DO .1 <- .4
	DO .2 <- #59
	DO (40003) NEXT DO NOTE .3 <- OBJ NE EMERALD
	DO .1 <- .3
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 OBJ NE EMERALD
	DO FORGET #1
	DO ,61000SUB#2 <- ,61000SUB#1 DO NOTE NEWLOC <- LOC
	DO .1 <- #117
	DO (55601) NEXT DO NOTE RSPEAK
	DO .2 <- .1
	DO (534) NEXT
	PLEASE STOP
(534)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (5) NEXT
	PLEASE STOP

	PLEASE TRAVEL 302.  PLOVER TRANSPORT.  DROP THE EMERALD (ONLY USE
	       SPECIAL TRAVEL IF TOTING IT), SO HE'S FORCED TO USE THE
	       PLOVER-PASSAGE TO GET IT OUT.  HAVING DROPPED IT, GO BACK AND
	       PRETEND HE WASN'T CARRYING IT AFTER ALL.
(541)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #59
	DO .2 <- ,61000SUB#1 DO NOTE .2 <- LOC
	DO (30001) NEXT DO NOTE MOVE EMERALD TO LOC
	DO .1 <- .5
	DO (501) NEXT

	PLEASE TRAVEL 303.  TROLL BRIDGE.  MUST BE DONE ONLY AS SPECIAL MOTION
	       SO THAT DWARVES WON'T WANDER ACROSS AND ENCOUNTER THE BEAR.
	       (THEY WON'T FOLLOW THE PLAYER THERE BECAUSE THAT REGION IS
	       FORBIDDEN TO THE PIRATE.)  IF PROP(TROLL)=1, HE'S CROSSED SINCE
	       PAYING, SO STEP OUT AND BLOCK HIM.  (STANDARD TRAVEL ENTRIES
	       CHECK FOR PROP(TROLL)=0.)  SPECIAL STUFF FOR BEAR.
(551)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (553) NEXT DO NOTE IF TROLL.PROP EQ #1
	DO .1 <- ,61003SUB#33#4
	DO .2 <- #0
	DO .3 <- #1
	DO .4 <- ,61003SUB#33#4
	DO (40021) NEXT
	DO ,61003SUB#33#4 <- .1 DO NOTE IF TROLL.PROP EQ #0, TROLL.PROP <- #1
	DO .1 <- #239
	DO .2 <- ,61000SUB#1 DO NOTE .2 <- LOC
	DO (1010) NEXT
	DO ,61000SUB#2 <- .3 DO NOTE NEWLOC <- #117 + #122 - LOC
	DO (552) NEXT DO NOTE IF TOTING(BEAR).
	DO (5) NEXT
(552)	DO .1 <- #35 DO (30101) NEXT DO NOTE .1 <- TOTING(BEAR).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 TOTING(BEAR).
	DO FORGET #1
	DO .1 <- #162
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,61003SUB#32#4 <- #1 DO NOTE CHASM.PROP <- #1
	DO ,61003SUB#33#4 <- #2 DO NOTE TROLL.PROP <- #2
	DO ,61003SUB#35#4 <- #3 DO NOTE BEAR.PROP <- #3
	DO .1 <- #35 DO .2 <- ,61000SUB#2 DO (30001) NEXT DO NOTE MOVE BEAR TO NEWLOC
	DO ,60007SUB#35#2 <- #65535 DO NOTE BEAR.FIXD <- #65535
	DO .1 <- ,61003SUB#63#5 DO NOTE .1 <- SPICES.PROPNEG
	DO .2 <- ,61000SUB#30 DO NOTE .2 <- TALLY2
	DO (1000) NEXT
	DO ,61000SUB#30 <- .3 DO NOTE TALLY2=TALLY2+SPICES.PROPNEG
	DO ,61000SUB#18 <- ,61000SUB#2 DO NOTE OLDLC2 <- NEWLOC
	DO (10001) NEXT
	PLEASE STOP
(553)	DO .1 <- ,61003SUB#33#4 DO NOTE .1 <- TROLL.PROP
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 TROLL.PROP EQ #1
	DO FORGET #1
	DO .1 <- #33
	DO .2 <- #3
	DO (55501) NEXT DO NOTE PSPEAK
	DO ,61003SUB#33#4 <- #0 DO NOTE TROLL.PROP <- #0
	DO .1 <- #33
	DO .2 <- ,61000SUB#1
	DO (30001) NEXT DO NOTE MOVE TROLL TO LOC
	DO ,60007SUB#33#1 <- #117 DO NOTE TROLL.PLAC <- #117
	DO ,60007SUB#33#2 <- #122 DO NOTE TROLL.FIXD <- #122
	DO .1 <- #34
	DO .2 <- #0
	DO (30001) NEXT DO NOTE MOVE TROLL2 TO #0
	DO ,60007SUB#34#1 <- #0 DO NOTE TROLL2.PLAC <- #0
	DO ,60007SUB#34#2 <- #0 DO NOTE TROLL2.FIXD <- #0
	DO .1 <- #32
	DO .2 <- ,61003SUB#32#1
	DO (30001) NEXT DO NOTE JUGGLE CHASM: MOVE CHASM TO CHASM.LOC
	DO ,61000SUB#2 <- ,61000SUB#1 DO NOTE NEWLOC <- LOC
	DO (5) NEXT
	PLEASE STOP

	PLEASE DO NON-APPLICABLE MOTION.  VARIOUS MESSAGES DEPENDING ON WORD
	       GIVEN.
(591)	DO FORGET #1
	DO NOTE
	   IF .5 IS #43 #44 #45 #46 #47 #48 #49 #50 #29 #30, RSPEAK(#9)
	   IF .5 IS #7 #36 #37, RSPEAK(#10)
	   IF .5 IS #11 #19, RSPEAK(#11)
	   IF .5 IS #62 #65, RSPEAK(#42)
	   IF .5 IS #17, RSPEAK(#80)
	   ELSE, RSPEAK(#12).
	DO .2 <- .5
	DO .3 <- #9
	DO .1 <- #43 DO (592) NEXT
	DO .1 <- #44 DO (592) NEXT
	DO .1 <- #45 DO (592) NEXT
	DO .1 <- #46 DO (592) NEXT
	DO .1 <- #47 DO (592) NEXT
	DO .1 <- #48 DO (592) NEXT
	DO .1 <- #49 DO (592) NEXT
	DO .1 <- #50 DO (592) NEXT
	DO .1 <- #29 DO (592) NEXT
	DO .1 <- #30 DO (592) NEXT
	DO .3 <- #10
	DO .1 <- #7  DO (592) NEXT
	DO .1 <- #36 DO (592) NEXT
	DO .1 <- #37 DO (592) NEXT
	DO .3 <- #11
	DO .1 <- #11 DO (592) NEXT
	DO .1 <- #19 DO (592) NEXT
	DO .3 <- #42
	DO .1 <- #62 DO (592) NEXT
	DO .1 <- #65 DO (592) NEXT
	DO .3 <- #80
	DO .1 <- #17 DO (592) NEXT
	DO .3 <- #12
	DO (593) NEXT
	PLEASE STOP
(592)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (593) NEXT
	PLEASE STOP
(593)	DO FORGET #1
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO (5) NEXT
	PLEASE STOP

	PLEASE ANALYSE A VERB.  REMEMBER WHAT IT WAS, GO BACK FOR OBJECT IF
	       SECOND WORD UNLESS VERB IS "SAY", WHICH SNARFS ARBITRARY
	       SECOND WORD.
(601)	DO FORGET #1
	DO .1 <- ,51001SUB#3#4 DO NOTE .1 <- CURRENT WORD INDEX INDEX
	DO ,61000SUB#21 <- ',60004SUB",51001SUB#3.1"#3'~#255 DO NOTE VERB <- VOCAB(CURRENT WORD).VALUE
	DO NOTE IF CURRENT WORD INDEX INDEX EQ #1 AND ,51001SUB#2#1 NE #0 AND VERB NE SAY, CURRENT WORD INDEX INDEX <- #2, GOTO (400).
	DO .2 <- #1
	DO (602) NEXT
	DO (605) NEXT
	PLEASE STOP
(602)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ,51001SUB#2#1 DO NOTE .1 <- WD2
	DO .2 <- #0
	DO (603) NEXT
	DO .1 <- ,61000SUB#21 DO NOTE .1 <- VERB
	DO .2 <- #3
	DO (604) NEXT
	DO ,51001SUB#3#4 <- #2 DO NOTE CURRENT WORD INDEX INDEX <- #2
	DO (400) NEXT DO NOTE GO BACK FOR OBJECT
	PLEASE STOP
(603)	DO (40011) NEXT DO NOTE .1=.2 WD=''
	DO FORGET #1
	DO (605) NEXT
	PLEASE STOP
(604)	DO (40011) NEXT DO NOTE .1=.2 VERB=SAY
	DO FORGET #1
	DO ,51001SUB#3#4 <- #2 DO NOTE CURRENT WORD INDEX INDEX <- #2
	DO (10101) NEXT
	PLEASE STOP
(605)	DO FORGET #1
	DO NOTE ANALYSE AN INTRANSITIVE VERB (IE, NO OBJECT GIVEN YET).
	DO .1 <- ',60004SUB",51001SUB#3#1"#3'~#255 DO NOTE VOCAB(VERB).VALUE
	DO .2 <- #1 DO (611) NEXT DO NOTE TAKE
	DO .2 <- #2 DO (612) NEXT DO NOTE DROP
	DO .2 <- #3 DO (612) NEXT DO NOTE SAY
	DO .2 <- #4 DO (613) NEXT DO NOTE OPEN
	DO .2 <- #5 DO (614) NEXT DO NOTE NOTH
	DO .2 <- #6 DO (613) NEXT DO NOTE LOCK
	DO .2 <- #7 DO (615) NEXT DO NOTE ON
	DO .2 <- #8 DO (616) NEXT DO NOTE OFF
	DO .2 <- #9 DO (612) NEXT DO NOTE WAVE
	DO .2 <- #10 DO (612) NEXT DO NOTE CALM
	DO .2 <- #11 DO (617) NEXT DO NOTE WALK
	DO .2 <- #12 DO (618) NEXT DO NOTE KILL
	DO .2 <- #13 DO (619) NEXT DO NOTE POUR
	DO .2 <- #14 DO (620) NEXT DO NOTE EAT
	DO .2 <- #15 DO (621) NEXT DO NOTE DRNK
	DO .2 <- #16 DO (612) NEXT DO NOTE RUB
	DO .2 <- #17 DO (612) NEXT DO NOTE TOSS
	DO .2 <- #18 DO (622) NEXT DO NOTE QUIT
	DO .2 <- #19 DO (612) NEXT DO NOTE FIND
	DO .2 <- #20 DO (623) NEXT DO NOTE INVN
	DO .2 <- #21 DO (612) NEXT DO NOTE FEED
	DO .2 <- #22 DO (624) NEXT DO NOTE FILL
	DO .2 <- #23 DO (625) NEXT DO NOTE BLST
	DO .2 <- #24 DO (626) NEXT DO NOTE SCOR
	DO .2 <- #25 DO (627) NEXT DO NOTE FOO
	DO .2 <- #26 DO (628) NEXT DO NOTE BRF
	DO .2 <- #27 DO (629) NEXT DO NOTE READ
	DO .2 <- #28 DO (612) NEXT DO NOTE BREK
	DO .2 <- #29 DO (612) NEXT DO NOTE WAKE
	DO .2 <- #30 DO (630) NEXT DO NOTE SUSP
	DO .2 <- #31 DO (631) NEXT DO NOTE HOUR
	PLEASE BUG UNRECOGNIZED INTRANSITIVE ACTION VERB

(611)	DO (40011) NEXT DO NOTE .1=.2 TAKE
	DO FORGET #1
	DO (10121) NEXT
	PLEASE STOP
(612)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE VERB THAT NEEDS OBJECT
	DO FORGET #1
	DO NOTE "<VERB> WHAT?"
	DO .1 <- "',51001SUB#3#4'~#2"¢#0 DO NOTE .1 <- (CURRENT WORD INDEX INDEX - 1)*2
	DO .2 <- #7
	DO (1000) NEXT
	DO .2 <- ,51001SUB#3.3
	DO .1 <- .3
	DO (1020) NEXT
	DO .3 <- .1
	DO .1 <- .2
	DO .2 <- ,51001SUB#3.3
	DO (57301) NEXT
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#87  DO,60006SUB#13#4<-#72
	DO,60006SUB#13#5<-#65  DO,60006SUB#13#6<-#84  DO,60006SUB#13#7<-#63
	DO,60006SUB#13#8<-#10  DO,60006SUB#13#9<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (251) NEXT
	PLEASE STOP
(613)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE OPEN/LOCK
	DO FORGET #1
	DO (10171) NEXT
	PLEASE STOP
(614)	DO (40011) NEXT DO NOTE .1=.2 NOTHING
	DO FORGET #1
	DO .1 <- #54
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(615)	DO (40011) NEXT DO NOTE .1=.2 ON
	DO FORGET #1
	DO (10151) NEXT
	PLEASE STOP
(616)	DO (40011) NEXT DO NOTE .1=.2 OFF
	DO FORGET #1
	DO (10161) NEXT
	PLEASE STOP
(617)	DO (40011) NEXT DO NOTE .1=.2 ACTION VERB DEFAULT
	DO FORGET #1
	DO .1 <- ,60008SUB.1 DO NOTE .1 <- ACTSPK(VERB).
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(618)	DO (40011) NEXT DO NOTE .1=.2 KILL
	DO FORGET #1
	DO (10271) NEXT
	PLEASE STOP
(619)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE POUR
	DO FORGET #1
	DO .1 <- #20 DO (30101) NEXT DO NOTE .1 <- TOTING(BOTTLE).
	DO .2 <- #0
	DO (612) NEXT DO NOTE TOTING(BOTTLE).
	DO ,61000SUB#22 <- #20 DO NOTE OBJECT <- BOTTLE
	DO (10221) NEXT
	PLEASE STOP
(620)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE EAT
	DO FORGET #1
	DO [...INTRANSITIVE EAT]
	DO (241) NEXT
	PLEASE STOP
(621)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE RUB
	DO FORGET #1
	DO [...INTRANSITIVE RUB]
	DO (241) NEXT
	PLEASE STOP
(622)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE QUIT
	DO FORGET #1
	DO (10047) NEXT
	DO (241) NEXT
	PLEASE STOP
(623)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE INVN
	DO FORGET #1
	DO (10141) NEXT
	PLEASE STOP
(624)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE FILL
	DO FORGET #1
	DO (10231) NEXT
	PLEASE STOP
(625)	DO (40011) NEXT DO NOTE .1=.2 BLST
	DO FORGET #1
	DO (10321) NEXT
	PLEASE STOP
(626)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE SCOR
	DO FORGET #1
	DO,60006SUB#13#2<-#73  DO,60006SUB#13#3<-#70  DO,60006SUB#13#4<-#32
	DO,60006SUB#13#5<-#89  DO,60006SUB#13#6<-#79  DO,60006SUB#13#7<-#85
	DO,60006SUB#13#8<-#32  DO,60006SUB#13#9<-#87  DO,60006SUB#13#10<-#69
	DO,60006SUB#13#11<-#82 DO,60006SUB#13#12<-#69 DO,60006SUB#13#13<-#32
	DO,60006SUB#13#14<-#84 DO,60006SUB#13#15<-#79 DO,60006SUB#13#16<-#32
	DO,60006SUB#13#17<-#81 DO,60006SUB#13#18<-#85 DO,60006SUB#13#19<-#73
	DO,60006SUB#13#20<-#84 DO,60006SUB#13#21<-#32 DO,60006SUB#13#22<-#78
	DO,60006SUB#13#23<-#79 DO,60006SUB#13#24<-#87 DO,60006SUB#13#25<-#44
	DO,60006SUB#13#26<-#32 DO,60006SUB#13#27<-#89 DO,60006SUB#13#28<-#79
	DO,60006SUB#13#29<-#85 DO,60006SUB#13#30<-#32 DO,60006SUB#13#31<-#87
	DO,60006SUB#13#32<-#79 DO,60006SUB#13#33<-#85 DO,60006SUB#13#34<-#76
	DO,60006SUB#13#35<-#68 DO,60006SUB#13#36<-#32 DO,60006SUB#13#37<-#83
	DO,60006SUB#13#38<-#67 DO,60006SUB#13#39<-#79 DO,60006SUB#13#40<-#82
	DO,60006SUB#13#41<-#69 DO,60006SUB#13#42<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO ,61000SUB#28 <- #1 DO NOTE SCORNG <- #1
	DO (10061) NEXT DO NOTE .1 <- SCORE
	DO ,61000SUB#28 <- #0 DO NOTE SCORNG <- #0
	DO .2 <- #1000
	DO (57201) NEXT DO NOTE FORMAT INTEGER
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#79  DO,60006SUB#13#4<-#85
	DO,60006SUB#13#5<-#84  DO,60006SUB#13#6<-#32  DO,60006SUB#13#7<-#79
	DO,60006SUB#13#8<-#70  DO,60006SUB#13#9<-#32  DO,60006SUB#13#10<-#65
	DO,60006SUB#13#11<-#32 DO,60006SUB#13#12<-#80 DO,60006SUB#13#13<-#79
	DO,60006SUB#13#14<-#83 DO,60006SUB#13#15<-#83 DO,60006SUB#13#16<-#73
	DO,60006SUB#13#17<-#66 DO,60006SUB#13#18<-#76 DO,60006SUB#13#19<-#69
	DO,60006SUB#13#20<-#32 DO,60006SUB#13#21<-#51 DO,60006SUB#13#22<-#53
	DO,60006SUB#13#23<-#48 DO,60006SUB#13#24<-#46 DO,60006SUB#13#25<-#10
	DO,60006SUB#13#26<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .1 <- #143
	DO (55601) NEXT DO NOTE RSPEAK
	DO (57101) NEXT DO NOTE .1 <- YES().
	DO STASH .1
	DO .1 <- #54
	DO (55601) NEXT DO NOTE RSPEAK
	DO RETRIEVE .1
	DO .2 <- #1
	DO (10048) NEXT
	DO (241) NEXT
	PLEASE STOP
(627)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE FOO
	DO FORGET #1
	DO (10261) NEXT
	PLEASE STOP
(628)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE BRF
	DO FORGET #1
	DO ,61000SUB#8 <- #3 DO NOTE DETAIL <- #3
	DO ,61000SUB#9 <- #10000 DO NOTE ABBNUM <- #10000
	DO .1 <- #156
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(629)	DO (40011) NEXT DO NOTE .1=.2 READ
	DO FORGET #1
	DO (10291) NEXT
	PLEASE STOP
(630)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE SUSP
	DO FORGET #1
	DO .1 <- #31
	DO (56201) NEXT DO NOTE MSPEAK
	DO (241) NEXT
	PLEASE STOP
(631)	DO (40011) NEXT DO NOTE .1=.2 INTRANSITIVE HOUR
	DO FORGET #1
	DO .1 <- #6
	DO (56201) NEXT DO NOTE MSPEAK
	DO NOTE "MON - FRI: OPEN ALL DAY"
	DO,60006SUB#13#2<-#77  DO,60006SUB#13#3<-#79  DO,60006SUB#13#4<-#78
	DO,60006SUB#13#5<-#32  DO,60006SUB#13#6<-#45  DO,60006SUB#13#7<-#32
	DO,60006SUB#13#8<-#70  DO,60006SUB#13#9<-#82  DO,60006SUB#13#10<-#73
	DO,60006SUB#13#11<-#58 DO,60006SUB#13#12<-#32 DO,60006SUB#13#13<-#79
	DO,60006SUB#13#14<-#80 DO,60006SUB#13#15<-#69 DO,60006SUB#13#16<-#78
	DO,60006SUB#13#17<-#32 DO,60006SUB#13#18<-#65 DO,60006SUB#13#19<-#76
	DO,60006SUB#13#20<-#76 DO,60006SUB#13#21<-#32 DO,60006SUB#13#22<-#68
	DO,60006SUB#13#23<-#65 DO,60006SUB#13#24<-#89 DO,60006SUB#13#25<-#10
	DO,60006SUB#13#26<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO NOTE "SAT - SUN: OPEN ALL DAY"
	DO,60006SUB#13#2<-#83  DO,60006SUB#13#3<-#65  DO,60006SUB#13#4<-#84
	DO,60006SUB#13#8<-#83  DO,60006SUB#13#9<-#85  DO,60006SUB#13#10<-#78
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (241) NEXT
	PLEASE STOP

	PLEASE ANALYSE A TRANSITIVE VERB.
(641)	DO FORGET #1
	DO NOT [TRANSITIVE VERB] ,61000SUB#21
	DO NOT [OBJ] ,61000SUB#22
	DO NOTE ANALYSE AN INTRANSITIVE VERB (IE, NO OBJECT GIVEN YET).
	DO .1 <- ,61000SUB#21 DO NOTE .1 <- VERB
	DO .2 <- #1 DO (611) NEXT DO NOTE TAKE
	DO .2 <- #2 DO (652) NEXT DO NOTE DROP
	DO .2 <- #3 DO (653) NEXT DO NOTE SAY
	DO .2 <- #4 DO (654) NEXT DO NOTE OPEN
	DO .2 <- #5 DO (614) NEXT DO NOTE NOTH
	DO .2 <- #6 DO (654) NEXT DO NOTE LOCK
	DO .2 <- #7 DO (615) NEXT DO NOTE ON
	DO .2 <- #8 DO (616) NEXT DO NOTE OFF
	DO .2 <- #9 DO (657) NEXT DO NOTE WAVE
	DO .2 <- #10 DO (617) NEXT DO NOTE CALM
	DO .2 <- #11 DO (617) NEXT DO NOTE WALK
	DO .2 <- #12 DO (618) NEXT DO NOTE KILL
	DO .2 <- #13 DO (659) NEXT DO NOTE POUR
	DO .2 <- #14 DO (660) NEXT DO NOTE EAT
	DO .2 <- #15 DO (661) NEXT DO NOTE DRNK
	DO .2 <- #16 DO (662) NEXT DO NOTE RUB
	DO .2 <- #17 DO (663) NEXT DO NOTE TOSS
	DO .2 <- #18 DO (617) NEXT DO NOTE QUIT
	DO .2 <- #19 DO (664) NEXT DO NOTE FIND
	DO .2 <- #20 DO (664) NEXT DO NOTE INVN
	DO .2 <- #21 DO (665) NEXT DO NOTE FEED
	DO .2 <- #22 DO (666) NEXT DO NOTE FILL
	DO .2 <- #23 DO (625) NEXT DO NOTE BLST
	DO .2 <- #24 DO (617) NEXT DO NOTE SCOR
	DO .2 <- #25 DO (617) NEXT DO NOTE FOO
	DO .2 <- #26 DO (617) NEXT DO NOTE BRF
	DO .2 <- #27 DO (629) NEXT DO NOTE READ
	DO .2 <- #28 DO (669) NEXT DO NOTE BREK
	DO .2 <- #29 DO (670) NEXT DO NOTE WAKE
	DO .2 <- #30 DO (617) NEXT DO NOTE SUSP
	DO .2 <- #31 DO (617) NEXT DO NOTE HOUR
	PLEASE BUG UNRECOGNIZED TRANSITIVE ACTION VERB

(652)	DO (40011) NEXT DO NOTE .1=.2 DROP
	DO FORGET #1
	DO (10201) NEXT
	PLEASE STOP
(653)	DO (40011) NEXT DO NOTE .1=.2 SAY
	DO FORGET #1
	DO [...SAY] ,61000SUB#22
	DO (241) NEXT
	PLEASE STOP
(654)	DO (40011) NEXT DO NOTE .1=.2 OPEN/LOCK
	DO FORGET #1
	DO .1 <- #28
	DO (10181) NEXT
	PLEASE STOP
(657)	DO (40011) NEXT DO NOTE .1=.2 WAVE
	DO FORGET #1
	DO (10311) NEXT
	PLEASE STOP
(659)	DO (40011) NEXT DO NOTE .1=.2 POUR
	DO FORGET #1
	DO (10221) NEXT
	PLEASE STOP
(660)	DO (40011) NEXT DO NOTE .1=.2 EAT
	DO FORGET #1
	DO [...EAT] ,61000SUB#22
	DO (241) NEXT
	PLEASE STOP
(661)	DO (40011) NEXT DO NOTE .1=.2 DRNK
	DO FORGET #1
	DO [...DRNK] ,61000SUB#22
	DO (241) NEXT
	PLEASE STOP
(662)	DO (40011) NEXT DO NOTE .1=.2 RUB
	DO FORGET #1
	DO [...RUB] ,61000SUB#22
	DO (241) NEXT
	PLEASE STOP
(663)	DO (40011) NEXT DO NOTE .1=.2 TOSS
	DO FORGET #1
	DO (10241) NEXT
	PLEASE STOP
(664)	DO (40011) NEXT DO NOTE .1=.2 FIND/INVN
	DO FORGET #1
	DO [...FIND/INVN] ,61000SUB#22
	DO (241) NEXT
	PLEASE STOP
(665)	DO (40011) NEXT DO NOTE .1=.2 FEED
	DO FORGET #1
	DO (10251) NEXT
	PLEASE STOP
(666)	DO (40011) NEXT DO NOTE .1=.2 FILL
	DO FORGET #1
	DO (10231) NEXT
	PLEASE STOP
(669)	DO (40011) NEXT DO NOTE .1=.2 BREK
	DO FORGET #1
	DO [...BREK] ,61000SUB#22
	DO (241) NEXT
	PLEASE STOP
(670)	DO (40011) NEXT DO NOTE .1=.2 WAKE
	DO FORGET #1
	DO [...WAKE] ,61000SUB#22
	DO (241) NEXT
	PLEASE STOP


	PLEASE ANALYSE AN OBJECT WORD.  SEE IF THE THING IS HERE, WHETHER WE'VE
	       GOT A VERB YET, AND SO ON.  OBJECT MUST BE HERE UNLESS VERB IS
	       "FIND" OR "INVENT(ORY)" (AND NO NEW VERB YET TO BE ANALYSED).
	       WATER AND OIL ARE ALSO FUNNY, SINCE THEY ARE NEVER ACTUALLY
	       DROPPED AT ANY LOCATION, BUT MIGHT BE HERE INSIDE THE BOTTLE OR
	       AS A FEATURE OF THE LOCATION.
(701)	DO FORGET #1
	DO .1 <- ,51001SUB#3#4 DO NOTE .1 <- CURRENT WORD INDEX INDEX
	DO ,61000SUB#22 <- ',60004SUB",51001SUB#3.1"#3'~#255 DO NOTE OBJ <- VOCAB(CURRENT WORD).VALUE
	DO (702) NEXT
	PLEASE STOP
(702)	DO FORGET #1
	DO (709) NEXT DO NOTE CHECK FOR ROD2
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO (30111) NEXT DO NOTE .1 <- HERE(OBJ).
	DO .2 <- #0
	DO (705) NEXT
	DO (703) NEXT
	PLEASE STOP
(703)	DO FORGET #1
	DO .1 <- ,61000SUB#21 DO NOTE .1 <- VERB
	DO .2 <- #0
	DO (704) NEXT
	DO .2 <- #19 DO NOTE FIND
	DO (704) NEXT
	DO .2 <- #20 DO NOTE INVEN
	DO (704) NEXT
	DO (641) NEXT
	PLEASE STOP
(704)	DO (40011) NEXT DO NOTE .1=.2 VERB EQ #0
	DO FORGET #1
	DO NOTE "WHAT D0 YOU WANT TO D0 WITH THE <OBJ>?"
	DO,60006SUB#13#2<-#87  DO,60006SUB#13#3<-#72  DO,60006SUB#13#4<-#65
	DO,60006SUB#13#5<-#84  DO,60006SUB#13#6<-#32  DO,60006SUB#13#7<-#68
	DO,60006SUB#13#8<-#79  DO,60006SUB#13#9<-#32  DO,60006SUB#13#10<-#89
	DO,60006SUB#13#11<-#79 DO,60006SUB#13#12<-#85 DO,60006SUB#13#13<-#32
	DO,60006SUB#13#14<-#87 DO,60006SUB#13#15<-#65 DO,60006SUB#13#16<-#78
	DO,60006SUB#13#17<-#84 DO,60006SUB#13#18<-#32 DO,60006SUB#13#19<-#84
	DO,60006SUB#13#20<-#79 DO,60006SUB#13#21<-#32 DO,60006SUB#13#22<-#68
	DO,60006SUB#13#23<-#79 DO,60006SUB#13#24<-#32 DO,60006SUB#13#25<-#87
	DO,60006SUB#13#26<-#73 DO,60006SUB#13#27<-#84 DO,60006SUB#13#28<-#72
	DO,60006SUB#13#29<-#32 DO,60006SUB#13#30<-#84 DO,60006SUB#13#31<-#72
	DO,60006SUB#13#32<-#69 DO,60006SUB#13#33<-#32 DO,60006SUB#13#34<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .1 <- "',51001SUB#3#4'~#2"¢#0 DO NOTE .1 <- (CURRENT WORD INDEX INDEX - 1)*2
	DO .2 <- #7
	DO (1000) NEXT
	DO .2 <- ,51001SUB#3.3
	DO .1 <- .3
	DO (1020) NEXT
	DO .3 <- .1
	DO .1 <- .2
	DO .2 <- ,51001SUB#3.3
	DO (57301) NEXT
	DO,60006SUB#13#2<-#63  DO,60006SUB#13#3<-#10  DO,60006SUB#13#4<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (251) NEXT
	PLEASE STOP
(705)	DO (40011) NEXT DO NOTE .1=.2 NOT HERE(OBJ).
	DO FORGET #1
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #3 DO NOTE GRATE
	DO (706) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #17 DO NOTE DWARF
	DO (710) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #18 DO NOTE KNIFE
	DO (715) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #21 DO NOTE WATER
	DO (720) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #22 DO NOTE OIL
	DO (725) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #24 DO NOTE PLANT
	DO (730) NEXT
	DO FORGET #1
	DO (736) NEXT
	PLEASE STOP
(706)	DO (40011) NEXT DO NOTE .1=.2 GRATE
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #1 DO (707) NEXT
	DO .2 <- #4 DO (707) NEXT
	DO .2 <- #7 DO (707) NEXT
	DO .2 <- #10 DO (708) NEXT
	DO .2 <- #11 DO (708) NEXT
	DO .2 <- #12 DO (708) NEXT
	DO .2 <- #13 DO (708) NEXT
	DO .2 <- #14 DO (708) NEXT
	DO RESUME #1
(707)	DO (40011) NEXT DO NOTE .1=.2 GOTO GRATE-DEPRESSION
	DO FORGET #2
	DO .1 <- #63
	DO (501) NEXT
	PLEASE STOP
(708)	DO (40011) NEXT DO NOTE .1=.2 GOTO GRATE-ENTRANCE
	DO FORGET #2
	DO .1 <- #64
	DO (501) NEXT
	PLEASE STOP
(709)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #5 DO NOTE ROD
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ ROD
	DO .1 <- #6 DO NOTE .1 <- ROD2
	DO (30111) NEXT DO NOTE .1 <- HERE(ROD2).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 HERE(ROD2).
	DO ,61000SUB#22 <- #6 DO NOTE OBJ <- ROD2
	DO RESUME #1
	PLEASE STOP
(710)	DO (40011) NEXT DO NOTE .1=.2 DWARF
	DO .1 <- ,61000SUB#10 DO NOTE .1 <- DFLAG
	DO .2 <- #2
	DO (711) NEXT
	DO RESUME #1
	PLEASE STOP
(711)	DO (40011) NEXT DO NOTE .1=.2 DFLAG EQ #2
	DO FORGET #1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- ,61002SUB#1#1 DO (712) NEXT
	DO .2 <- ,61002SUB#2#1 DO (712) NEXT
	DO .2 <- ,61002SUB#3#1 DO (712) NEXT
	DO .2 <- ,61002SUB#4#1 DO (712) NEXT
	DO .2 <- ,61002SUB#5#1 DO (712) NEXT
	DO RESUME #1
	PLEASE STOP
(712)	DO (40011) NEXT DO NOTE .1=.2 OBJECT IS HERE VIRTUALLY
	DO FORGET #2
	DO (703) NEXT
	PLEASE STOP
(715)	DO (40011) NEXT DO NOTE .1=.2 KNIFE
	DO [...KNIFE...]
	DO RESUME #1
	PLEASE STOP
(720)	DO (40011) NEXT DO NOTE .1=.2 WATER
	DO .1 <- ",60009SUB',61000SUB#1'"~#6
	DO .2 <- #2
	DO (712) NEXT DO NOTE IF LOC.PROP IS WATER
	DO NOTE HERE(BOTTLE) AND BOTTLE.PROP EQ #0
	DO .1 <- #20 DO (30101) NEXT DO NOTE .1 <- HERE(BOTTLE).
	DO .3 <- .1 DO .1 <- ,61003SUB#20#4 DO .2 <- #0 DO .4 <- #0
	DO (40021) NEXT DO NOTE IF BOTTLE.PROP EQ #0, .1 <- HERE(BOTTLE), ELSE .1 <- #0
	DO .2 <- #1
	DO (712) NEXT DO NOTE IF HERE(BOTTLE) AND BOTTLE.PROP EQ #0
	DO RESUME #1
	PLEASE STOP
(725)	DO (40011) NEXT DO NOTE .1=.2 OIL
	DO .1 <- ",60009SUB',61000SUB#1'"~#6
	DO .2 <- #3
	DO (712) NEXT DO NOTE IF LOC.PROP IS OIL
	DO NOTE HERE(BOTTLE) AND BOTTLE.PROP EQ #2
	DO .1 <- #20 DO (30101) NEXT DO NOTE .1 <- HERE(BOTTLE).
	DO .3 <- .1 DO .1 <- ,61003SUB#20#4 DO .2 <- #2 DO .4 <- #0
	DO (40021) NEXT DO NOTE IF BOTTLE.PROP EQ #2, .1 <- HERE(BOTTLE), ELSE .1 <- #0
	DO .2 <- #1
	DO (712) NEXT DO NOTE IF HERE(BOTTLE) AND BOTTLE.PROP EQ #2
	DO RESUME #1
	PLEASE STOP
(730)	DO (40011) NEXT DO NOTE .1=.2 PLANT
	DO [...PLANT...]
	DO RESUME #1
	PLEASE STOP
(735)	DO (40011) NEXT DO NOTE .1=.2
	DO NOT STOP, FALLTHROUGH
(736)	DO FORGET #1
	DO NOTE "I SEE NO <OBJ> HERE."
	DO,60006SUB#13#2<-#73  DO,60006SUB#13#3<-#32  DO,60006SUB#13#4<-#83
	DO,60006SUB#13#5<-#69  DO,60006SUB#13#6<-#69  DO,60006SUB#13#7<-#32
	DO,60006SUB#13#8<-#78  DO,60006SUB#13#9<-#79  DO,60006SUB#13#10<-#32
	DO,60006SUB#13#11<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .1 <- "',51001SUB#3#4'~#2"¢#0 DO NOTE .1 <- (CURRENT WORD INDEX INDEX - 1)*2
	DO .2 <- #7
	DO (1000) NEXT
	DO .2 <- ,51001SUB#3.3
	DO .1 <- .3
	DO (1020) NEXT
	DO .3 <- .1
	DO .1 <- .2
	DO .2 <- ,51001SUB#3.3
	DO (57301) NEXT
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#72  DO,60006SUB#13#4<-#69
	DO,60006SUB#13#5<-#82  DO,60006SUB#13#6<-#69  DO,60006SUB#13#7<-#46
	DO,60006SUB#13#8<-#10  DO,60006SUB#13#9<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (241) NEXT
	PLEASE STOP

	PLEASE NOTE "YOU'RE DEAD, JIM."
	       IF THE CURRENT LOC IS ZERO, IT MEANS THE CLOWN GOT HIMSELF
	       KILLED.  WE'LL ALLOW THIS MAXDIE TIMES.  MAXDIE IS AUTOMATICALLY
	       SET BASED ON THE NUMBER OF SNIDE MESSAGES AVAILABLE.  EACH DEATH
	       RESULTS IN A MESSAGE (81, 83, ETC.)  WHICH OFFERS REINCARNATION;
	       IF ACCEPTED, THIS RESULTS IN MESSAGE 82, 84, ETC.  THE LAST
	       TIME, IF HE WANTS ANOTHER CHANCE, HE GETS A SNIDE REMARK AS WE
	       EXIT.  WHEN REINCARNATED, ALL OBJECTS BEING CARRIED GET DROPPED
	       AT OLDLC2 (PRESUMABLY THE LAST PLACE PRIOR TO BEING KILLED)
	       WITHOUT CHANGE OF PROPS.  THE LOOP RUNS BACKWARDS TO ASSURE THAT
	       THE BIRD IS DROPPED BEFORE THE CAGE.  (THIS KLUGE COULD BE
	       CHANGED ONCE WE'RE SURE ALL REFERENCES TO BIRD AND CAGE ARE D0NE
	       BY KEYWORDS.)  THE LAMP IS A SPECIAL CASE (IT WOULDN'T D0 TO
	       LEAVE IT IN THE CAVE).  IT IS TURNED OFF AND LEFT OUTSIDE THE
	       BUILDING (ONLY IF HE WAS CARRYING IT, OF COURSE).  HE HIMSELF
	       IS LEFT INSIDE THE BUILDING (AND HEAVEN HELP HIM IF HE TRIES TO
	       XYZZY BACK INTO THE CAVE WITHOUT THE LAMP!).  OLDLOC IS ZAPPED
	       SO HE CAN'T JUST "RETREAT".
(10001)	DO FORGET #1
	DO .1 <- ,61000SUB#19 DO NOTE .1 <- CLOSNG
	DO .2 <- #1
	DO (10049) NEXT
	DO .1 <- ,61000SUB#11 DO NOTE .1 <- NUMDIE
	DO (1020) NEXT
	DO ,61000SUB#11 <- .1 DO NOTE NUMDIE <- .1
	DO .2 <- .1
	DO (1000) NEXT
	DO .1 <- .3
	DO .2 <- #81
	DO (1000) NEXT
	DO .1 <- ,60006SUB.3#1
	DO .2 <- #0
	DO (10002) NEXT DO NOTE CHECK IF NEXT RESURRECTION MESSAGE IS EMPTY
	DO (10003) NEXT
	PLEASE STOP
(10002)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (10051) NEXT
	PLEASE STOP
(10003)	DO FORGET #1
	DO .1 <- .3
	DO .2 <- #2
	DO (1010) NEXT
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- #2
	DO (57101) NEXT DO NOTE .1 <- YES().
	DO .2 <- #1
	DO (10004) NEXT
	DO .1 <- #54
	DO (55601) NEXT DO NOTE RSPEAK
	DO (10051) NEXT
	PLEASE STOP
(10004)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- .3
	DO (1020) NEXT
	DO (55601) NEXT DO NOTE RSPEAK
	DO ... NOT IMPLEMENTED: RESURRECTION
	PLEASE STOP

	PLEASE QUIT IF CLOSED, .3 HAS ARBITRARY MESSAGE INDEX IF QUITTING
(10046)	DO .1 <- ,61000SUB#26 DO NOTE .1 <- CLOSED
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO (10051) NEXT
	PLEASE STOP

	PLEASE ASK REALLY QUIT
(10047)	DO .1 <- #22
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- #1
	DO (57101) NEXT DO NOTE .1 <- YES().
	DO STASH .1
	DO .1 <- #54
	DO (55601) NEXT DO NOTE RSPEAK
	DO RETRIEVE .1
	DO .2 <- #1
	DO (10048) NEXT
	DO RESUME #1
	PLEASE STOP
(10048)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #2
	DO ,61000SUB#27 <- #1 DO NOTE GAVEUP <- #1
	DO (10051) NEXT
	PLEASE STOP

	PLEASE IF DIED DURING CLOSING TIME, NO RESURRECTION
(10049)	DO .1 <- ,61000SUB#19 DO NOTE .1 <- CLOSNG
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #131
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- ,61000SUB#11 DO NOTE .1 <- NUMDIE
	DO (1020) NEXT
	DO ,61000SUB#11 <- .1 DO NOTE NUMDIE <- .1
	DO (10051) NEXT
	PLEASE STOP

	PLEASE EXIT
(10051)	DO FORGET #1
	DO ,61000SUB#28 <- #0 DO NOTE SCORNG <- #0
	DO (10061) NEXT DO NOTE .1 <- SCORE
	DO .4 <- .1
	DO NOTE THAT SHOULD BE GOOD ENOUGH.  LET'S TELL HIM ALL ABOUT IT.
	DO,60006SUB#13#2<-#10  DO,60006SUB#13#3<-#89  DO,60006SUB#13#4<-#79
	DO,60006SUB#13#5<-#85  DO,60006SUB#13#6<-#32  DO,60006SUB#13#7<-#83
	DO,60006SUB#13#8<-#67  DO,60006SUB#13#9<-#79  DO,60006SUB#13#10<-#82
	DO,60006SUB#13#11<-#69 DO,60006SUB#13#12<-#68 DO,60006SUB#13#13<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .1 <- .4
	DO .2 <- #1000
	DO (57201) NEXT DO NOTE FORMAT INTEGER
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#79  DO,60006SUB#13#4<-#85
	DO,60006SUB#13#5<-#84  DO,60006SUB#13#6<-#32  DO,60006SUB#13#7<-#79
	DO,60006SUB#13#8<-#70  DO,60006SUB#13#9<-#32  DO,60006SUB#13#10<-#65
	DO,60006SUB#13#11<-#32 DO,60006SUB#13#12<-#80 DO,60006SUB#13#13<-#79
	DO,60006SUB#13#14<-#83 DO,60006SUB#13#15<-#83 DO,60006SUB#13#16<-#73
	DO,60006SUB#13#17<-#66 DO,60006SUB#13#18<-#76 DO,60006SUB#13#19<-#69
	DO,60006SUB#13#20<-#32 DO,60006SUB#13#21<-#51 DO,60006SUB#13#22<-#53
	DO,60006SUB#13#23<-#48 DO,60006SUB#13#24<-#44 DO,60006SUB#13#25<-#32
	DO,60006SUB#13#26<-#85 DO,60006SUB#13#27<-#83 DO,60006SUB#13#28<-#73
	DO,60006SUB#13#29<-#78 DO,60006SUB#13#30<-#71 DO,60006SUB#13#31<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .1 <- ,61000SUB#4 DO NOTE .1 <- TURNS
	DO .2 <- #10000
	DO (57201) NEXT DO NOTE FORMAT INTEGER
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#84  DO,60006SUB#13#4<-#85
	DO,60006SUB#13#5<-#82  DO,60006SUB#13#6<-#78  DO,60006SUB#13#7<-#83
	DO,60006SUB#13#8<-#46  DO,60006SUB#13#9<-#10  DO,60006SUB#13#10<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .1 <- .4 DO NOTE .1 <- SCORE
	DO (56001) NEXT DO NOTE .1 <- CLASSIFICATION INDEX
	DO,60006SUB#13#2<-#84  DO,60006SUB#13#3<-#79  DO,60006SUB#13#4<-#32
	DO,60006SUB#13#5<-#65  DO,60006SUB#13#6<-#67  DO,60006SUB#13#7<-#72
	DO,60006SUB#13#8<-#73  DO,60006SUB#13#9<-#69  DO,60006SUB#13#10<-#86
	DO,60006SUB#13#11<-#69 DO,60006SUB#13#12<-#32 DO,60006SUB#13#13<-#84
	DO,60006SUB#13#14<-#72 DO,60006SUB#13#15<-#69 DO,60006SUB#13#16<-#32
	DO,60006SUB#13#17<-#78 DO,60006SUB#13#18<-#69 DO,60006SUB#13#19<-#88
	DO,60006SUB#13#20<-#84 DO,60006SUB#13#21<-#32 DO,60006SUB#13#22<-#72
	DO,60006SUB#13#23<-#73 DO,60006SUB#13#24<-#71 DO,60006SUB#13#25<-#72
	DO,60006SUB#13#26<-#69 DO,60006SUB#13#27<-#82 DO,60006SUB#13#28<-#32
	DO,60006SUB#13#29<-#82 DO,60006SUB#13#30<-#65 DO,60006SUB#13#31<-#84
	DO,60006SUB#13#32<-#73 DO,60006SUB#13#33<-#78 DO,60006SUB#13#34<-#71
	DO,60006SUB#13#35<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .2 <- #9 DO NOTE MAX CLASSIFICATION
	DO (10053) NEXT
	DO .1 <- ,60010SUB.1#1
	DO (1020) NEXT
	DO .2 <- .4
	DO (1010) NEXT
	DO,60006SUB#13#2<-#44  DO,60006SUB#13#3<-#32  DO,60006SUB#13#4<-#89
	DO,60006SUB#13#5<-#79  DO,60006SUB#13#6<-#85  DO,60006SUB#13#7<-#32
	DO,60006SUB#13#8<-#78  DO,60006SUB#13#9<-#69  DO,60006SUB#13#10<-#69
	DO,60006SUB#13#11<-#68 DO,60006SUB#13#12<-#0
	DO,60006SUB#13#35<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .1 <- .3
	DO .2 <- #100
	DO (57201) NEXT DO NOTE FORMAT INTEGER
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#77  DO,60006SUB#13#4<-#79
	DO,60006SUB#13#5<-#82  DO,60006SUB#13#6<-#69  DO,60006SUB#13#7<-#32
	DO,60006SUB#13#8<-#80  DO,60006SUB#13#9<-#79  DO,60006SUB#13#10<-#73
	DO,60006SUB#13#11<-#78 DO,60006SUB#13#12<-#84 DO,60006SUB#13#13<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO .2 <- #1
	DO (10052) NEXT
	DO,60006SUB#13#2<-#83  DO,60006SUB#13#3<-#46  DO,60006SUB#13#4<-#10
	DO,60006SUB#13#5<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO GIVE UP
	PLEASE STOP
(10052) DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO,60006SUB#13#2<-#46  DO,60006SUB#13#3<-#10  DO,60006SUB#13#4<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO GIVE UP
	PLEASE STOP
(10053)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO,60006SUB#13#2<-#32  DO,60006SUB#13#3<-#87  DO,60006SUB#13#4<-#79
	DO,60006SUB#13#5<-#85  DO,60006SUB#13#6<-#76  DO,60006SUB#13#7<-#68
	DO,60006SUB#13#8<-#32  DO,60006SUB#13#9<-#66  DO,60006SUB#13#10<-#69
	DO,60006SUB#13#11<-#32 DO,60006SUB#13#12<-#65 DO,60006SUB#13#13<-#32
	DO,60006SUB#13#14<-#78 DO,60006SUB#13#15<-#69 DO,60006SUB#13#16<-#65
	DO,60006SUB#13#17<-#84 DO,60006SUB#13#18<-#32 DO,60006SUB#13#19<-#84
	DO,60006SUB#13#20<-#82 DO,60006SUB#13#21<-#73 DO,60006SUB#13#22<-#67
	DO,60006SUB#13#23<-#75 DO,60006SUB#13#24<-#33 DO,60006SUB#13#25<-#10
	DO,60006SUB#13#26<-#67 DO,60006SUB#13#27<-#79 DO,60006SUB#13#28<-#78
	DO,60006SUB#13#29<-#71 DO,60006SUB#13#30<-#82 DO,60006SUB#13#31<-#65
	DO,60006SUB#13#32<-#84 DO,60006SUB#13#33<-#85 DO,60006SUB#13#34<-#76
	DO,60006SUB#13#35<-#65 DO,60006SUB#13#36<-#84 DO,60006SUB#13#37<-#73
	DO,60006SUB#13#38<-#79 DO,60006SUB#13#39<-#78 DO,60006SUB#13#40<-#83
	DO,60006SUB#13#41<-#33 DO,60006SUB#13#42<-#33 DO,60006SUB#13#43<-#10
	DO,60006SUB#13#44<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO GIVE UP
	PLEASE STOP

	PLEASE .1 <- SCORE
	       THE PRESENT SCORING ALGORITHM IS AS FOLLOWS:
	          OBJECTIVE:          POINTS:        PRESENT TOTAL POSSIBLE:
	       GETTING WELL INTO CAVE   25                    25
	       EACH TREASURE < CHEST    12                    60
	       TREASURE CHEST ITSELF    14                    14
	       EACH TREASURE > CHEST    16                   144
	       SURVIVING             (MAX-NUM)*10             30
	       NOT QUITTING              4                     4
	       REACHING "CLOSNG"        25                    25
	       "CLOSED": QUIT/KILLED    10
	                 KLUTZED        25
	                 WRONG WAY      30
	                 SUCCESS        45                    45
	       CAME TO WITT'S END        1                     1
	       ROUND OUT THE TOTAL       2                     2
                                                    TOTAL:   350
	       (POINTS CAN ALSO BE DEDUCTED FOR USING HINTS.)
(10061)	DO STASH .2 + .3 + .4 + .5 + .6 + .7
	DO .7 <- #0 DO NOTE SCORE
	DO .5 <- #50
	DO .6 <- #10
	DO (10062) NEXT
	PLEASE STOP
	PLEASE FIRST TALLY UP THE TREASURES.  MUST BE IN BUILDING AND NOT
	       BROKEN.  GIVE THE POOR GUY 2 POINTS JUST FOR FINDING EACH
	       TREASURE.
	       FOR .5 IN #50 .. #64
(10062)	DO FORGET #1
	DO .1 <- .5 DO .2 <- #55 DO .3 <- #12 DO .4 <- .6
	DO (40021) NEXT DO .6 <- .1 DO NOTE IF .5 EQ CHEST, .6 <- #12
	DO .1 <- .5 DO .2 <- #56 DO .3 <- #14 DO .4 <- .6
	DO (40021) NEXT DO .6 <- .1 DO NOTE IF .5 EQ CHEST+1, .6 <- #14
	DO .1 <- ,61003SUB.5#5 DO .2 <- #0 DO .3 <- #2 DO .4 <- #0
	DO (40021) NEXT DO NOTE IF OBJ.PROPNEG, .1 <- #0, ELSE .1 <- #2
	DO .2 <- .7 DO (1000) NEXT DO .7 <- .3 DO NOTE SCORE=SCORE + #0 OR #2
	DO .1 <- ,61003SUB.5#1 DO .2 <- #3 DO .3 <- .6 DO .4 <- #0
	DO (40021) NEXT DO NOTE IF OBJ.LOC EQ #3, .1 <- .6, ELSE .1 <- #0
	DO .3 <- .1 DO .1 <- ,61003SUB.5#4 DO .2 <- #0 DO .4 <- #0
	DO (40021) NEXT DO NOTE IF OBJ.PROP EQ #0, .1 <- .6 OR #0, ELSE .1 <- #0
	DO .2 <- .7 DO (1000) NEXT DO .7 <- .3 DO NOTE SCORE=SCORE + #0 OR .6
	DO .1 <- .5
	DO .2 <- #64
	DO (10063) NEXT
	DO (1020) NEXT
	DO .5 <- .1
	DO (10062) NEXT
	PLEASE STOP

	PLEASE NOW LOOK AT HOW HE FINISHED AND HOW FAR HE GOT.  MAXDIE AND
	       NUMDIE TELL US HOW WELL HE SURVIVED.  GAVEUP SAYS WHETHER HE
	       EXITED VIA QUIT.  DFLAG WILL TELL US IF HE EVER GOT SUITABLY
	       DEEP INTO THE CAVE.  CLOSNG STILL INDICATES WHETHER HE REACHED
	       THE ENDGAME.  AND IF HE GOT AS FAR AS "CAVE CLOSED" (INDICATED
	       BY "CLOSED"), THEN BONUS IS ZERO FOR MUNDANE EXITS OR 133, 134,
	       135 IF HE BLEW IT (SO TO SPEAK).
(10063)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ,61000SUB#11 DO .2 <- #1 DO .3 <- #20 DO .4 <- #30
	DO (40021) NEXT DO NOTE NUMDIE EQ #1
	DO .4 <- .1 DO .1 <- ,61000SUB#11 DO .2 <- #2 DO .3 <- #10
	DO (40021) NEXT DO NOTE NUMDIE EQ #2
	DO .4 <- .1 DO .1 <- ,61000SUB#11 DO .2 <- #3 DO .3 <- #0
	DO (40021) NEXT DO NOTE NUMDIE EQ #3
	DO .2 <- .7 DO (1000) NEXT DO .7 <- .3
	DO .1 <- ',61000SUB#27'¢',61000SUB#28' DO .2 <- #0 DO .3 <- #4 DO .4 <- #0
	DO (40021) NEXT DO NOTE NOT(GAVEUP OR SCORNG).
	DO .2 <- .7 DO (1000) NEXT DO .7 <- .3
	DO .1 <- ,61000SUB#10 DO .2 <- #0 DO .3 <- #0 DO .4 <- #25
	DO (40021) NEXT DO NOTE DFLAG
	DO .2 <- .7 DO (1000) NEXT DO .7 <- .3
	DO .1 <- ,61000SUB#19 DO .2 <- #0 DO .3 <- #0 DO .4 <- #25
	DO (40021) NEXT DO NOTE CLOSNG
	DO .2 <- .7 DO (1000) NEXT DO .7 <- .3
	DO .1 <- ,61000SUB#15 DO .2 <- #0 DO .3 <- #10 DO .4 <- #0
	DO (40021) NEXT DO NOTE BONUS EQ #0
	DO .4 <- .1 DO .1 <- ,61000SUB#15 DO .2 <- #135 DO .3 <- #25
	DO (40021) NEXT DO NOTE BONUS EQ #135
	DO .4 <- .1 DO .1 <- ,61000SUB#15 DO .2 <- #134 DO .3 <- #30
	DO (40021) NEXT DO NOTE BONUS EQ #134
	DO .4 <- .1 DO .1 <- ,61000SUB#15 DO .2 <- #133 DO .3 <- #45
	DO (40021) NEXT DO NOTE BONUS EQ #133
	DO .4 <- .1 DO .1 <- ,61000SUB#26 DO .2 <- #0 DO .3 <- #0
	DO (40021) NEXT DO NOTE CLOSED
	DO .2 <- .7 DO (1000) NEXT DO .7 <- .3

	DO NOTE DID HE COME TO WITT'S END AS HE SHOULD?
	DO .1 <- ,61003SUB#16#1 DO .2 <- #108 DO .3 <- #1 DO .4 <- #0
	DO (40021) NEXT DO NOTE MAGZIN.LOC EQ #108
	DO .2 <- .7 DO (1000) NEXT DO .7 <- .3

	DO NOTE ROUND IT OFF
	DO .1 <- .7 DO .2 <- #2 DO (1000) NEXT DO .7 <- .3

	DO .5 <- #1
	DO (10064) NEXT

	PLEASE DEDUCT POINTS FOR HINTS.  HINTS < 4 ARE SPECIAL; SEE DATABASE
	       DESCRIPTION.
	       FOR .5 IN #1 .. #9
(10064)	DO FORGET #1
	DO .1 <- ,61004SUB.5#1 DO .2 <- #0 DO .3 <- #0 DO .4 <- ,60011SUB.5#2
	DO (40021) NEXT DO NOTE HINTUSED
	DO .2 <- .1 DO .1 <- .7 DO (1010) NEXT DO .7 <- .3
	DO .1 <- .5
	DO .2 <- #9
	DO (10065) NEXT
	DO (1020) NEXT
	DO .5 <- .1
	DO (10064) NEXT
	PLEASE STOP
(10065)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- .7~#32768 DO .2 <- #0 DO .3 <- .7 DO .4 <- #0
	DO (40021) NEXT DO NOTE CLAMP NEGATIVE SCORES TO ZERO
	DO RETRIEVE .2 + .3 + .4 + .5 + .6 + .7
	DO RESUME #1

	PLEASE DO SAY
(10101)	DO FORGET #1
	DO .1 <- ,51001SUB#3#4 DO NOTE .1 <- CURRENT WORD INDEX INDEX
	DO .1 <- ,51001SUB#3.1 DO NOTE .1 <- CURRENT WORD INDEX
	DO .2 <- #0
	DO (10102) NEXT
	DO .1 <- ,60004SUB.1#3 DO NOTE .1 <- VOCAB(CURRENT WORD).
	DO .2 <- #318 DO NOTE XYZZY
	DO (10103) NEXT
	DO .2 <- #321 DO NOTE PLUGH
	DO (10103) NEXT
	DO .2 <- #327 DO NOTE PLOVER
	DO (10103) NEXT
	DO .2 <- #793 DO NOTE FEE/FIE/FOE/FOO/FUM
	DO (10104) NEXT
	DO (10105) NEXT
	PLEASE STOP
(10102)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (10105) NEXT
	PLEASE STOP
(10103)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- .1~#255 DO NOTE .1 <- VOCAB(CURRENT WORD).VALUE
	DO (501) NEXT
	PLEASE STOP
(10104)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (10261) NEXT
	PLEASE STOP
(10105)	DO FORGET #1
	DO .1 <- ,51001SUB#3#4 DO NOTE .1 <- CURRENT WORD INDEX INDEX
	DO,60006SUB#13#2<-#79  DO,60006SUB#13#3<-#75  DO,60006SUB#13#4<-#65
	DO,60006SUB#13#5<-#89  DO,60006SUB#13#6<-#44  DO,60006SUB#13#7<-#32
	DO,60006SUB#13#8<-#34  DO,60006SUB#13#9<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (51501) NEXT DO NOTE READ OUT INPUT BUFFER
	DO,60006SUB#13#2<-#34  DO,60006SUB#13#3<-#46  DO,60006SUB#13#4<-#10
	DO,60006SUB#13#5<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (241) NEXT
	PLEASE STOP

	PLEASE CAVE.  DIFFERENT MESSAGES DEPENDING ON WHETHER ABOVE GROUND.
(10111)	DO FORGET #1
	DO .1 <- '#65535~"',61000SUB#1'~#65528"'~#1
	DO .2 <- #57
	DO (1000) NEXT
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO (5) NEXT
	PLEASE STOP

	PLEASE CARRY AN OBJECT.  SPECIAL CASES FOR BIRD AND CAGE (IF BIRD IN
	       CAGE, CAN'T TAKE ONE WITHOUT THE OTHER).  LIQUIDS ALSO SPECIAL,
	       SINCE THEY DEPEND ON STATUS OF BOTTLE.  ALSO VARIOUS SIDE
	       EFFECTS, ETC.
(10121)	DO FORGET #1
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #0
	DO (10130) NEXT DO NOTE CHOOSE IMPLICIT OBJECT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #0
	DO (612) NEXT DO NOTE "<VERB> WHAT?"
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO (30101) NEXT DO NOTE .1 <- TOTING(.1).
	DO .2 <- #1
	DO .3 <- #24
	DO (240) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #24
	DO (40002) NEXT DO NOTE .3 <- OBJ EQ PLANT
	DO .1 <- .3
	DO .2 <- ,61003SUB#24#5 DO NOTE .2 <- PLANT.PROPNEG
	DO (40006) NEXT DO NOTE .3 <- OBJ EQ PLANT AND PLANT.PROPNEG
	DO .1 <- .3
	DO .2 <- #1
	DO .3 <- #115
	DO (240) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #35
	DO (40002) NEXT DO NOTE .3 <- OBJ EQ BEAR
	DO .4 <- .3
	DO .1 <- ,61003SUB#35#4 DO NOTE .1 <- BEAR.PROP
	DO .2 <- #1
	DO (40002) NEXT DO NOTE .3 <- BEAR.PROP EQ #1
	DO .1 <- .3
	DO .2 <- .4
	DO (40006) NEXT DO NOTE .3 <- OBJ EQ BEAR AND BEAR.PROP EQ #1
	DO .1 <- .3
	DO .2 <- #1
	DO .3 <- #169
	DO (240) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #64
	DO (40002) NEXT DO NOTE .3 <- OBJ EQ CHAIN
	DO .4 <- .3
	DO .1 <- ,61003SUB#35#4 DO NOTE .1 <- BEAR.PROP
	DO .2 <- #0
	DO (40003) NEXT DO NOTE .3 <- BEAR.PROP NE #0
	DO .4 <- .3
	DO .2 <- .4
	DO (40006) NEXT DO NOTE .3 <- OBJ EQ CHAIN AND BEAR.PROP NE #0
	DO .4 <- .3
	DO .1 <- ,60007SUB#64#2 DO NOTE .1 <- CHAIN.FIXD
	DO .2 <- #0
	DO (40003) NEXT DO NOTE .3 <- OBJ.FIXD NE #0
	DO .1 <- .3
	DO .2 <- .4
	DO (40006) NEXT DO NOTE .3 <- OBJ EQ CHAIN AND BEAR.PROP NE #0 AND CHAIN.FIXD NE #0
	DO .1 <- .3
	DO .2 <- #1
	DO .3 <- #170
	DO (240) NEXT
	DO .1 <- ,60007SUB',61000SUB#22'#2 DO NOTE .1 <- OBJ.LOC2
	DO .2 <- #0
	DO (40003) NEXT DO NOTE .3 <- OBJ.LOC2 NE #0
	DO .1 <- .3
	DO .2 <- #1
	DO .3 <- #25
	DO (240) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #21
	DO (10123) NEXT DO NOTE OBJ EQ WATER
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #22
	DO (10124) NEXT DO NOTE OBJ EQ OIL
	DO (30171) NEXT DO NOTE .1 <- HOLDNG
	DO .2 <- #7
	DO (40004) NEXT DO NOTE .3 <- HOLDNG LT #7
	DO .1 <- .3
	DO .2 <- #0
	DO .3 <- #92
	DO (240) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #8
	DO (10125) NEXT DO NOTE OBJ EQ BIRD
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #4
	DO (10126) NEXT DO NOTE OBJ EQ CAGE
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJ
	DO .2 <- #65535
	DO (30001) NEXT DO NOTE MOVE OBJ TO #65535
	DO .2 <- #20
	DO (10127) NEXT
	DO .1 <- #54
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10123)	DO (40011) NEXT DO NOTE .1=.2 WATER
	DO .2 <- #0
	DO NOTE IF HERE(BOTTLE) AND BOTTLE.PROP EQ #0
	DO (10129) NEXT DO NOTE TAKE BOTTLE OF WATER
	DO FORGET #1
	DO .1 <- #20 DO (30101) NEXT DO NOTE .1 <- TOTING(BOTTLE).
	DO .2 <- #0
	DO .3 <- #104
	DO (240) NEXT DO NOTE IF NOT TOTING(BOTTLE).
	DO .1 <- ,61003SUB#20#4 DO NOTE .1 <- BOTTLE.PROP
	DO .2 <- #1
	DO (40003) NEXT DO NOTE .3 <- BOTTLE.PROP NE #1
	DO .1 <- .3
	DO .3 <- #105
	DO (240) NEXT DO NOTE IF BOTTLE IS FULL
	DO ,61000SUB#22 <- #20 DO NOTE OBJECT <- BOTTLE
	DO (10231) NEXT DO NOTE FILL
	PLEASE STOP
(10124)	DO (40011) NEXT DO NOTE .1=.2 OIL
	DO .2 <- #2
	DO NOTE IF HERE(BOTTLE) AND BOTTLE.PROP EQ #0
	DO (10129) NEXT DO NOTE TAKE BOTTLE OF OIL
	DO FORGET #1
	DO .1 <- #20 DO (30101) NEXT DO NOTE .1 <- TOTING(BOTTLE).
	DO .2 <- #0
	DO .3 <- #104
	DO (240) NEXT DO NOTE IF NOT TOTING(BOTTLE).
	DO .1 <- ,61003SUB#20#4 DO NOTE .1 <- BOTTLE.PROP
	DO .2 <- #1
	DO (40003) NEXT DO NOTE .3 <- BOTTLE.PROP NE #1
	DO .1 <- .3
	DO .3 <- #105
	DO (240) NEXT DO NOTE IF BOTTLE IS FULL
	DO ,61000SUB#22 <- #20 DO NOTE OBJECT <- BOTTLE
	DO (10231) NEXT DO NOTE FILL
	PLEASE STOP
(10125)	DO (40011) NEXT DO NOTE .1=.2 BIRD
	DO FORGET #1
	DO .1 <- ,61003SUB#8#4 DO NOTE .1 <- BIRD.PROP
	DO .2 <- #1
	DO (10126) NEXT DO NOTE TAKE CAGE IF BIRD.PROP EQ #1
	DO .1 <- #5 DO (30101) NEXT DO NOTE .1 <- TOTING(ROD).
	DO .2 <- #1
	DO .3 <- #26
	DO (240) NEXT DO NOTE IF TOTING(ROD).
	DO .1 <- #4 DO (30101) NEXT DO NOTE .1 <- TOTING(CAGE).
	DO .2 <- #0
	DO .3 <- #27
	DO (240) NEXT DO NOTE IF NOT TOTING(CAGE).
	DO ,61003SUB#8#4 <- #1 DO NOTE BIRD.PROP <- #1
	DO .1 <- #8
	DO .2 <- #65535
	DO (30001) NEXT DO NOTE TAKE BIRD
	DO .1 <- #54
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10126)	DO (40011) NEXT DO NOTE .1=.2 CAGE
	DO FORGET #1
	DO .1 <- ,61003SUB#8#4 DO NOTE .1 <- BIRD.PROP
	DO .2 <- #1
	DO .3 <- #8
	DO (10128) NEXT DO NOTE TAKE BIRD IF BIRD.PROP EQ #1
	DO .1 <- #4
	DO .2 <- #65535
	DO (30001) NEXT DO NOTE TAKE CAGE
	DO .1 <- #54
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10127)	DO (40011) NEXT DO NOTE .1=.2 BOTTLE
	DO .1 <- ,61003SUB#20#4 DO NOTE .1 <- BOTTLE.PROP
	DO .2 <- #0
	DO .3 <- #21
	DO (10128) NEXT
	DO .1 <- ,61003SUB#20#4 DO NOTE .1 <- BOTTLE.PROP
	DO .2 <- #2
	DO .3 <- #21
	DO (10128) NEXT
	DO RESUME #1
	PLEASE STOP
(10128)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- .3
	DO .2 <- #65535
	DO (30001) NEXT DO NOTE MOVE .1 TO #65535
	DO RESUME #1
	PLEASE STOP
(10129)	DO .1 <- ,61003SUB#20#4 DO NOTE .1 <- BOTTLE.PROP
	DO (40002) NEXT DO NOTE .3 <- BOTTLE.PROP EQ .2
	DO .1 <- #20 DO (30101) NEXT DO NOTE .1 <- HERE(BOTTLE).
	DO .2 <- .3
	DO (40006) NEXT DO NOTE .3 <- HERE(BOTTLE) AND BOTTLE.PROP EQ .2
	DO .1 <- .3
	DO .2 <- #1
	DO (40011) NEXT DO NOTE HERE(BOTTLE) AND BOTTLE.PROP EQ .2
	DO ,61000SUB#22 <- #20 DO NOTE OBJECT <- BOTTLE
	DO RESUME #2
	PLEASE STOP
	PLEASE CHOOSE IMPLICIT OBJECT
(10130)	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ #0
	DO .4 <- ,61001SUB',61000SUB#1'#1 DO NOTE .4 <- LOC.FIRSTOBJ
	DO .5 <- #0
	DO (10131) NEXT
	PLEASE STOP
(10131)	DO FORGET #1
	DO .1 <- .4
	DO .2 <- #0
	DO (10132) NEXT
	DO .1 <- ,60007SUB.4#2 DO NOTE .1 <- OBJ.LOC2
	DO .2 <- #0
	DO (10133) NEXT
	DO .4 <- ,61003SUB.4#2 DO NOTE OBJ <- OBJ.NEXTOBJ
	DO (10131) NEXT
	PLEASE STOP
(10132)	DO (40011) NEXT DO NOTE .1=.2 AFTER LAST OBJ
	DO ,61000SUB#22 <- .5 DO NOTE OBJECT <- .5
	DO RESUME #2
	PLEASE STOP
(10133)	DO (40011) NEXT DO NOTE .1=.2 OBJ IS TAKEABLE
	DO .1 <- .5
	DO .2 <- #0
	DO (10134) NEXT
	DO RESUME #2 DO NOTE LEAVE OBJECT AS #0
	PLEASE STOP
(10134)	DO (40011) NEXT DO NOTE .1=.2 OBJ IS FIRST TAKEABLE
	DO .5 <- .4
	DO RESUME #2 DO NOTE CONTINUE ITERATING OVER OBJECTS AT LOC
	PLEASE STOP

	PLEASE INVENTORY.  IF OBJECT, TREAT SAME AS FIND.  ELSE REPORT ON
	       CURRENT BURDEN.
(10141)	DO FORGET #1
	DO .3 <- #1
	DO .4 <- #98
	DO (10142) NEXT
	PLEASE STOP
(10142)	DO FORGET #1
	DO .1 <- .3
	DO .2 <- #35
	DO (10143) NEXT
	DO .1 <- .3
	DO (30101) NEXT DO NOTE .1 <- TOTING(OBJ).
	DO .2 <- #0
	DO (10143) NEXT
	DO .1 <- .4
	DO .2 <- #98
	DO (10144) NEXT
	DO .4 <- #13
	DO .1 <- .3
	DO .2 <- #1
	DO (55501) NEXT DO NOTE PSPEAK
	DO .2 <- #64
	DO (10145) NEXT
	PLEASE STOP
(10143)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (10145) NEXT
	PLEASE STOP
(10144)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- #99
	DO (55601) NEXT DO NOTE RSPEAK
	DO RESUME #1
	PLEASE STOP
(10145)	DO FORGET #1
	DO .1 <- .3
	DO .2 <- #64
	DO (10146) NEXT
	DO (1020) NEXT
	DO .3 <- .1
	DO (10142) NEXT
	PLEASE STOP
(10146)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #35
	DO (30101) NEXT DO NOTE .1 <- TOTING(BEAR).
	DO .2 <- #1
	DO (10147) NEXT
	DO .1 <- .4
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10147)	DO (40011) NEXT DO NOTE .1=.2 TOTING(BEAR).
	DO .4 <- #141
	DO RESUME #1
	PLEASE STOP

	PLEASE LIGHT LAMP
(10151)	DO FORGET #1
	DO .1 <- #2
	DO (30101) NEXT DO NOTE .1 <- HERE(LAMP).
	DO .2 <- #0
	DO .3 <- #38
	DO (240) NEXT
	DO .1 <- ,61000SUB#5 DO NOTE .1 <- LIMIT
	DO .3 <- #184
	DO (240) NEXT
	DO ,61003SUB#2#4 <- #1 DO NOTE LAMP.PROP <- #1
	DO .1 <- #39
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- ,61000SUB#20 DO NOTE .1 <- WZDARK
	DO .3 <- #13
	DO (240) NEXT
	DO (101) NEXT
	PLEASE STOP

	PLEASE LAMP OFF
(10161)	DO FORGET #1
	DO .1 <- #2
	DO (30101) NEXT DO NOTE .1 <- HERE(LAMP).
	DO .2 <- #0
	DO .3 <- #38
	DO (240) NEXT
	DO ,61003SUB#2#4 <- #0 DO NOTE LAMP.PROP <- #0
	DO .1 <- #40
	DO (55601) NEXT DO NOTE RSPEAK
	DO (30161) NEXT DO NOTE .1 <- DARK().
	DO .2 <- #1
	DO .3 <- #16
	DO (240) NEXT
	DO (241) NEXT
	PLEASE STOP

	PLEASE LOCK, UNLOCK, NO OBJECT GIVEN.  ASSUME VARIOUS THINGS IF PRESENT.
(10171)	DO FORGET #1
	DO .1 <- #64 DO NOTE .1 <- CHAIN
	DO (30121) NEXT DO NOTE .1 <- HERE(CHAIN).
	DO .4 <- .1
	DO .3 <- #14 DO NOTE .3 <- CLAM
	DO .1 <- .3 DO NOTE .1 <- CLAM
	DO (30111) NEXT DO NOTE .1 <- HERE(CLAM).
	DO .2 <- #1
	DO (10172) NEXT
	DO .3 <- #15 DO NOTE .3 <- OYSTER
	DO .1 <- .3 DO NOTE .1 <- OYSTER
	DO (30111) NEXT DO NOTE .1 <- HERE(OYSTER).
	DO .2 <- #1
	DO (10172) NEXT
	DO .3 <- #9 DO NOTE .3 <- D0OR
	DO .1 <- .3 DO NOTE .1 <- D0OR
	DO (30121) NEXT DO NOTE .1 <- AT(D0OR).
	DO .2 <- #1
	DO (10172) NEXT
	DO .3 <- #3 DO NOTE .3 <- GRATE
	DO .1 <- .3 DO NOTE .1 <- GRATE
	DO (30121) NEXT DO NOTE .1 <- AT(GRATE).
	DO .2 <- #1
	DO (10172) NEXT
	DO .3 <- #64 DO NOTE .3 <- CHAIN
	DO .1 <- .4 DO NOTE .1 <- HERE(CHAIN).
	DO .2 <- #1
	DO .4 <- #0
	DO (10172) NEXT
	DO .1 <- #28
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10172)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- .4 DO NOTE .1 <- HERE(CHAIN).
	DO .2 <- #1
	DO (612) NEXT DO NOTE "<LOCK/UNLOCK> WHAT?"
	DO ,61000SUB#22 <- .3
	DO .1 <- #33
	DO (10181) NEXT

	PLEASE LOCK, UNLOCK OBJECT.  SPECIAL STUFF FOR OPENING CLAM/OYSTER AND
	       FOR CHAIN.
	       .1 CONTAINS DEFAULT RESPONSE EITHER #28 (OBJ) OR #33 (NO OBJ)
(10181)	DO FORGET #1
	DO .4 <- .1
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #14 DO (10192) NEXT DO NOTE CLAM
	DO .2 <- #15 DO (10193) NEXT DO NOTE OYSTER
	DO .2 <- #3 DO (10184) NEXT DO NOTE GRATE
	DO .2 <- #64 DO (10188) NEXT DO NOTE CHAIN
	DO .2 <- #9 DO (10183) NEXT DO NOTE D0OR
	DO .2 <- #4 DO .3 <- #32 DO (240) NEXT DO NOTE CAGE
	DO .2 <- #1 DO .3 <- #55 DO (240) NEXT DO NOTE KEYS
	DO .1 <- .4
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10183)	DO (40011) NEXT DO NOTE .1=.2 D0OR
	DO FORGET #1
	DO .1 <- ,61003SUB#D0OR#4 DO NOTE .1 D0OR.PROP
	DO .2 <- #1
	DO .3 <- #54
	DO (240) NEXT
	DO .1 <- #111
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10184)	DO (40011) NEXT DO NOTE .1=.2 GRATE
	DO FORGET #1
	DO .1 <- #1 DO (30111) NEXT DO NOTE .1 <- HERE(KEYS).
	DO .2 <- #0
	DO .3 <- #31
	DO (240) NEXT
	DO .1 <- ,61000SUB#19 DO NOTE .1 <- CLOSNG
	DO .2 <- #1
	DO (10186) NEXT DO NOTE IF CLOSNG
	DO .1 <- ,61000SUB#21 DO NOTE .1 <- VERB
	DO .2 <- #4
	DO (10185) NEXT DO NOTE IF UNLOCK
	DO .1 <- #34
	DO .2 <- ,61003SUB#3#4 DO NOTE .2 <- GRATE.PROP
	DO ,61003SUB#3#4 <- #0 DO NOTE GRATE.PROP <- #0
	DO (1000) NEXT
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10185)	DO (40011) NEXT DO NOTE .1=.2 UNLOCK GRATE
	DO FORGET #1
	DO .1 <- #36
	DO .2 <- ,61003SUB#3#4 DO NOTE .2 <- GRATE.PROP
	DO ,61003SUB#3#4 <- #1 DO NOTE GRATE.PROP <- #1
	DO (1000) NEXT
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10186)	DO (40011) NEXT DO NOTE .1=.2 LOCK/UNLOCK GRATE CLOSNG
	DO FORGET #1
	DO .1 <- ,61000SUB#25 DO NOTE .1 <- PANIC
	DO .2 <- #0
	DO (10187) NEXT
	DO .1 <- #130
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10187)	DO (40011) NEXT DO NOTE .1=.2 LOCK/UNLOCK GRATE NOT PANIC
	DO ,61000SUB#25 <- #1 DO NOTE PANIC <- #1
	DO ,61000SUB#17 <- #15 DO NOTE CLOCK2 <- #15
	DO RESUME #1
	PLEASE STOP
(10188)	DO (40011) NEXT DO NOTE .1=.2 CHAIN
	DO FORGET #1
	DO .1 <- ,61000SUB#21 DO NOTE .1 <- VERB
	DO .2 <- #4
	DO (10190) NEXT DO NOTE IF UNLOCK
	DO .1 <- ,61003SUB#64#4 DO NOTE .1 <- CHAIN.PROP
	DO .2 <- #0
	DO (40003) NEXT DO NOTE .3 <- CHAIN.PROP NE #0
	DO .1 <- .3
	DO .2 <- #1
	DO .3 <- #34
	DO (240) NEXT DO NOTE IF CHAIN.PROP NE #0
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #130
	DO (40003) NEXT DO NOTE .3 <- LOC NE #130
	DO .1 <- .3
	DO .2 <- #1
	DO .3 <- #173
	DO (240) NEXT DO NOTE IF LOC NE #130
	DO ,61003SUB#64#4 <- #2 DO NOTE CHAIN.PROP <- #2
	DO ,60007SUB#64#2 <- #65535 DO NOTE CHAIN.FIXD <- #65535
	DO (10189) NEXT DO NOTE IF TOTING(CHAIN) DROP(CHAIN).
	DO .1 <- #172
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10189)	DO .1 <- #64 DO (30101) NEXT DO NOTE .1 <- TOTING(CHAIN).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 TOTING(CHAIN).
	DO .1 <- #64 DO .2 <- #130 DO (30001) NEXT DO NOTE MOVE CHAIN TO #130
	DO RESUME #1
	PLEASE STOP
(10190)	DO (40011) NEXT DO NOTE .1=.2 UNLOCK CHAIN
	DO FORGET #1
	DO .1 <- ,61003SUB#35#4 DO NOTE .1 <- BEAR.PROP
	DO .2 <- #0
	DO .3 <- #41
	DO (240) NEXT DO NOTE IF BEAR.PROP EQ #0
	DO .1 <- ,61003SUB#64#4 DO NOTE .1 <- CHAIN.PROP
	DO .2 <- #0
	DO .3 <- #37
	DO (240) NEXT DO NOTE IF CHAIN.PROP EQ #0
	DO ,61003SUB#64#4 <- #0 DO NOTE CHAIN.PROP <- #0
	DO ,60007SUB#64#2 <- #0 DO NOTE CHAIN.FIXD <- #0
	DO (10191) NEXT DO NOTE IF BEAR IS NOT DEAD, BEAR.PROP/FIXD <- #2/#0
	DO .1 <- #171
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10191)	DO .1 <- ,61003SUB#35#4 DO NOTE .1 <- BEAR.PROP
	DO .2 <- #3
	DO (40002) NEXT DO NOTE .3 <- BEAR.PROP EQ #3
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 BEAR.PROP NE #3
	DO ,61003SUB#35#4 <- #2 DO NOTE BEAR.PROP <- #2
	DO ,60007SUB#35#2 <- #0 DO NOTE BEAR.FIXD <- #0
	DO RESUME #1
	PLEASE STOP
(10192)	DO (40011) NEXT DO NOTE .1=.2 CLAM
	DO FORGET #1
	DO .1 <- #14 DO (30101) NEXT DO NOTE .1 <- TOTING(CLAM).
	DO .2 <- #1
	DO .3 <- #120
	DO (240) NEXT DO NOTE IF TOTING(CLAM).
	DO .1 <- ,61000SUB#21 DO NOTE .1 <- VERB
	DO .2 <- #6
	DO .3 <- #61
	DO (240) NEXT DO NOTE IF VERB EQ CLOSE/LOCK
	DO .1 <- #57 DO (30101) NEXT DO NOTE .1 <- TOTING(TRIDENT).
	DO .2 <- #0
	DO .3 <- #122
	DO (240) NEXT DO NOTE IF NOT TOTING(TRIDENT).
	DO .1 <- #14 DO .2 <- #0 DO (30001) NEXT DO NOTE MOVE CLAM TO #0
	DO .1 <- #15 DO .2 <- ,61000SUB#1 DO (30001) NEXT DO NOTE MOVE OYSTER TO LOC
	DO .1 <- #61 DO .2 <- #105 DO (30001) NEXT DO NOTE MOVE PEARL TO #105
	DO .1 <- #124
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10193)	DO (40011) NEXT DO NOTE .1=.2 OYSTER
	DO FORGET #1
	DO .1 <- #15 DO (30101) NEXT DO NOTE .1 <- TOTING(OYSTER).
	DO .2 <- #1
	DO .3 <- #121
	DO (240) NEXT DO NOTE IF TOTING(OYSTER).
	DO .1 <- ,61000SUB#21 DO NOTE .1 <- VERB
	DO .2 <- #6
	DO .3 <- #61
	DO (240) NEXT DO NOTE IF VERB EQ CLOSE/LOCK
	DO .1 <- #57 DO (30101) NEXT DO NOTE .1 <- TOTING(TRIDENT).
	DO .2 <- #1
	DO .3 <- #125
	DO (240) NEXT DO NOTE IF TOTING(TRIDENT).
	DO .1 <- #123
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP

	PLEASE DISCARD OBJECT.  "THROW" ALSO COMES HERE FOR MOST OBJECTS.
	       SPECIAL CASES FOR BIRD (MIGHT ATTACK SNAKE OR DRAGON) AND CAGE
	       (MIGHT CONTAIN BIRD) AND VASE.  DROP COINS AT VENDING MACHINE
	       FOR EXTRA BATTERIES.
(10201)	DO FORGET #1
	DO (10203) NEXT DO NOTE IF OBJECT EQ ROD AND TOTING(ROD2), OBJECT <- ROD2
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO (30101) NEXT DO NOTE .1 <- TOTING(OBJECT).
	DO .2 <- #0
	DO .3 <- #29
	DO (240) NEXT DO NOTE IF NOT TOTING(OBJECT).
	DO (10204) NEXT DO NOTE IF OBJECT EQ BIRD AND HERE(SNAKE).
	DO (10205) NEXT DO NOTE IF OBJECT EQ COINS AND HERE(VEND).
	DO (10206) NEXT DO NOTE IF OBJECT EQ BIRD AND AT(DRAGON) AND DRAGON.PROP EQ #0
	DO (10207) NEXT DO NOTE IF OBJECT EQ BEAR AND AT(TROLL).
	DO (10208) NEXT DO NOTE IF OBJECT EQ VASE AND LOC NE SOFT ROOM AND AT(PILLOW).
	DO (10209) NEXT DO NOTE IF OBJECT EQ VASE AND LOC NE SOFT ROOM
	DO .1 <- #54
	DO (55601) NEXT DO NOTE RSPEAK
	DO (10210) NEXT DO NOTE IF OBJECT EQ CAGE AND BIRD.PROP NE #0
	DO .1 <- #21 DO (10211) NEXT DO NOTE IF OBJECT EQ WATER
	DO .1 <- #22 DO (10211) NEXT DO NOTE IF OBJECT EQ OIL
	DO (10212) NEXT DO NOTE IF OBJECT EQ BOTTLE
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- ,61000SUB#1 DO NOTE .2 <- LOC
	DO (30001) NEXT DO NOTE MOVE OBJECT TO LOC
	DO (241) NEXT
	PLEASE STOP
(10203)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #8
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ ROD
	DO (30101) NEXT DO NOTE .1 <- TOTING(ROD).
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 NOT TOTING(ROD).
	DO .1 <- #9
	DO (30101) NEXT DO NOTE .1 <- TOTING(ROD2).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 TOTING(ROD2).
	DO ,61000SUB#22 <- #9 DO NOTE OBJECT <- ROD2
	DO RESUME #1
	PLEASE STOP
(10204)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #8
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ BIRD
	DO .1 <- #11 DO (30111) NEXT DO NOTE .1 <- HERE(SNAKE).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 HERE(SNAKE).
	DO FORGET #1
	DO .1 <- #30
	DO (55601) NEXT DO NOTE RSPEAK
	DO .3 <- #136
	DO (10046) NEXT DO NOTE QUIT IF CLOSED
	DO .1 <- #11 DO .2 <- #0 DO (30001) NEXT DO NOTE MOVE SNAKE TO #0
	DO ,61003SUB#11#4 <- #1 DO NOTE SNAKE.PROP <- #1
	DO ,61003SUB#8#4 <- #0 DO NOTE BIRD.PROP <- #0
	DO .1 <- #8
	DO .2 <- ,61000SUB#1
	DO (30001) NEXT DO NOTE MOVE BIRD TO LOC
	DO (241) NEXT
	PLEASE STOP
(10205)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #54
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ COINS
	DO .1 <- #38 DO (30111) NEXT DO NOTE .1 <- HERE(VEND).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 HERE(VEND).
	DO FORGET #1
	DO .1 <- #54 DO .2 <- #0 DO (30001) NEXT DO NOTE MOVE COINS TO #0
	DO .1 <- #39
	DO .2 <- ,61000SUB#1
	DO (30001) NEXT DO NOTE MOVE BATTERIES TO LOC
	DO .2 <- #2
	DO (55501) NEXT DO NOTE PSPEAK
	DO (241) NEXT
	PLEASE STOP
(10206)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #8
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ BIRD
	DO .1 <- #31 DO (30121) NEXT DO NOTE .1 <- AT(DRAGON).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 AT(DRAGON).
	DO .1 <- ,61003SUB#31#4 DO NOTE .1 <- DRAGON.PROP
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 DRAGON.PROP EQ #0
	DO FORGET #1
	DO .1 <- #154
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- #8 DO .2 <- #0 DO (30001) NEXT DO NOTE MOVE BIRD TO #0
	DO ,61003SUB#8#4 <- #0 DO NOTE BIRD.PROP <- #0
	DO .1 <- ,61003SUB#11#1 DO NOTE .1 <- SNAKE.LOC
	DO .2 <- #19 DO NOTE .2 <- HALL OF MOUNTAIN KING
	DO .3 <- #1
	DO .4 <- #0
	DO (40021) NEXT DO NOTE IF SNAKE.LOC EQ HALL OF MT KING .1 <- #1, ELSE .1 <- #0
	DO .2 <- ,61000SUB#30 DO NOTE .2 <- TALLY2
	DO (1000) NEXT
	DO ,61000SUB#30 <- .3 DO NOTE TALLY2=TALLY2+.1
	DO (241) NEXT
	PLEASE STOP
(10207)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #35
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ BEAR
	DO .1 <- #33 DO (30121) NEXT DO NOTE .1 <- AT(TROLL).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 AT(TROLL).
	DO FORGET #1
	DO .1 <- #163
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,60007SUB#33#1 <- #0 DO NOTE TROLL.PLAC <- #0
	DO ,60007SUB#33#2 <- #0 DO NOTE TROLL.FIXD <- #0
	DO .1 <- #33 DO .2 <- #0 DO (30001) NEXT DO NOTE MOVE TROLL TO #0
	DO ,60007SUB#34#1 <- #122 DO NOTE TROLL2.PLAC <- #117
	DO ,60007SUB#34#2 <- #122 DO NOTE TROLL2.FIXD <- #122
	DO .1 <- #34 DO .2 <- ,61000SUB#1 DO (30001) NEXT DO NOTE MOVE TROLL2 TO LOC
	DO ,61003SUB#33#4 <- #2 DO NOTE TROLL.PROP <- #2
	DO .1 <- #32 DO .2 <- ,61003SUB#32#1 DO (30001) NEXT DO NOTE JUGGLE CHASM
	DO ,61003SUB#33#4 <- #2 DO NOTE TROLL.PROP <- #2
	DO .1 <- #35 DO .2 <- ,61000SUB#1 DO (30001) NEXT DO NOTE MOVE BEAR TO LOC
	DO (241) NEXT
	PLEASE STOP
(10208)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #58
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ VASE
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #96
	DO (40002) NEXT DO NOTE .3 <- LOC EQ SOFT ROOM
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 LOC NE SOFT ROOM
	DO .1 <- #10 DO (30121) NEXT DO NOTE .1 <- AT(PILLOW).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 LOC AT(PILLOW).
	DO FORGET #1
	DO .1 <- #58
	DO .2 <- #3
	DO (55501) NEXT DO NOTE PSPEAK
	DO .1 <- #58 DO .2 <- ,61000SUB#1 DO (30001) NEXT DO NOTE MOVE VASE TO LOC
	DO (241) NEXT
	PLEASE STOP
(10209)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #58
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ VASE
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #96
	DO (40002) NEXT DO NOTE .3 <- LOC EQ SOFT ROOM
	DO .1 <- .3
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 LOC NE SOFT ROOM
	DO FORGET #1
	DO ,61003SUB#58#4 <- #2 DO NOTE VASE.PROP <- #2
	DO .1 <- #58
	DO .2 <- #5
	DO (55501) NEXT DO NOTE PSPEAK
	DO ,60007SUB#58#2 <- #65535 DO NOTE VASE.FIXD <- #65535
	DO .1 <- #58 DO .2 <- ,61000SUB#1 DO (30001) NEXT DO NOTE MOVE VASE TO LOC
	DO (241) NEXT
	PLEASE STOP
(10210)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #4
	DO (40011) NEXT DO NOTE OBJECT EQ CAGE
	DO .1 <- ,61003SUB#8#4 DO NOTE .1 <- BIRD.PROP
	DO .2 <- #1
	DO (40011) NEXT DO NOTE BIRD.PROP EQ #1
	DO .1 <- #8 DO .2 <- ,61000SUB#1 DO (30001) NEXT DO NOTE MOVE BIRD TO LOC
	DO RESUME #1
	PLEASE STOP
(10211)	DO .2 <- ,61000SUB#22 DO NOTE .2 <- OBJECT
	DO (40011) NEXT DO NOTE .1=.2
	DO ,61000SUB#22 <- #20 DO NOTE OBJECT <- BOTTLE
	DO RESUME #1
	PLEASE STOP
(10212)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #20
	DO (40011) NEXT DO NOTE OBJECT EQ BOTTLE
	DO .1 <- #21 DO .2 <- #0 DO (30001) NEXT DO NOTE MOVE WATER TO #0
	DO .1 <- #22 DO .2 <- #0 DO (30001) NEXT DO NOTE MOVE OIL TO #0
	DO RESUME #1
	PLEASE STOP

	PLEASE POUR.  IF NO OBJECT, OR OBJECT IS BOTTLE, ASSUME CONTENTS OF
	       BOTTLE.  SPECIAL TESTS FOR POURING WATER OR OIL ON PLANT OR
	       RUSTY DOOR.
(10221)	DO FORGET #1
	DO .3 <- #0 DO .4 <- #21 DO (10222) NEXT DO NOTE IF OBJECT EQ BOTTLE OF WATER, OBJECT <- WATER
	DO .3 <- #2 DO .4 <- #22 DO (10222) NEXT DO NOTE IF OBJECT EQ BOTTLE OF OIL, OBJECT <- OIL
	DO .1 <- ,61000SUB#22 DO (30101) NEXT DO NOTE .1 <- TOTING(OBJECT).
	DO .2 <- #0
	DO .3 <- #29
	DO (240) NEXT DO NOTE TOTING(OBJECT).
	DO .1 <- ,61000SUB#22 DO .2 <- #21 DO (40002) NEXT DO NOTE .3 <- OBJECT EQ WATER
	DO .4 <- .3 DO .2 <- #22 DO .3 <- #1 DO (40021) NEXT DO NOTE .1 <- OBJECT EQ OIL OR OBJECT EQ WATER
	DO .2 <- #0
	DO .3 <- #78
	DO (240) NEXT DO NOTE OBJECT EQ WATER OR OBJECT EQ OIL
	DO ,61003SUB#20#4 <- #1 DO NOTE BOTTLE.PROP <- #1
	DO .1 <- ,61000SUB#22
	DO .2 <- #0 
	DO (30001) NEXT DO NOTE MOVE OBJECT TO #0
	DO (10223) NEXT DO NOTE IF AT D0OR
	DO (10224) NEXT DO NOTE IF AT PLANT
	DO .1 <- #77
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10222)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #20
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ BOTTLE
	DO .1 <- ,61003SUB#20#4 DO NOTE .1 <- BOTTLE.PROP
	DO .2 <- .3
	DO (40011) NEXT DO NOTE .1=.2
	DO ,61000SUB#22 <- .4
	DO RESUME #1
	PLEASE STOP
(10223)	DO .1 <- #9 DO (30121) NEXT DO NOTE .1 <- AT(D0OR).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 AT(D0OR).
	DO FORGET #1
	DO ,61003SUB#9#4 <- #0 DO NOTE D0OR.PROP <- #0
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #21
	DO .3 <- #113
	DO (240) NEXT DO NOTE OBJECT NE WATER
	DO ,61003SUB#9#4 <- #1 DO NOTE D0OR.PROP <- #1
	DO .1 <- #114
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10224)	DO .1 <- #24 DO (30121) NEXT DO NOTE .1 <- AT(PLANT).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 AT(PLANT).
	DO FORGET #1
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #22
	DO .3 <- #112
	DO (240) NEXT DO NOTE OBJECT NE OIL
	DO .1 <- ,61003SUB#24#4 DO NOTE .1 <- PLANT.PROP
	DO (1020) NEXT
	DO (1020) NEXT
	DO (1020) NEXT
	DO .2 <- .1
	DO .1 <- #24
	DO (55501) NEXT DO NOTE PSPEAK
	DO .1 <- .2
	DO .2 <- #6
	DO .1 <- ,61003SUB#24#4 DO NOTE .1 <- PLANT.PROP
	DO (1020) NEXT
	DO (1020) NEXT
	DO (40001) NEXT DO NOTE .3 <- PLANT.PROP+2 MOD #6
	DO ,61003SUB#24#4 <- .3 DO NOTE PLANT.PROP <- PLANT.PROP+2 MOD #6
	DO ,61003SUB#25#4 <- ',61003SUB#24#4'~#65534 DO NOTE PLANT2.PROP <- PLANT.PROP/2
	DO .1 <- #21 DO NOTE .1 <- NULL
	DO (501) NEXT
	PLEASE STOP

	PLEASE FILL.  BOTTLE MUST BE EMPTY, AND SOME LIQUID AVAILABLE.  (VASE
	       IS NASTY.)
(10231)	DO FORGET #1
	DO (10232) NEXT DO NOTE IF OBJECT EQ VASE
	DO (10233) NEXT DO NOTE IF OBJECT EQ #0 AND HERE(BOTTLE), OBJECT <- BOTTLE
	DO .1 <- ,61000SUB#22
	DO .2 <- #0
	DO (612) NEXT DO NOTE IF OBJECT STILL EQ #0
	DO .1 <- ,61000SUB#22
	DO .2 <- #20
	DO (40003) NEXT DO NOTE .3 <- OBJECT NE BOTTLE
	DO .1 <- .3
	DO .2 <- #1
	DO .3 <- #109
	DO (240) NEXT DO NOTE IF OBJECT NE BOTTLE
	DO .1 <- ,61003SUB#20#4 DO NOTE .1 <- BOTTLE.PROP
	DO .2 <- #1
	DO (40003) NEXT DO NOTE .3 <- BOTTLE.PROP NE #1
	DO .1 <- .3
	DO .2 <- #1
	DO .3 <- #105
	DO (240) NEXT DO NOTE IF BOTTLE IS ALREADY FULL
	DO .1 <- ",60009SUB',61000SUB#1'"~#4
	DO .2 <- #0
	DO .3 <- #106
	DO (240) NEXT DO NOTE IF LIQLOC(LOC) EQ NO WATER OR OIL
	DO .4 <- ",60009SUB',61000SUB#1'"~#2 DO NOTE .4 <- #0 FOR WATER #1 FOR OIL
	DO ,61003SUB#20#4 <- .4¢#0 DO NOTE BOTTLE.PROP <- .4*#2
	DO .1 <- .4
	DO .2 <- #107
	DO (1000) NEXT
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- .4
	DO .2 <- #21
	DO (1000) NEXT DO NOTE .3 <- WATER OR OIL
	DO (10234) NEXT DO NOTE IF TOTING(BOTTLE), TAKE WATER OR OIL
	DO (241) NEXT
	PLEASE STOP
(10232)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #58
	DO (40011) NEXT DO NOTE OBJECT EQ VASE
	DO .1 <- ",60009SUB',61000SUB#1'"~#4
	DO .2 <- #0
	DO .3 <- #144
	DO (240) NEXT DO NOTE IF LIQLOC(LOC) EQ NO WATER OR OIL
	DO .1 <- #58 DO (30101) NEXT DO NOTE .1 <- TOTING(VASE).
	DO .2 <- #0
	DO .3 <- #29
	DO (240) NEXT DO NOTE IF NOT TOTING(VASE).
	DO .1 <- #145
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,61003SUB#58#4 <- #2 DO NOTE VASE.PROP <- #2
	DO ,60007SUB#58#2 <- #65535 DO NOTE VASE.FIXD <- #65535
	DO .1 <- #58 DO .2 <- ,61000SUB#1 DO (30001) NEXT DO NOTE MOVE VASE TO LOC
	DO (241) NEXT
	PLEASE STOP
(10233)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ #0
	DO .1 <- #20 DO (30111) NEXT DO NOTE .1 <- HERE(BOTTLE).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 HERE(BOTTLE).
	DO ,61000SUB#22 <- #20 DO NOTE OBJECT <- BOTTLE
	DO RESUME #1
	PLEASE STOP
(10234)	DO .1 <- #20 DO (30101) NEXT DO NOTE .1 <- TOTING(BOTTLE).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 TOTING(BOTTLE).
	DO .1 <- .3 DO NOTE .1 <- WATER OR OIL
	DO .2 <- #65535
	DO (30001) NEXT DO NOTE MOVE WATER OR OIL TO #65535
	DO RESUME #1
	PLEASE STOP

	PLEASE THROW.  SAME AS DISCARD UNLESS AXE.  THEN SAME AS ATTACK EXCEPT
	       IGNORE BIRD,  AND IF DWARF IS PRESENT THEN ONE MIGHT BE KILLED.
	       (ONLY WAY TO D0 SO!)  AXE ALSO SPECIAL FOR DRAGON, BEAR, AND
	       TROLL.  TREASURES SPECIAL FOR TROLL.
(10241)	DO FORGET #1
	DO (10203) NEXT DO NOTE IF OBJECT EQ ROD AND TOTING(ROD2), OBJECT <- ROD2
	DO .3 <- #29
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO (30101) NEXT DO NOTE .1 <- TOTING(OBJECT).
	DO .2 <- #0
	DO .3 <- #29
	DO (240) NEXT DO NOTE IF NOT TOTING(OBJECT).
	DO (10242) NEXT DO NOTE IF OBJECT GE #50 AND #65 GE OBJECT AND AT(TROLL).
	DO (10243) NEXT DO NOTE IF OBJECT EQ FOOD AND HERE(BEAR).
	DO (10244) NEXT DO NOTE IF OBJECT EQ AXE
	DO (10201) NEXT DO NOTE DISCARD
	PLEASE STOP
	PLEASE SNARF A TREASURE FOR THE TROLL.
(10242)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #50
	DO (40012) NEXT DO NOTE OBJECT GE #50
	DO .1 <- #65
	DO .2 <- ,61000SUB#22 DO NOTE .2 <- OBJECT
	DO (40012) NEXT DO NOTE #65 GE OBJECT
	DO .1 <- #33 DO (30121) NEXT DO NOTE .1 <- AT(TROLL).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE AT(TROLL).
	DO FORGET #1
	DO .1 <- ,61000SUB#22
	DO .2 <- #0
	DO (30001) NEXT DO NOTE MOVE OBJECT TO #0
	DO .1 <- #33
	DO .2 <- #0
	DO (30001) NEXT DO NOTE MOVE TROLL TO #0
	DO ,60007SUB#33#1 <- #0 DO NOTE TROLL.PLAC <- #0
	DO ,60007SUB#33#2 <- #0 DO NOTE TROLL.FIXD <- #0
	DO .1 <- #34
	DO .2 <- ,61000SUB#1
	DO (30001) NEXT DO NOTE MOVE TROLL2 TO LOC
	DO ,60007SUB#34#1 <- #117 DO NOTE TROLL2.PLAC <- #117
	DO ,60007SUB#34#2 <- #122 DO NOTE TROLL2.FIXD <- #122
	DO .1 <- #32
	DO .2 <- ,61003SUB#32#1
	DO (30001) NEXT DO NOTE JUGGLE CHASM: MOVE CHASM TO CHASM.LOC
	DO .1 <- #159
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
	PLEASE FEED BEAR IF OBJECT EQ FOOD AND HERE(BEAR)
(10243)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #19
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ FOOD
	DO .1 <- #35 DO (30111) NEXT DO NOTE .1 <- HERE(BEAR).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 HERE(BEAR).
	DO FORGET #1
	DO ,61000SUB#22 <- #35 DO NOTE OBJECT <- BEAR
	DO (10251) NEXT DO NOTE FEED
	PLEASE STOP
(10244)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #28
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ AXE
	DO FORGET #1
	DO .4 <- #1 DO (10245) NEXT DO NOTE DWARF 1
	DO .4 <- #2 DO (10245) NEXT DO NOTE DWARF 2
	DO .4 <- #3 DO (10245) NEXT DO NOTE DWARF 3
	DO .4 <- #4 DO (10245) NEXT DO NOTE DWARF 4
	DO .4 <- #5 DO (10245) NEXT DO NOTE DWARF 5
	DO .3 <- #152
	DO [...IF AT(DRAGON) AND DRAGON.PROP EQ #0]
	DO .1 <- #33 DO (30121) NEXT DO NOTE .1 <- AT(TROLL).
	DO .2 <- #1
	DO .3 <- #158
	DO (10246) NEXT DO NOTE IF AT(TROLL).
	DO [...IF HERE(BEAR) AND BEAR.PROP EQ #0]
	DO ,61000SUB#22 <- #0 DO NOTE OBJECT <- #0
	DO (10271) NEXT DO NOTE ATTACK
	PLEASE STOP
(10245)	DO .1 <- ,61002SUB.4#1 DO NOTE .1 <- DLOC(.4).
	DO .2 <- ,61000SUB#1 DO NOTE .2 <- LOC
	DO (40011) NEXT DO NOTE .1=.2 DLOC(.4) EQ LOC
	DO FORGET #1
	DO .2 <- .1
	DO .3 <- #48
	DO %33 (10246) NEXT
	DO ,61002SUB.4#3 <- #0 DO NOTE DSEEN(.4) <- #0
	DO ,61002SUB.4#1 <- #0 DO NOTE DLOC(.4) <- #0
	DO .1 <- ,61000SUB#13 DO NOTE .1 <- DKILL
	DO (1020) NEXT
	DO ,61000SUB#13 <- .1 DO NOTE DKILL <- DKILL + #1
	DO .2 <- #1
	DO .3 <- #149
	DO .4 <- #47
	DO (40021) NEXT DO NOTE IF DKILL EQ #1, .1 <- #149, ELSE .1 <- #47
	DO .3 <- .1
	DO .2 <- .1
	DO (10246) NEXT
	PLEASE STOP
(10246)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- .3
	DO (55601) NEXT DO NOTE RSPEAK
	DO .1 <- #28 DO .2 <- ,61000SUB#1 DO (30001) NEXT DO NOTE MOVE AXE TO LOC
	DO .1 <- #21 DO NOTE .1 <- NULL
	DO (501) NEXT
	PLEASE STOP

	PLEASE FEED.  IF BIRD, NO SEED.  SNAKE, DRAGON, TROLL: QUIP.  IF DWARF,
	   MAKE HIM MAD.  BEAR, SPECIAL.
(10251)	DO FORGET #1
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #8
	DO .3 <- #100
	DO (240) NEXT DO NOTE IF OBJECT EQ BIRD
	DO .2 <- #33
	DO .3 <- #182
	DO (240) NEXT DO NOTE IF OBJECT EQ TROLL
	DO .2 <- #31
	DO (10252) NEXT DO NOTE FEED DRAGON
	DO .2 <- #17
	DO (10253) NEXT DO NOTE FEED DWARF
	DO .2 <- #11
	DO (10254) NEXT DO NOTE FEED SNAKE
	DO .2 <- #35
	DO (10255) NEXT DO NOTE FEED BEAR
	DO .1 <- #14
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10252)	DO (40011) NEXT DO NOTE .1=.2 FEED DRAGON
	DO FORGET #1
	DO .1 <- ,61003SUB#31#4 DO NOTE .1 <- DRAGON.PROP
	DO .2 <- #0
	DO .3 <- #102
	DO (240) NEXT DO NOTE IF DRAGON.PROP EQ #0
	DO .1 <- #110
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10253)	DO (40011) NEXT DO NOTE .1=.2 FEED DWARF
	DO FORGET #1
	DO .1 <- #19 DO (30111) NEXT DO NOTE .1 <- HERE(FOOD).
	DO .2 <- #0
	DO .3 <- #174
	DO (240) NEXT DO NOTE IF NOT HERE(FOOD).
	DO .1 <- ,61000SUB#10 DO NOTE .1 <- DFLAG
	DO (1020) NEXT
	DO ,61000SUB#10 <- #1 DO NOTE DFLAG <- DFLAG+#1
	DO .1 <- #103
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10254)	DO (40011) NEXT DO NOTE FEED SNAKE
	DO FORGET #1
	DO .1 <- ,61000SUB#26 DO NOTE .1 <- CLOSED
	DO .2 <- #1
	DO .3 <- #102
	DO (240) NEXT DO NOTE IF CLOSED
	DO .1 <- #8 DO (30111) NEXT DO NOTE .1 <- HERE(BIRD).
	DO .2 <- #0
	DO (240) NEXT DO NOTE IF NOT HERE(BIRD).
	DO .1 <- #8 DO .2 <- #65535 DO (30001) NEXT DO NOTE MOVE BIRD TO #65535
	DO ,61003SUB#8#4 <- #0 DO NOTE BIRD.PROP <- #0
	DO .1 <- ,61000SUB#30 DO NOTE .1 <- TALLY2
	DO (1020) NEXT
	DO ,61000SUB#30 <- .1 DO NOTE TALLY2 <- TALLY2+#1
	DO .1 <- #101
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10255)	DO (40011) NEXT DO NOTE .1=.2 FEED BEAR
	DO FORGET #1
	DO .1 <- #19 DO (30111) NEXT DO NOTE .1 <- HERE(FOOD).
	DO .2 <- #0
	DO (10256) NEXT DO NOTE IF NOT HERE(FOOD).
	DO .1 <- #19 DO .2 <- #0 DO (30001) NEXT DO NOTE MOVE FOOD TO #0
	DO ,61003SUB#35#4 <- #1 DO NOTE BEAR.PROP <- #1
	DO ,61003SUB#28#4 <- #0 DO NOTE AXE.PROP <- #0
	DO ,60007SUB#28#2 <- #0 DO NOTE AXE.FIXD <- #0
	DO .1 <- #168
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10256)	DO (40011) NEXT DO NOTE .1=.2 FEED BEAR NOT FOOD
	DO FORGET #1
	DO .1 <- ,61003SUB#35#4 DO NOTE .1 <- BEAR.PROP
	DO .2 <- #0
	DO .3 <- #102
	DO (240) NEXT DO NOTE IF BEAR.PROP EQ #0
	DO .2 <- #3
	DO .3 <- #110
	DO (240) NEXT DO NOTE IF BEAR.PROP EQ #3
	DO .1 <- #174
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP

	PLEASE FEE FIE FOE FOO (AND FUM).  ADVANCE TO NEXT STATE IF GIVEN IN
	       PROPER ORDER.  LOOK UP WD1 IN SECTION 3 OF VOCAB TO DETERMINE
	       WHICH WORD WE'VE GOT.  LAST WORD ZIPS THE EGGS BACK TO THE GIANT
	       ROOM (UNLESS ALREADY THERE).
(10261)	DO FORGET #1
	DO .1 <- ,51001SUB#3#4 DO NOTE .1 <- CURRENT WORD INDEX INDEX
	DO .2 <- ,51001SUB#3.1 DO NOTE .2 <- CURRENT WORD INDEX
	DO .1 <- ',61000SUB#14'~#32767
	DO (1020) NEXT DO NOTE .1 <- FOOBAR+#1
	DO (10263) NEXT DO NOTE IF NEXT IN SEQUENCE
	DO .2 <- #1
	DO (10262) NEXT DO NOTE IF FIRST IN SEQUENCE
	DO ,61000SUB#14 <- #0 DO NOTE FOOBAR <- #0
	DO .1 <- #151
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10262)	DO (40011) NEXT DO NOTE .1=.2 FOOBAR EQ #0
	DO FORGET #1
	DO .1 <- ,51001SUB#3#4 DO NOTE .1 <- CURRENT WORD INDEX INDEX
	DO .1 <- ,51001SUB#3.1 DO NOTE .1 <- CURRENT WORD INDEX
	DO .2 <- #255
	DO (10263) NEXT DO NOTE IF FEE
	DO .1 <- #42
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10263)	DO (40011) NEXT DO NOTE .1=.2 FEE/FIE/FOE/FOO IN ORDER
	DO FORGET #1
	DO ,61000SUB#14 <- .1 DO NOTE FOOBAR <- .1
	DO .2 <- #258
	DO (10264) NEXT DO NOTE IF FOO
	DO .1 <- #54
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10264)	DO (40011) NEXT DO NOTE .1=.2 FOO
	DO FORGET #1
	DO ,61000SUB#14 <- #0 DO NOTE FOOBAR <- #0
	DO .1 <- ,61003SUB#56#1 DO NOTE .1 <- EGGS.LOC
	DO .2 <- #92
	DO .3 <- #42
	DO (240) NEXT DO NOTE IF EGGS.LOC EQ GIANT ROOM
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #92
	DO (40002) NEXT DO NOTE .3 <- LOC EQ GIANT ROOM
	DO .1 <- #56 DO (30101) NEXT DO NOTE .1 <- TOTING(EGGS).
	DO .1 <- .1¢.3
	DO .2 <- #3
	DO .3 <- #42
	DO (240) NEXT DO NOTE IF LOC EQ GIANT ROOM AND TOTING(EGGS).
	DO (10265) NEXT DO NOTE BRING BACK TROLL IF STEAL EGGS BACK BEFORE CROSSING
	DO .1 <- #56 DO (30111) NEXT DO NOTE .1 <- HERE(EGGS).
	DO .2 <- #1
	DO .3 <- #3
	DO .4 <- #4
	DO (40021) NEXT DO NOTE IF HERE(EGGS), .1 <- #3, ELSE .1 <- #4
	DO .3 <- #2
	DO .4 <- .1
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #92
	DO (40021) NEXT DO NOTE IF LOC EQ GIANT ROOM, .1 <- #2, ELSE #3/#4
	DO .2 <- .1
	DO .1 <- #56
	DO (55501) NEXT DO NOTE PSPEAK
	DO .1 <- #56 DO .2 <- #92 DO (30001) NEXT DO NOTE MOVE EGGS TO GIANT ROOM
	DO (241) NEXT
	PLEASE STOP
	PLEASE BRING BACK TROLL IF WE STEAL THE EGGS BACK FROM HIM BEFORE CROSSING.
(10265)	DO .1 <- ,61003SUB#56#1 DO NOTE .1 <- EGGS.LOC
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- ,61003SUB#33#1 DO NOTE .1 <- TROLL.LOC
	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- ,61003SUB#33#4 DO NOTE .1 <- TROLL.PROP
	DO (40011) NEXT DO NOTE .1=.2
	DO ,61003SUB#33#4 <- #1 DO NOTE TROLL.PROP <- #0
	DO RESUME #1
	PLEASE STOP

	PLEASE ATTACK.  ASSUME TARGET IF UNAMBIGUOUS.  "THROW" ALSO LINKS HERE.
	       ATTACKABLE OBJECTS FALL INTO TWO CATEGORIES: ENEMIES (SNAKE,
	       DWARF, ETC.)  AND OTHERS (BIRD, CLAM).  AMBIGUOUS IF TWO
	       ENEMIES, OR IF NO ENEMIES BUT TWO OTHERS.
(10271)	DO FORGET #1
	DO (10288) NEXT DO NOTE CHOOSE IMPLICIT OBJECT
	DO (10277) NEXT DO NOTE HANDLE BIRD
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #0
	DO .3 <- #44 DO NOTE "THERE IS NOTHING HERE TO ATTACK."
	DO (240) NEXT DO NOTE IF OBJECT EQ #0
	DO .2 <- #14
	DO .3 <- #150 DO NOTE "THE SHELL IS VERY STRONG AND IS IMPERVIOUS TO ATTACK."
	DO (240) NEXT DO NOTE IF OBJECT EQ CLAM
	DO .2 <- #15
	DO (240) NEXT DO NOTE IF OBJECT EQ OYSTER
	DO .2 <- #11
	DO .3 <- #46 DO NOTE "ATTACKING THE SNAKE BOTH D0ESN'T WORK AND IS VERY DANGEROUS."
	DO (240) NEXT DO NOTE IF OBJECT EQ SNAKE
	DO (10278) NEXT DO NOTE HANDLE DWARF AND CLOSED
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #17
	DO .3 <- #49
	DO (240) NEXT DO NOTE IF OBJECT EQ DWARF
	DO .2 <- #33
	DO .3 <- #157
	DO (240) NEXT DO NOTE IF OBJECT EQ TROLL
	DO .1 <- #165
	DO .2 <- "'&,61003SUB#35#4'~#1"¢"'∀,61003SUB#35#4'~#1"
	DO (1000) NEXT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #35
	DO (240) NEXT DO NOTE IF OBJECT EQ BEAR
	DO .2 <- #31
	DO (40003) NEXT DO NOTE .3 <- OBJECT NE DRAGON
	DO .1 <- .3
	DO .2 <- #1
	DO .3 <- #110
	DO (240) NEXT DO NOTE IF OBJECT NE DRAGON
	DO .1 <- ,61003SUB#31#4 DO NOTE .1 <- DRAGON.PROP
	DO .2 <- #2
	DO .3 <- #167
	DO (240) NEXT DO NOTE IF DRAGON.PROP EQ #2
	DO .1 <- #49
	DO (55601) NEXT DO NOTE RSPEAK
	DO ,61000SUB#21 <- #0 DO NOTE VERB <- #0
	DO ,61000SUB#22 <- #0 DO NOTE OBJ <- #0
	DO,60006SUB#13#2<-#62  DO,60006SUB#13#3<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (51001) NEXT DO NOTE CALL GETIN
	DO (10272) NEXT DO NOTE IF WD1(4..5) NE #0
	DO .1 <- ,51001SUB#3#7
	DO .2 <- #25
	DO (10273) NEXT DO NOTE IF WD1 EQ "Y", KILL DRAGON
	DO .2 <- #19641
	DO (10273) NEXT DO NOTE IF WD1 EQ "YES", KILL DRAGON
	DO (309) NEXT
	PLEASE STOP
(10272)	DO .1 <- ,51001SUB#3#8
	DO .2 <- #0
	DO (40002) NEXT DO NOTE .3 <- WD1(4..5) EQ #0
	DO .1 <- .3
	DO (40011) NEXT DO NOTE WD1(4..5) NE #0
	DO FORGET #1
	DO (309) NEXT
	PLEASE STOP
	PLEASE FUN STUFF FOR DRAGON.  IF HE INSISTS ON ATTACKING IT, WIN!  SET
	       PROP TO DEAD, MOVE DRAGON TO CENTRAL LOC (STILL FIXED), MOVE RUG
	       THERE (NOT FIXED), AND MOVE HIM THERE, TOO.  THEN D0 A NULL
	       MOTION TO GET NEW DESCRIPTION.
(10273)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #31
	DO .2 <- #3
	DO (55501) NEXT DO NOTE PSPEAK
	DO ,61003SUB#31#4 <- #2 DO NOTE DRAGON.PROP <- #2
	DO ,60007SUB#31#2 <- #65535 DO NOTE DRAGON.FIXD <- #65535
	DO .1 <- #31 DO .2 <- #120 DO (30001) NEXT DO NOTE MOVE DRAGON TO #120
	DO ,61003SUB#62#4 <- #0 DO NOTE RUG.PROP <- #0
	DO ,60007SUB#62#2 <- #0 DO NOTE RUG.FIXD <- #0
	DO .1 <- #62 DO .2 <- #120 DO (30001) NEXT DO NOTE MOVE RUG TO #120
	DO .3 <- #1
	DO (10274) NEXT
	PLEASE STOP
	PLEASE FOR .3 IN #1 .. #64, MOVE OBJECTS IN #119 OR #121 TO #120
(10274)	DO FORGET #1
	DO .1 <- ,61003SUB.3#1 DO NOTE .1 <- OBJ.LOC
	DO .2 <- #119
	DO (10275) NEXT
	DO .2 <- #121
	DO (10275) NEXT
	DO .1 <- .3
	DO .2 <- #64
	DO (10276) NEXT
	DO (1020) NEXT
	DO .3 <- .1 DO NOTE NEXT OBJ
	DO (10274) NEXT
(10275)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- .3 DO .2 <- #120 DO (30001) NEXT DO NOTE MOVE .1 TO #120
	DO RESUME #1
	PLEASE STOP
(10276)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO ,61000SUB#1 <- #120 DO NOTE LOC <- #120
	DO .1 <- #21 DO NOTE .1 <- NULL
	DO (501) NEXT
	PLEASE STOP
(10277)	DO .1 <- ,61000SUB#22
	DO .2 <- #8
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ BIRD
	DO FORGET #1
	DO .1 <- ,61000SUB#26 DO NOTE .1 <- CLOSED
	DO .2 <- #1
	DO .3 <- #137
	DO (240) NEXT DO NOTE IF CLOSED
	DO .2 <- #0
	DO (30001) NEXT DO NOTE MOVE BIRD TO #0
	DO ,61003SUB#8#4 <- #0 DO NOTE BIRD.PROP <- #0
	DO .1 <- ,61003SUB#11#1 DO NOTE .1 <- SNAKE.LOC
	DO .2 <- #19 DO NOTE .2 <- HALL OF MOUNTAIN KING
	DO .3 <- #1
	DO .4 <- #0
	DO (40021) NEXT DO NOTE IF SNAKE.LOC EQ HALL OF MT KING .1 <- #1, ELSE .1 <- #0
	DO .2 <- ,61000SUB#30 DO NOTE .2 <- TALLY2
	DO (1000) NEXT
	DO ,61000SUB#30 <- .3 DO NOTE IF SNAKE.LOC EQ HALL OF MT KING, TALLY2 <- TALLY2+#1
	DO .1 <- #45
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10278)	DO .1 <- ,61000SUB#22
	DO .2 <- #17
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ DWARF
	DO .1 <- ,61000SUB#26
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2 CLOSED
	DO .1 <- #136
	DO (55601) NEXT DO NOTE RSPEAK
	DO (10051) NEXT DO NOTE EXIT
	PLEASE STOP
(10288)	DO .1 <- ,61000SUB#22
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ #0
	DO [...CHOOSE IMPLICIT KILL OBJ]
	DO RESUME #1
	PLEASE STOP

	PLEASE READ.  MAGAZINES IN DWARVISH, MESSAGE WE'VE SEEN,
	       AND . . . OYSTER?
(10291)	DO FORGET #1
	DO (10297) NEXT DO NOTE CHOOSE IMPLICIT OBJECT
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #0
	DO (612) NEXT DO NOTE "<VERB> WHAT?"
	DO (30161) NEXT DO NOTE .1 <- DARK().
	DO .2 <- #1
	DO (735) NEXT DO NOTE "I SEE NO <OBJ> HERE."
	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #16 DO NOTE MAGAZINE
	DO .3 <- #190
	DO (240) NEXT
	DO .2 <- #13 DO NOTE TABLET
	DO .3 <- #196
	DO (240) NEXT
	DO .2 <- #36 DO NOTE MESSAGE
	DO .3 <- #191
	DO (240) NEXT
	DO [...READ OYSTER...]
	DO (241) NEXT
(10297)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #0
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ #0
	DO (30161) NEXT DO NOTE .1 <- DARK().
	DO .2 <- #0
	DO (10299) NEXT
	DO .3 <- #0
	DO [...CHOOSE IMPLICIT READ OBJ] DO FORGET #1 DO (241) NEXT
	PLEASE STOP
(10299)	DO (40003) NEXT DO NOTE .3 <- .1 NE .2
	DO .1 <- .3
	DO .2 <- #1
	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #2
	DO (612) NEXT DO NOTE "<VERB> WHAT?"
	PLEASE STOP

	PLEASE LOOK.  CAN'T GIVE MORE DETAIL.  PRETEND IT WASN'T DARK (THOUGH
	       IT MAY "NOW" BE DARK) SO HE WON'T FALL INTO A PIT WHILE STARING
	       INTO THE GLOOM.
(10301)	DO FORGET #1
	DO .1 <- ,61000SUB#8 DO NOTE .1 <- DETAIL
	DO (1020) NEXT
	DO ,61000SUB#8 <- .1 DO NOTE DETAIL <- DETAIL+#1
	DO .2 <- #1 DO (10302) NEXT
	DO .2 <- #2 DO (10302) NEXT
	DO .2 <- #3 DO (10302) NEXT
	DO ,61000SUB#20 <- #0 DO NOTE WZDARK <- #0
	DO ,61001SUB",61000SUB#1"#2 <- #0 DO NOTE ABB(LOC) <- #0
	DO (5) NEXT
	PLEASE STOP
(10302)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- #15
	DO (55601) NEXT DO NOTE RSPEAK
	DO RESUME #1
	PLEASE STOP

	PLEASE WAVE.  NO EFFECT UNLESS WAVING ROD AT FISSURE.
(10311)	DO FORGET #1
	DO (10313) NEXT DO NOTE IF WAVING ROD AT FISSURE
	DO (10312) NEXT DO NOTE IF OBJECT EQ ROD AND TOTING(ROD2), OBJECT <- ROD2
	DO .1 <- ,61000SUB#22 DO (30101) NEXT DO NOTE .1 <- TOTING(OBJ).
	DO .2 <- #0
	DO .3 <- #29 DO NOTE "YOU AREN'T CARRYING IT!"
	DO (240) NEXT DO NOTE IF NOT TOTING(OBJ).
	DO .1 <- #42 DO NOTE "NOTHING HAPPENS."
	DO (55601) NEXT DO NOTE RSPEAK
	DO (241) NEXT
	PLEASE STOP
(10312)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #5
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ ROD
	DO .1 <- #6 DO (30101) NEXT DO NOTE .1 <- TOTING(ROD2).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE TOTING(ROD2).
	DO ,61000SUB#22 <- #6 DO NOTE OBJECT <- ROD2
	DO RESUME #1
	PLEASE STOP
(10313)	DO .1 <- ,61000SUB#22 DO NOTE .1 <- OBJECT
	DO .2 <- #5
	DO (40011) NEXT DO NOTE .1=.2 OBJECT EQ ROD
	DO .1 <- #5 DO (30101) NEXT DO NOTE .1 <- TOTING(ROD).
	DO .2 <- #1
	DO (40011) NEXT DO NOTE TOTING(ROD).
	DO .1 <- #12 DO (30121) NEXT DO NOTE .1 <- AT(FISSURE).
	DO (40011) NEXT DO NOTE AT(FISSURE).
	DO .1 <- ,61000SUB#19 DO NOTE .1 <- CLOSNG
	DO .2 <- #0
	DO (40011) NEXT DO NOTE NOT CLOSNG
	DO FORGET #1
	DO ,61003SUB#12#4 <- "∀',61003SUB#12#4'¢#1"~#1 DO NOTE FISSURE.PROP <- NOT FISSURE.PROP
	DO .1 <- #4
	DO .2 <- ,61003SUB#12#4 DO NOTE .2 <- FISSURE.PROP
	DO (1010) NEXT DO NOTE .3 <- #4 - FISSURE.PROP
	DO .1 <- #12
	DO .2 <- .3
	DO (55501) NEXT DO NOTE PSPEAK
	DO (241) NEXT
	PLEASE STOP

	PLEASE BLAST.  NO EFFECT UNLESS YOU'VE GOT DYNAMITE, WHICH IS A NEAT
	       TRICK!
(10321)	DO FORGET #1
	DO .1 <- ,61003SUB#6#5 DO NOTE .1 <- ROD2.PROPNEG
	DO .2 <- #1
	DO .3 <- #67
	DO (240) NEXT DO NOTE IF ROD2.PROPNEG
	DO .1 <- ,61000SUB#26 DO NOTE .1 <- CLOSED
	DO .2 <- #0
	DO (240) NEXT DO NOTE IF NOT CLOSED
	DO .1 <- ,61000SUB#1 DO NOTE .1 <- LOC
	DO .2 <- #115
	DO .3 <- #134
	DO .4 <- #133
	DO (40021) NEXT DO NOTE IF LOC EQ #115, .1 <- #134, ELSE .2 <- #133
	DO .4 <- .1
	DO .3 <- #135
	DO .1 <- #6 DO (30111) NEXT DO NOTE .1 <- HERE(ROD2).
	DO .2 <- #1
	DO (40021) NEXT DO NOTE IF HERE(ROD2), .1 <- #135
	DO ,61000SUB#15 <- .1 DO NOTE BONUS <- .1
	DO (55601) NEXT DO NOTE RSPEAK
	DO (10051) NEXT
	PLEASE STOP



	PLEASE MOVE OBJECT .1 TO LOCATION .2
	       BY UNLINKING IT FROM ITS CURRENT LOCATION LIST (IF NOT #0)
	       AND ADDING IT TO THE HEAD OF ITS NEW LOCATION LIST (IF NOT #0)
(30001)	DO STASH .1 + .2 + .3 + .4 + .5 + .6 + .7
	DO NOT [MOVE OBJ] .1
	DO NOT [MOVE FROM] ,61003SUB.1#1
	DO NOT [MOVE TO] .2
	DO .3 <- .1 DO NOTE .3 <- OBJ
	DO .4 <- .2 DO NOTE .4 <- NEWLOC
	DO .5 <- ,61003SUB.3#1 DO NOTE .5 <- OBJ.LOC
	DO .6 <- ,61003SUB.3#2 DO NOTE .6 <- OBJ.NEXTOBJ
	DO .7 <- ,61003SUB.3#3 DO NOTE .7 <- OBJ.PREVOBJ
	DO NOTE CHECK IF OBJ.LOC=#0
	DO .1 <- .5
	DO .2 <- #0
	DO (30002) NEXT
	DO NOTE CHECK IF OBJ.LOC=#65535
	DO .2 <- #65535
	DO (30002) NEXT
	DO .2 <- #0
	DO NOTE CHECK IF OBJ.PREVOBJ=#0
	DO .1 <- .7
	DO (30003) NEXT
	DO ,61003SUB.7#2 <- .6 DO NOTE PREVOBJ.NEXTOBJ <- NEXTOBJ
	DO (30004) NEXT
	PLEASE STOP
(30002)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE OBJ.LOC=#0 OR #65535
	DO (30006) NEXT
	PLEASE STOP
(30003)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE OBJ.PREVOBJ=#0
	DO ,61001SUB.5#1 <- .6 DO NOTE OLDLOC.FIRSTOBJ <- NEXTOBJ
	DO (30004) NEXT
	PLEASE STOP
(30004)	DO FORGET #1
	DO NOTE CHECK IF LAST IN LIST
	DO .1 <- .6 DO NOTE .1 <- NEXTOBJ
	DO (30005) NEXT
	DO ,61003SUB.6#3 <- .7 DO NOTE NEXTOBJ.PREVOBJ <- PREVOBJ
	DO (30006) NEXT
	PLEASE STOP
(30005)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE OBJ.NEXTOBJ=#0
	DO (30006) NEXT
	PLEASE STOP
(30006)	DO FORGET #1
	DO ,61003SUB.3#1 <- .4 DO NOTE OBJ.LOC <- NEWLOC
	DO ,61003SUB.3#2 <- #0 DO NOTE OBJ.NEXTOBJ <- #0
	DO ,61003SUB.3#3 <- #0 DO NOTE OBJ.PREVOBJ <- #0
	DO NOTE CHECK IF NEW LOCATION=#0
	DO .1 <- .4
	DO .2 <- #0
	DO (30007) NEXT
	DO NOTE CHECK IF NEW LOCATION=#65535
	DO .1 <- .4
	DO .2 <- #65535
	DO (30007) NEXT
	DO .2 <- #0
	DO .1 <- ,61001SUB.4#1 DO NOTE .1 <- NEWLOC.FIRSTOBJ
	DO ,61001SUB.4#1 <- .3 DO NOTE NEWLOC.FIRSTOBJ <- OBJ
	DO NOTE CHECK IF NEWLOC.FORMERFIRSTOBJ=#0
	DO (30007) NEXT
	DO ,61003SUB.1#3 <- .3 DO NOTE NEWLOC.FORMERFIRSTOBJ.PREVOBJ <- OBJ
	DO ,61003SUB.3#2 <- .1 DO NOTE OBJ.NEXTOBJ <- NEWLOC.FORMERFIRSTOBJ
	DO RETRIEVE .1 + .2 + .3 + .4 + .5 + .6 + .7
	DO RESUME #1
	PLEASE STOP
(30007)	DO (40011) NEXT DO NOTE .1=.2
	DO RETRIEVE .1 + .2 + .3 + .4 + .5 + .6 + .7
	DO RESUME #2
	PLEASE STOP

	PLEASE MOVE OBJECT .1 TO LOCATION .2
	       BY UNLINKING IT FROM ITS CURRENT LOCATION LIST (IF NOT #0)
	       AND ADDING IT TO THE TAIL OF ITS NEW LOCATION LIST (IF NOT #0)
(30011)	DO STASH .1 + .2 + .3 + .4 + .5
	DO .3 <- .1
	DO .4 <- .2
	DO .2 <- #0
	DO (30001) NEXT
	DO .1 <- .4
	DO (30012) NEXT
	DO ,61003SUB.3#1 <- .4 DO NOTE OBJ.LOC <- NEWLOC
	DO .1 <- ,61001SUB.4#1 DO NOTE .1 <- NEWLOC.FIRSTOBJ
	DO (30013) NEXT
	DO .5 <- .1 DO NOTE .5 <- NEWLOCOBJ
	DO (30014) NEXT
	PLEASE STOP
(30012)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE MOVING TO #0
	DO RETRIEVE .1 + .2 + .3 + .4 + .5
	DO RESUME #1
	PLEASE STOP
(30013)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE FIRST OBJECT IN NEWLOC
	DO ,61001SUB.4#1 <- .3 DO NOTE NEWLOC.FIRSTOBJ <- OBJ
	DO RETRIEVE .1 + .2 + .3 + .4 + .5
	DO RESUME #1
	PLEASE STOP
(30014)	DO FORGET #1
	DO .1 <- ,61003SUB.5#2 DO NOTE .1 <- NEWLOCOBJ.NEXTOBJ
	DO (30015) NEXT
	DO .5 <- .1 DO NOTE .5 <- NEWLOCOBJ.NEXTOBJ
	DO (30014) NEXT
	PLEASE STOP
(30015)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO NOTE NEWLOCOBJ.NEXTOBJ=#0
	DO ,61003SUB.3#3 <- .5 DO NOTE OBJ.PREVOBJ <- NEWLOCOBJ
	DO ,61003SUB.5#2 <- .3 DO NOTE NEWLOCOBJ.NEXTOBJ <- OBJ
	DO RETRIEVE .1 + .2 + .3 + .4 + .5
	DO RESUME #1
	PLEASE STOP

	PLEASE MOVE OBJECTS IN MULTIPLE LOCATIONS THAT INCLUDE THE CURRENT
	       LOCATION TO THE CURRENT LOCATION
(30021)	DO STASH .1 + .2 + .3 + .4
	DO .4 <- #1
	DO (30022) NEXT
	PLEASE STOP
(30022)	DO FORGET #1
	DO .1 <- ,60007SUB.4#2 DO NOTE .1 <- INITIAL SECOND LOCATION
	DO .2 <- #0
	DO (30025) NEXT
	DO .2 <- #65535
	DO (30025) NEXT
	DO .2 <- ,61000SUB#1 DO NOTE .2 <- LOC
	DO (30026) NEXT
	DO .1 <- ,60007SUB.4#1 DO NOTE .1 <- INITIAL LOCATION
	DO (30026) NEXT
	DO (30023) NEXT
	PLEASE STOP
(30023)	DO FORGET #1
	DO .1 <- .4
	DO .2 <- #64
	DO (30024) NEXT
	DO (1020) NEXT
	DO .4 <- .1
	DO (30022) NEXT
	PLEASE STOP
(30024)	DO (40011) NEXT DO NOTE .1=.2
	DO RESUME #2
	PLEASE STOP
(30025)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (30023) NEXT
	PLEASE STOP
(30026)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- .4
	DO (30011) NEXT DO NOTE MOVEOBJ_TO_END(.1 TO .4).
	DO (30023) NEXT
	PLEASE STOP

	PLEASE .1 <- TOTING(.1) #0 IS FALSE, #1 IS TRUE, .1 IS OBJ
(30101)	DO .1 <- '∀"'#65535~"'∀",61003SUB.1#1"¢#65535'~'#0¢#65535'"'~#1"¢#1'~#1 DO NOTE .1 <- OBJ.LOC=#65535
	DO RESUME #1
	PLEASE STOP

	PLEASE .1 <- HERE(.1) #0 IS FALSE, #1 IS TRUE, .1 IS OBJ
(30111)	DO STASH .2
	DO .2 <- "∀'"#65535~'"∀',61003SUB.1#1'¢',61000SUB#1'"~"#0¢#65535"'"~#1'¢#1"~#1 DO NOTE .2 <- OBJ.LOC=LOC
	DO (30101) NEXT DO NOTE .1 <- TOTING(OBJ).
	DO .1 <- 'V.1¢.2'~#1 DO NOTE .1 <- .1 OR .2
	DO RETRIEVE .2
	DO RESUME #1
	PLEASE STOP

	PLEASE .1 <- AT(.1) #0 IS FALSE, #1 IS TRUE, .1 IS OBJ
	       NO NEED TO CHECK OBJ.FIXED BECAUSE (30021) MOVES FIXED OBJECTS
(30121)	DO .1 <- "∀'"#65535~'"∀',61003SUB.1#1'¢',61000SUB#1'"~"#0¢#65535"'"~#1'¢#1"~#1 DO NOTE .1 <- OBJ.LOC=LOC
	DO RESUME #1
	PLEASE STOP

	PLEASE .1 <- LIQ() #0 IS FALSE, #1 IS TRUE
	       ... NOTE IMPLEMENTED YET
(30131)	DO .1 <- #0
	DO RESUME #1
	PLEASE STOP

	PLEASE .1 <- LIQLOC(.1) #0 IS FALSE, #1 IS TRUE, .1 IS LOC
	       ... NOTE IMPLEMENTED YET
(30141)	DO .1 <- #0
	DO RESUME #1
	PLEASE STOP

	PLEASE .1 <- FORCED(.1) #0 IS FALSE, #1 IS TRUE, .1 IS LOC
(30151)	DO .1 <- '∀"'#65535~"'∀#2¢,60009SUB.1'~'#0¢#65535'"'~#1"¢#1'~#1
	DO RESUME #1
	PLEASE STOP

	PLEASE .1 <- COND(.1,.2) #0 IS FALSE, #1 IS TRUE, .1 IS LOC, .2 IS FLAG
(30152)	DO .1 <- '∀"'#65535~"'∀.2¢,60009SUB.1'~'#0¢#65535'"'~#1"¢#1'~#1
	DO RESUME #1
	PLEASE STOP

	PLEASE .1 <- DARK() #0 IS FALSE, #1 IS TRUE
(30161)	DO STASH .2
	DO .1 <- #2
	DO (30111) NEXT DO NOTE .1 <- HERE(LAMP).
	DO .1 <- '∀.1¢#1'~#1 DO NOTE .1 <- NOT HERE(LAMP).
	DO .2 <- '∀"'#255~,61003SUB#2#4'~#1"¢#1'~#1 DO NOTE .2 <- LAMP.PROP=#0
	DO .1 <- 'V.1¢.2'~#1 DO NOTE .1 <- .1 OR .2
	DO .2 <- '∀"',60009SUB",61000SUB#1"'~#1"¢#1'~#1 DO NOTE .2 <- NOT LOC.COND.LIGHT
	DO .1 <- '&.1¢.2'~#1 DO NOTE .1 <- .1 AND .2
	DO RETRIEVE .2
	DO RESUME #1
	PLEASE STOP

	PLEASE .1 <- HOLDNG
(30171)	DO STASH .2 + .3 + .4
	DO .3 <- #1
	DO .4 <- #0
	DO (30172) NEXT
	PLEASE STOP
(30172)	DO FORGET #1
	DO .1 <- ,61003SUB.3#1
	DO .2 <- #65535
	DO (30173) NEXT
	DO .1 <- .3
	DO .2 <- #64
	DO (30174) NEXT
	DO (1020) NEXT
	DO .3 <- .1
	DO (30172) NEXT
	PLEASE STOP
(30173)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- .4
	DO (1020) NEXT
	DO .4 <- .1
	DO RESUME #1
	PLEASE STOP
(30174)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- .4
	DO RETRIEVE .2 + .3 + .4
	DO RESUME #2



	PLEASE COMPUTE .3 <- .1 MOD .2
(40001)	DO STASH .1 + .2 + .4
	DO .4 <- .1
	DO (1040) NEXT
	DO .1 <- .3
	DO (1030) NEXT
	DO .1 <- .4
	DO .2 <- .3
	DO (1010) NEXT
	DO RETRIEVE .1 + .2 + .4
	DO RESUME #1
	PLEASE STOP

	PLEASE COMPUTE .3 <- .1 EQ .2
(40002)	DO .3 <- "∀'"#65535~'"∀.1¢.2"~"#0¢#65535"'"~#1'¢#1"~#1
	DO RESUME #1
	PLEASE STOP

	PLEASE COMPUTE .3 <- .1 NE .2
(40003)	DO .3 <- "&'"#65535~'"∀.1¢.2"~"#0¢#65535"'"~#1'¢#1"
	DO RESUME #1
	PLEASE STOP

	PLEASE COMPUTE .3 <- .1 LT .2
(40004)	DO (1010) NEXT
	DO .3 <- .3~#32768
	DO RESUME #1
	PLEASE STOP

	PLEASE COMPUTE .3 <- .1 OR .2
(40005)	DO .3 <- "V!1~#1'¢!2~#1'"~#1
	DO RESUME #1
	PLEASE STOP

	PLEASE COMPUTE .3 <- .1 AND .2
(40006)	DO .3 <- "&!1~#1'¢!2~#1'"~#1
	DO RESUME #1
	PLEASE STOP

	PLEASE RESUME #1 IF .1 EQUALS .2 OTHERWISE RESUME #2
(40011)	DO RESUME "∀'"#65535~'"∀.1¢.2"~"#0¢#65535"'"~#1'¢#1"~#3
	PLEASE STOP

	PLEASE RESUME #1 IF .1 >= .2 OTHERWISE RESUME #2
(40012)	DO STASH .3
	DO (1010) NEXT
	DO FORGET .3~#32768
	DO RETRIEVE .3
	DO RESUME #1
	PLEASE STOP

	PLEASE COMPUTE IF .1 EQ .2, .1 <- .3, ELSE .1 <- .4
(40021)	DO (40022) NEXT
	DO .1 <- .4
	DO RESUME #1
	PLEASE STOP
(40022)	DO (40011) NEXT
	DO .1 <- .3
	DO RESUME #2
	PLEASE STOP

	PLEASE COMPUTE IF .1 GE .2, .1 <- .3, ELSE .1 <- .4
(40023)	DO (40024) NEXT
	DO .1 <- .4
	DO RESUME #1
	PLEASE STOP
(40024)	DO (40012) NEXT
	DO .1 <- .3
	DO RESUME #2
	PLEASE STOP



	PLEASE INITIALIZE INPUT/OUTPUT
	PLEASE CHOOSE PUKE BINARY I/O OR C-INTERCAL BINARY I/O
		BY ABSTAINING FROM (50000) FOR PUKE BINARY I/O OR
		REINSTATING (50000) FOR C-INTERCAL BINARY I/O
(50000)	DON'T (50001) NEXT
	DO ,50121 <- #2
	DO REINSTATE (50011)
	DO ABSTAIN FROM (50012)
	DO REINSTATE (50021)
	DO ABSTAIN FROM (50022)
	DO (50001) NEXT
	PLEASE STOP
(50001)	DO RESUME #2
	PLEASE STOP

	PLEASE READ OUT IN ONE BYTE FROM .1
	PLEASE RETAIN .50211 BETWEEN INVOCATIONS WHEN USING C-INTERCAL BINARY I/O
(50011)	DON'T (50111) NEXT
(50012) DO (50211) NEXT
	DO RESUME #1
	PLEASE STOP

	PLEASE WRITE IN ONE BYTE INTO .1
	PLEASE RETAIN ,50121 BETWEEN INVOCATIONS WHEN USING PUKE BINARY I/O
	PLEASE RETAIN .50221 BETWEEN INVOCATIONS WHEN USING C-INTERCAL BINARY I/O
(50021)	DON'T (50121) NEXT
(50022)	DO (50221) NEXT
	DO RESUME #1
	PLEASE STOP

	PLEASE READ OUT ONE BYTE FROM .1 USING PUKE BINARY I/O
(50111)	DO STASH ,8
	DO ,8 <- #8
	DO ,8SUB#1 <- .1~#1
	DO ,8SUB#2 <- !1~#2'¢#0
	DO ,8SUB#3 <- !1~#4'¢!1~#4'
	DO ,8SUB#4 <- #0¢"!1~#8'¢#0"
	DO ,8SUB#5 <- #0¢"!1~#16'¢!1~#16'"
	DO ,8SUB#6 <- !1~#32'¢"!1~#32'¢#0"
	DO ,8SUB#7 <- !1~#64'¢"!1~#64'¢!1~#64'"
	DO ,8SUB#8 <- "!1~#128'¢#0"¢#0
	DO READ OUT ,8
	DO RETRIEVE ,8
	DO RESUME #1
	PLEASE STOP

	PLEASE WRITE IN ONE BYTE TO .1 USING PUKE BINARY I/O
	PLEASE RETAIN ,50121 BETWEEN INVOCATIONS
(50121)	DO STASH .2 + .3 + .50120 + .50121
	DO .50120 <- #1
	DO .50121 <- #0
	DO (50122) NEXT
	PLEASE STOP
(50122)	DO FORGET #1
	DO (50123) NEXT
	DO (50130) NEXT
	PLEASE STOP
(50123)	DO (50124) NEXT
	DO .1 <- .50121
	DO RETRIEVE .2 + .3 + .50120 + .50121
	DO RESUME #2
	PLEASE STOP
(50124)	DO RESUME "∀'"#255~!50120~#255'"~#1'¢#1"~#3
	PLEASE STOP
(50130)	DO FORGET #1
	DO (50131) NEXT
	DO .1 <- ,50121SUB#1
	DO .2 <- #1
	DO (1010) NEXT
	DO ,50121SUB#1 <- .3
	DO .50120 <- !50120¢#0'~'#32767¢#1'
	DO (50122) NEXT
	PLEASE STOP
(50131)	DO (50132) NEXT
	DO FORGET #1
	DO (50140) NEXT
	PLEASE STOP
(50132)	DO RESUME "∀'"#65535~,50121SUB#1"~#1'¢#1"~#3
	PLEASE STOP
(50140)	DO FORGET #1
	DO (50141) NEXT
	DO .1 <- ,50121SUB#2
	DO .2 <- #1
	DO (1010) NEXT
	DO ,50121SUB#2 <- .3
	DO .1 <- .50120
	DO .2 <- .50121
	DO (1000) NEXT
	DO .50120 <- !50120¢#0'~'#32767¢#1'
	DO .50121 <- .3
	DO (50122) NEXT
	PLEASE STOP
(50141)	DO (50142) NEXT
	DO FORGET #1
	DO (50150) NEXT
	PLEASE STOP
(50142)	DO RESUME "∀'"#65535~,50121SUB#2"~#1'¢#1"~#3
	PLEASE STOP
(50150)	DO FORGET #1
	DO STASH ,50150
	DO ,50150 <- #1
	DO WRITE IN ,50150
	DO ,50121SUB#1 <- ,50150SUB#1
	DO ,50121SUB#2 <- #0
	DO RETRIEVE ,50150
	DO (50153) NEXT
	DO (50151) NEXT
	DO (50122) NEXT
	PLEASE STOP
(50151)	DO (50152) NEXT
	DO .1 <- .50121
	DO RETRIEVE .2 + .3 + .50120 + .50121
	DO RESUME #2
	PLEASE STOP
(50152)	DO RESUME '∀"'"#65535¢#65535"~"',50121SUB#1'¢',50121SUB#2'"'~#1"¢#1'~#3
	PLEASE STOP
(50153)	DO (50152) NEXT
	DO WRITE IN ,50121
	DO RESUME #1
	PLEASE STOP

	PLEASE READ OUT ONE BYTE FROM .1 USING C-INTERCAL BINARY I/O
	PLEASE RETAIN .50211 BETWEEN INVOCATIONS
(50211)	DO STASH .1 + .2 + .3 + ,50211
	DO ,50211 <- #1
	DO .2 <- '"!1~#1'¢!1~#16'"¢"!1~#4'¢!1~#64'"'¢'"!1~#2'¢!1~#32'"¢"!1~#8'¢!1~#128'"'
	DO .1 <- .50211
	DO .50211 <- .2
	DO (1010) NEXT
	DO ,50211SUB#1 <- .3~#255
	DO READ OUT ,50211
	DO RETRIEVE .1 + .2 + .3 + ,50211
	DO RESUME #1
	PLEASE STOP

	PLEASE WRITE IN ONE BYTE TO .1 USING C-INTERCAL BINARY I/O
	PLEASE RETAIN .50221 BETWEEN INVOCATIONS
(50221) DO STASH .2 + .3 + ,50221
	DO ,50221 <- #1
	DO WRITE IN ,50221
	DO (50222) NEXT
	DO (50224) NEXT
	PLEASE STOP
(50222)	DO (50223) NEXT
	DO .1 <- #0
	DO RETRIEVE .2 + .3 + ,50221
	DO RESUME #2
	PLEASE STOP
(50223)	DO RESUME '∀"'#511~"'∀#256¢,50221SUB#1'~'#0¢#511'"'~#1"¢#1'~#3
	PLEASE STOP
(50224)	DO .1 <- ,50221SUB#1
	DO .2 <- .50221
	DO (1000) NEXT
	DO .1 <- .3~#255
	DO .50221 <- .1
	DO RETRIEVE .2 + .3 + ,50221
	DO RESUME #2
	PLEASE STOP

	PLEASE DO LINE INPUT AND TOKENIZATION
	PLEASE USE (51000) FOR INITIALIZATION ENTRY POINT
	PLEASE USE (51001) FOR MAIN ENTRY POINT
	PLEASE NOTE ON RESUMPTION FROM (51001):
	PLEASE NOTE   .1 IS THE VOCABULARY TABLE INDEX OF THE FIRST WORD OR #0
	PLEASE NOTE   .2 IS THE VOCABULARY TABLE INDEX OF THE SECOND WORD OR #0
	PLEASE NOTE
	PLEASE NOTE ,51001 HOLDS THE INPUT
	PLEASE NOTE ,51001SUB#1.C CONTAINS UP TO #15 CHARACTERS OF THE
	PLEASE NOTE   FIRST WORD WITH #0 INDICATING THE END OF WORD
	PLEASE NOTE ,51001SUB#2.C CONTAINS UP TO #15 CHARACTERS OF THE
	PLEASE NOTE   SECOND WORD WITH #0 INDICATING THE END OF WORD
	PLEASE NOTE ,51001SUB#3#1 THE INDEX OF THE FIRST WORD'S ENTRY
	PLEASE NOTE   IN THE VOCABULARY TABLE ,60004 OR #0 IF NONE
	PLEASE NOTE ,51001SUB#3#2 THE INDEX OF THE SECOND WORD'S ENTRY
	PLEASE NOTE   IN THE VOCABULARY TABLE ,60004 OR #0 IF NONE
	PLEASE NOTE ,51001SUB#3#11 IS EOF FLAG
	PLEASE NOTE CHARACTERS BEYOND THE 15TH CHARACTER OF THE FIRST WORD,
	PLEASE NOTE   CHARACTERS BEYOND THE 15TH CHARACTER OF THE SECOND WORD,
	PLEASE NOTE   AND ALL CHARACTERS AFTER THE SECOND WORD ARE DISCARDED
	PLEASE NOTE   UNTIL THE END OF LINE
	PLEASE NOTE ONLY THE FIRST FIVE CHARACTERS OF THE FIRST AND SECOND
	PLEASE NOTE   WORDS ARE USED TO LOOK UP THE VOCABULARY TABLE INDEX
(51000)	DO ,51001 <- #3 BY #16
	DO RESUME #1
	PLEASE STOP
(51001)	DO ,51001SUB#1#1 <- #0  DO ,51001SUB#2#1 <- #0  DO ,51001SUB#3#1 <- #0
	DO ,51001SUB#1#2 <- #0  DO ,51001SUB#2#2 <- #0  DO ,51001SUB#3#2 <- #0
	DO ,51001SUB#1#3 <- #0  DO ,51001SUB#2#3 <- #0  DO ,51001SUB#3#3 <- #0
	DO ,51001SUB#1#4 <- #0  DO ,51001SUB#2#4 <- #0  DO ,51001SUB#3#4 <- #0
	DO ,51001SUB#1#5 <- #0  DO ,51001SUB#2#5 <- #0  DO ,51001SUB#3#5 <- #0
	DO ,51001SUB#1#6 <- #0  DO ,51001SUB#2#6 <- #0  DO ,51001SUB#3#6 <- #0
	DO ,51001SUB#1#7 <- #0  DO ,51001SUB#2#7 <- #0  DO ,51001SUB#3#7 <- #0
	DO ,51001SUB#1#8 <- #0  DO ,51001SUB#2#8 <- #0  DO ,51001SUB#3#8 <- #0
	DO ,51001SUB#1#9 <- #0  DO ,51001SUB#2#9 <- #0  DO ,51001SUB#3#9 <- #0
	DO ,51001SUB#1#10 <- #0 DO ,51001SUB#2#10 <- #0 DO ,51001SUB#3#10 <- #0
	DO ,51001SUB#1#11 <- #0 DO ,51001SUB#2#11 <- #0 DO ,51001SUB#3#11 <- #0
	DO ,51001SUB#1#12 <- #0 DO ,51001SUB#2#12 <- #0 DO ,51001SUB#3#12 <- #0
	DO ,51001SUB#1#13 <- #0 DO ,51001SUB#2#13 <- #0 DO ,51001SUB#3#13 <- #0
	DO ,51001SUB#1#14 <- #0 DO ,51001SUB#2#14 <- #0 DO ,51001SUB#3#14 <- #0
	DO ,51001SUB#1#15 <- #0 DO ,51001SUB#2#15 <- #0 DO ,51001SUB#3#15 <- #0
	DO ,51001SUB#1#16 <- #0 DO ,51001SUB#2#16 <- #0 DO ,51001SUB#3#16 <- #0
	DO ,51001SUB#3#4 <- #1
	DO ,51001SUB#3#5 <- #7
	DO ,51001SUB#3#6 <- #8
	DO (50021) NEXT
	DO (51002) NEXT
	PLEASE SKIP LEADING SPACES, FIRST CHECKING FOR END OF LINE
	PLEASE NOTE   ,51001SUB#3#3 IS CURRENT CHARACTER INDEX
	PLEASE NOTE   ,51001SUB#3#4 IS CURRENT WORD INDEX (#1 OR #2)
	PLEASE NOTE   ,51001SUB#3#5 IS CURRENT WORD INDEX FOR FIRST
	PLEASE NOTE      THREE CHARACTERS (#7 OR #9)
	PLEASE NOTE   ,51001SUB#3#6 IS CURRENT WORD INDEX FOR LAST
	PLEASE NOTE      TWO CHARACTERS (#8 OR #10)
	PLEASE NOTE   ,51001SUB#3#7 IS FIRST THREE CHARACTERS OF FIRST WORD
	PLEASE NOTE     AT FIVE BITS PER CHARACTER
	PLEASE NOTE   ,51001SUB#3#8 IS LAST TWO CHARACTERS OF FIRST WORD
	PLEASE NOTE   ,51001SUB#3#9 IS FIRST THREE CHARACTERS OF SECOND WORD
	PLEASE NOTE     AT FIVE BITS PER CHARACTER
	PLEASE NOTE   ,51001SUB#3#10 IS LAST TWO CHARACTERS OF SECOND WORD
(51002)	DO FORGET #1
	DO NOTE CHECK FOR END OF LINE
	DO (51003) NEXT
	DO NOTE NOT AT END OF LINE, CHECK FOR SPACE
	DO (51005) NEXT
	DO NOTE NOT SPACE, START WRITING IN FIRST OR SECOND WORD
	DO ,51001SUB#3#3 <- #1
	DO (51011) NEXT
	PLEASE STOP
(51003)	DO ,51001SUB#3#11 <- "∀'#255~.1'¢#1"~#1
	DO (51004) NEXT
	DO FORGET #1
	DO NOTE AT END OF LINE
	DO (51081) NEXT
	PLEASE STOP
(51004)	DO RESUME "∀'"&'"#255~'"∀#10¢.1"~"#0¢#255"'"~#1'¢'#255~.1'"~#1'¢#1"~#3
	PLEASE STOP
(51005)	DO (51006) NEXT
	DO NOTE AT SPACE
	DO FORGET #1
	DO (50021) NEXT
	DO (51002) NEXT
	PLEASE STOP
(51006)	DO RESUME '∀"'#255~"'∀#32¢.1'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
	PLEASE WRITE IN CURRENT CHARACTER INTO CURRENT WORD
(51011)	DO FORGET #1
	DO ,51001SUB',51001SUB#3#4'',51001SUB#3#3' <- .1
	DO NOTE WRITE INTO FIVE BITS
	DO (51031) NEXT
	DO .1 <- ,51001SUB#3#3
	DO (1020) NEXT
	DO (51012) NEXT
	DO NOTE NOT AT 16TH CHARACTER, ADVANCE TO NEXT CHARACTER
	DO ,51001SUB#3#3 <- .1
	DO (50021) NEXT
	DO NOTE CHECK FOR END OF LINE
	DO ,51001SUB#3#11 <- "∀'#255~.1'¢#1"~#1
	DO (51003) NEXT
	DO NOTE NOT AT END OF LINE, CHECK FOR SPACE
	DO (51014) NEXT
	DO NOTE NOT SPACE, CONTINUE WRITING IN CURRENT WORD
	DO (51011) NEXT
	PLEASE STOP
(51012)	DO (51013) NEXT
	DO FORGET #1
	DO NOTE AT 16TH CHARACTER, DISCARD UNTIL SPACE OR END OF LINE
	DO (51018) NEXT
	PLEASE STOP
(51013)	DO RESUME '∀"'#255~"'∀#16¢.1'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51014)	DO (51006) NEXT
	DO FORGET #1
	DO NOTE AT END OF WORD, CHECK IF FIRST WORD OR SECOND WORD
	DO (51015) NEXT
	DO NOTE FINISHED SECOND WORD, DISCARD UNTIL END OF LINE
	DO (51017) NEXT
	PLEASE STOP
(51015)	DO (51016) NEXT
	DO FORGET #1
	DO NOTE FINISHED FIRST WORD, SKIP SPACES AND START SECOND WORD
	DO ,51001SUB#3#4 <- #2
	DO ,51001SUB#3#5 <- #9
	DO ,51001SUB#3#6 <- #10
	DO (51002) NEXT
	PLEASE STOP
(51016)	DO RESUME ,51001SUB#3#4
	PLEASE STOP
(51017)	DO FORGET #1
	DO NOTE DISCARD UNTIL END OF LINE
	DO (50021) NEXT
	DO (51003) NEXT
	DO (51017) NEXT
	PLEASE STOP
(51018)	DO FORGET #1
	DO NOTE DISCARD UNTIL SPACE OR END OF LINE
	DO (50021) NEXT
	DO (51003) NEXT
	DO (51005) NEXT
	DO (51018) NEXT
	PLEASE STOP

	PLEASE CONVERT .1 TO FIVE BIT REPRESENTATION WHERE 2 IS MAPPED TO #29
	       AND " IS MAPPED TO #30
(51021)	DO (51022) NEXT
	DO NOTE NOT 2
	DO (51024) NEXT
	DO NOTE NOT "
	DO .1 <- .1~#31
	DO RESUME #1
	PLEASE STOP
(51022)	DO (51023) NEXT
	DO .1 <- #29
	DO RESUME #2
	PLEASE STOP
(51023)	DO RESUME '∀"'#255~"'∀#50¢.1'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51024)	DO (51025) NEXT
	DO .1 <- #30
	DO RESUME #2
	PLEASE STOP
(51025)	DO RESUME '∀"'#255~"'∀#34¢.1'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP

	PLEASE ADD FIVE BIT REPRESENTATION TO ONE OF
	       ,51001SUB#3#7 ,51001SUB#3#8 ,51001SUB#3#9 ,51001SUB#3#10
	       AFTER MULTIPLYING BY ONE OF 1, 32, OR 1024
(51031)	DO STASH .1 + .2 + .3
	DO (51021) NEXT
	DO (51032) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 1
	DO (51034) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 2
	DO (51036) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 3
	DO (51038) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 4
	DO (51040) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 5
	DO (51042) NEXT
	PLEASE STOP
(51032)	DO (51033) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 1
	DO FORGET #1
	DO ,51001SUB#3",51001SUB#3#5" <- .1
	DO (51042) NEXT
	PLEASE STOP
(51033)	DO RESUME '∀"'#255~"'∀#1¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51034)	DO (51035) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 2
	DO FORGET #1
	DO .2 <- #32
	DO (1030) NEXT
	DO .1 <- .3
	DO .2 <- ,51001SUB#3",51001SUB#3#5"
	DO (1000) NEXT
	DO ,51001SUB#3",51001SUB#3#5" <- .3
	DO (51042) NEXT
	PLEASE STOP
(51035)	DO RESUME '∀"'#255~"'∀#2¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51036)	DO (51037) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 3
	DO FORGET #1
	DO .2 <- #1024
	DO (1030) NEXT
	DO .1 <- .3
	DO .2 <- ,51001SUB#3",51001SUB#3#5"
	DO (1000) NEXT
	DO ,51001SUB#3",51001SUB#3#5" <- .3
	DO (51042) NEXT
	PLEASE STOP
(51037)	DO RESUME '∀"'#255~"'∀#3¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51038)	DO (51039) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 4
	DO FORGET #1
	DO ,51001SUB#3",51001SUB#3#6" <- .1
	DO (51042) NEXT
	PLEASE STOP
(51039)	DO RESUME '∀"'#255~"'∀#4¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51040)	DO (51041) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 5
	DO FORGET #1
	DO .2 <- #32
	DO (1030) NEXT
	DO .1 <- .3
	DO .2 <- ,51001SUB#3",51001SUB#3#6"
	DO (1000) NEXT
	DO ,51001SUB#3",51001SUB#3#6" <- .3
	DO (51042) NEXT
	PLEASE STOP
(51041)	DO RESUME '∀"'#255~"'∀#5¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51042)	DO RETRIEVE .1 + .2 + .3
	DO RESUME #2
	PLEASE STOP

	PLEASE LOOK UP WORDS IN VOCABULARY TABLE, THEN RETURN
(51081)	DO FORGET #1
	DO STASH .3
	DO .3 <- #1
	DO ,51001SUB#3#1 <- #0
	DO ,51001SUB#3#2 <- #0
	DO (51082) NEXT
	PLEASE STOP
(51082)	DO FORGET #1
	DO .1 <- ,51001SUB#3#7
	DO .2 <- ,60004SUB.3#1
	DO (51085) NEXT
	DO (51083) NEXT
	PLEASE STOP
(51083)	DO FORGET #1
	DO .1 <- .3
	DO (1020) NEXT
	DO .2 <- #297
	DO (51084) NEXT
	DO .3 <- .1
	DO (51082) NEXT
(51084)	DO (40011) NEXT
	DO FORGET #1
	DO .3 <- #1
	DO (51087) NEXT
	PLEASE STOP
(51085)	DO (40011) NEXT
	DO FORGET #1
	DO .1 <- ,51001SUB#3#8
	DO .2 <- ,60004SUB.3#2
	DO (51086) NEXT
	DO (51083) NEXT
	PLEASE STOP
(51086)	DO (40011) NEXT
	DO FORGET #1
	DO ,51001SUB#3#1 <- .3
	DO .3 <- #1
	DO (51087) NEXT
	PLEASE STOP
(51087)	DO FORGET #1
	DO .1 <- ,51001SUB#3#9
	DO .2 <- ,60004SUB.3#1
	DO (51090) NEXT
	DO (51088) NEXT
	PLEASE STOP
(51088)	DO FORGET #1
	DO .1 <- .3
	DO (1020) NEXT
	DO .2 <- #297
	DO (51089) NEXT
	DO .3 <- .1
	DO (51087) NEXT
(51089)	DO (40011) NEXT
	DO FORGET #1
	DO (51092) NEXT
	PLEASE STOP
(51090)	DO (40011) NEXT
	DO FORGET #1
	DO .1 <- ,51001SUB#3#10
	DO .2 <- ,60004SUB.3#2
	DO (51091) NEXT
	DO (51088) NEXT
	PLEASE STOP
(51091)	DO (40011) NEXT
	DO FORGET #1
	DO ,51001SUB#3#2 <- .3
	DO (51092) NEXT
	PLEASE STOP
(51092)	DO FORGET #1
	DO RETRIEVE .3
	DO .1 <- ,51001SUB#3#1
	DO .2 <- ,51001SUB#3#2
	DO RESUME #1
	PLEASE STOP

	PLEASE READ OUT WORD .1 OF INPUT BUFFER
(51501)	DO,60006SUB#13#2<-,51001SUB.1#1   DO,60006SUB#13#3<-,51001SUB.1#2
	DO,60006SUB#13#4<-,51001SUB.1#3   DO,60006SUB#13#5<-,51001SUB.1#4
	DO,60006SUB#13#6<-,51001SUB.1#5   DO,60006SUB#13#7<-,51001SUB.1#6
	DO,60006SUB#13#8<-,51001SUB.1#7   DO,60006SUB#13#9<-,51001SUB.1#8
	DO,60006SUB#13#10<-,51001SUB.1#9  DO,60006SUB#13#11<-,51001SUB.1#10
	DO,60006SUB#13#12<-,51001SUB.1#11 DO,60006SUB#13#13<-,51001SUB.1#12
	DO,60006SUB#13#14<-,51001SUB.1#13 DO,60006SUB#13#15<-,51001SUB.1#14
	DO,60006SUB#13#16<-,51001SUB.1#15 DO,60006SUB#13#17<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO RESUME #1
	PLEASE STOP

	PLEASE READ OUT LONG FORM DESCRIPTION
	       .1 IS OBJECT INDEX
(55101)	DO STASH .1 + .2 + .3
	DO .2 <- .1
	DO .3 <- #1
	DO (55102) NEXT
	PLEASE STOP
(55102)	DO FORGET #1
	DO .1 <- ,60001SUB.2.3
	DO (55103) NEXT
	DO (50011) NEXT
	DO .1 <- .3
	DO (1020) NEXT
	DO .3 <- .1
	DO (55102) NEXT
	PLEASE STOP
(55103)	DO (55104) NEXT
	DO RETRIEVE .1 + .2 + .3
	DO RESUME #2
	PLEASE STOP
(55104)	DO RESUME "∀'#255~.1'¢#1"~#3
	PLEASE STOP

	PLEASE READ OUT LOCATION SHORT FORM DESCRIPTION
	       .1 IS OBJECT INDEX
(55201)	DO (55202) NEXT
	DO NOTE HAVE SHORT FORM DESCRIPTION
	DO STASH .1 + .2 + .3
	DO .2 <- .1
	DO .3 <- #1
	DO (55204) NEXT
	PLEASE STOP
(55202)	DO (55203) NEXT
	DO NOTE NO SHORT FORM DESCRIPTION, READ OUT LONG FORM
	DO (55101) NEXT
	DO RESUME #2
	PLEASE STOP
(55203)	DO RESUME "∀'#255~,60002SUB.1#1'¢#1"~#3
	PLEASE STOP
(55204)	DO FORGET #1
	DO .1 <- ,60002SUB.2.3
	DO (55103) NEXT
	DO (50011) NEXT
	DO .1 <- .3
	DO (1020) NEXT
	DO .3 <- .1
	DO (55204) NEXT
	PLEASE STOP

	PLEASE READ OUT OBJECT DESCRIPTION
	       .1 IS OBJECT INDEX
	       .2 IS MESSAGE INDEX
(55501)	DO STASH .1 + .2 + .3 + .4
	DO NOT [PSPEAK OBJ] .1
	DO NOT [PSPEAK MESSAGE] .2
	DO .3 <- .1
	DO .4 <- #1
	DO (55502) NEXT
	PLEASE STOP
(55502)	DO FORGET #1
	DO .1 <- ,60005SUB.3.2.4
	DO (55503) NEXT
	DO (50011) NEXT DO NOTE PUTCHAR
	DO .1 <- .4
	DO (1020) NEXT
	DO .4 <- .1
	DO (55502) NEXT
	PLEASE STOP
(55503)	DO (55104) NEXT
	DO RETRIEVE .1 + .2 + .3 + .4
	DO RESUME #2
	PLEASE STOP

	PLEASE READ OUT ARBITRARY MESSAGE
	       .1 IS MESSAGE INDEX
(55601)	DO STASH .1 + .2 + .3
	DO .2 <- .1
	DO .3 <- #1
	DO (55602) NEXT
	PLEASE STOP
(55602)	DO FORGET #1
	DO .1 <- ,60006SUB.2.3
	DO (55103) NEXT
	DO (50011) NEXT
	DO .1 <- .3
	DO (1020) NEXT
	DO .3 <- .1
	DO (55602) NEXT
	PLEASE STOP

	PLEASE READ OUT CLASSIFICATION MESSAGE
	       .1 HAS THE SCORE ON ENTRY
	       .1 HAS CLASSIFICATION INDEX ON EXIT
(56001)	DO STASH .2 + .3 + .4 + .5
	DO .4 <- .1
	DO .5 <- #1
	DO (56002) NEXT
	PLEASE STOP
(56002)	DO FORGET #1
	DO .1 <- ,60010SUB.5#1
	DO .2 <- .4
	DO (1010) NEXT
	DO (56003) NEXT
	DO .1 <- .5
	DO (1020) NEXT
	DO .5 <- .1
	DO (56002) NEXT
	PLEASE STOP
(56003)	DO (56004) NEXT
	DO FORGET #1
	DO .2 <- #2
	DO (56005) NEXT
	PLEASE STOP
(56004)	DO RESUME "∀'.3~#32768'¢#1"~#3
	PLEASE STOP
(56005)	DO FORGET #1
	DO .1 <- ,60010SUB.5.2
	DO (56006) NEXT
	DO (50011) NEXT
	DO .1 <- .2
	DO (1020) NEXT
	DO .2 <- .1
	DO (56005) NEXT
	PLEASE STOP
(56006)	DO (56007) NEXT
	DO .1 <- .5
	DO RETRIEVE .2 + .3 + .4 + .5
	DO RESUME #2
	PLEASE STOP
(56007)	DO RESUME "∀'"#255~.1"~#1'¢#1"~#3
	PLEASE STOP

	PLEASE READ OUT MAGIC MESSAGE
	       .1 IS MAGIC MESSAGE INDEX INDEX
(56201)	DO STASH .1 + .2 + .3
	DO .2 <- .1
	DO .3 <- #1
	DO (56202) NEXT
	PLEASE STOP
(56202)	DO FORGET #1
	DO .1 <- ,60012SUB.2.3
	DO (55103) NEXT
	DO (50011) NEXT
	DO .1 <- .3
	DO (1020) NEXT
	DO .3 <- .1
	DO (56202) NEXT
	PLEASE STOP

	PLEASE READ OUT CUSTOM MESSAGE IN STARTING FROM ,60006SUB#13#2
(57001)	DO STASH .1 + .2 + .3
	DO .2 <- #13
	DO .3 <- #2
	DO (55602) NEXT
	PLEASE STOP

	PLEASE WRITE IN YES OR NO
	       .1 IS #1 IF YES
	       .1 IS #2 IF NO
	       .1 IS UNCHANGED IF EOF
(57101)	DO STASH .1 + .2 + .3
	DO (57102) NEXT
	PLEASE STOP
(57102)	DO FORGET #1
	DO,60006SUB#13#2<-#62  DO,60006SUB#13#3<-#0
	DO (57001) NEXT DO NOTE CUSTOM MESSAGE
	DO (51001) NEXT DO NOTE CALL GETIN
	DO (57107) NEXT DO NOTE CHECK FOR EOF
	DO .1 <- ,51001SUB#3#8
	DO .2 <- #0
	DO (57103) NEXT
	DO (57106) NEXT
	PLEASE STOP
(57103)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- ,51001SUB#3#7
	DO .2 <- #19641
	DO .3 <- #1
	DO (57105) NEXT
	DO .2 <- #25
	DO (57105) NEXT
	DO .3 <- #2
	DO .2 <- #494
	DO (57105) NEXT
	DO .2 <- #14
	DO (57105) NEXT
	DO (57106) NEXT
	PLEASE STOP
(57105)	DO (40011) NEXT DO NOTE .1=.2
	DO RETRIEVE .1
	DO .1 <- .3
	DO RETRIEVE .2 + .3
	DO RESUME #2
	PLEASE STOP
(57106)	DO FORGET #1
	DO,60006SUB#13#2<-#80  DO,60006SUB#13#3<-#76  DO,60006SUB#13#4<-#69
	DO,60006SUB#13#5<-#65  DO,60006SUB#13#6<-#83  DO,60006SUB#13#7<-#69
	DO,60006SUB#13#8<-#32  DO,60006SUB#13#9<-#65  DO,60006SUB#13#10<-#78
	DO,60006SUB#13#11<-#83 DO,60006SUB#13#12<-#87 DO,60006SUB#13#13<-#69
	DO,60006SUB#13#14<-#82 DO,60006SUB#13#15<-#32 DO,60006SUB#13#16<-#84
	DO,60006SUB#13#17<-#72 DO,60006SUB#13#18<-#69 DO,60006SUB#13#19<-#32
	DO,60006SUB#13#20<-#81 DO,60006SUB#13#21<-#85 DO,60006SUB#13#22<-#69
	DO,60006SUB#13#23<-#83 DO,60006SUB#13#24<-#84 DO,60006SUB#13#25<-#73
	DO,60006SUB#13#26<-#79 DO,60006SUB#13#27<-#78 DO,60006SUB#13#28<-#46
	DO,60006SUB#13#29<-#10 DO,60006SUB#13#30<-#0
	DO (57001) NEXT
	DO (57102) NEXT
	PLEASE STOP
(57107)	DO (57108) NEXT
	DO NOTE AT EOF
	DO RETRIEVE .1 + .2 + .3
	DO RESUME #2
	PLEASE STOP
(57108)	DO RESUME '"'∀",51001SUB#3#11"¢#1'~#1"¢",51001SUB#3#11"'~#3
	PLEASE STOP

	PLEASE READ OUT FORMATTED INTEGER
	       .1 IS INTEGER
	       .2 IS 10^(N-1) WHERE N IS THE FORMAT WIDTH
(57201)	DO STASH .1 + .2 + .3 + .4 + .5 + .6
	DO .4 <- .1
	DO .5 <- .2
	DO .6 <- #1
	DO (57202) NEXT
	PLEASE STOP
(57202)	DO FORGET #1
	DO .1 <- #0
	DO .2 <- .5
	DO (57203) NEXT
	DO .1 <- .4
	DO (1040) NEXT
	DO .1 <- .3
	DO .2 <- #10
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2
	DO .1 <- .3
	DO .2 <- #0
	DO (57204) NEXT
	DO NOTE NOT ZERO
	DO .6 <- #0
	DO .2 <- #48
	DO (1000) NEXT
	DO .1 <- .3
	DO (50011) NEXT DO NOTE PUTCHAR
	DO (57207) NEXT
	PLEASE STOP
(57203)	DO (40011) NEXT DO NOTE .1=.2
	DO RETRIEVE .1 + .2 + .3 + .4 + .5 + .6
	DO RESUME #2
(57204)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO (57205) NEXT
	DO .1 <- #32
	DO (50011) NEXT DO NOTE PUTCHAR
	DO (57207) NEXT
	PLEASE STOP
(57205)	DO (57206) NEXT
	DO FORGET #1
	DO .1 <- #48
	DO (50011) NEXT DO NOTE PUTCHAR
	DO (57207) NEXT
	PLEASE STOP
(57206)	DO RESUME '∀"&'"#65535~'"∀.5¢#1"~"#0¢#65535"'"~#1'¢.6"¢#1'~#3
	PLEASE STOP
(57207)	DO FORGET #1
	DO .1 <- .5
	DO .2 <- #10
	DO (1040) NEXT
	DO .5 <- .3
	DO (57202) NEXT
	PLEASE STOP

	PLEASE READ OUT FIVE BIT ENCODING OF WORD IN .1 AND .2
(57301)	DO STASH .1 + .2 + .3 + .4 + .5
	DO .4 <- .1
	DO .5 <- .2
	DO .2 <- #32
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2
	DO .1 <- .3
	DO (57302) NEXT
	DO .1 <- .4
	DO .2 <- #32
	DO (1040) NEXT
	DO .1 <- .3
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2
	DO .1 <- .3
	DO (57302) NEXT
	DO .1 <- .4
	DO .2 <- #1024
	DO (1040) NEXT
	DO .1 <- .3
	DO .2 <- #32
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2
	DO .1 <- .3
	DO (57302) NEXT
	DO .1 <- .5
	DO .2 <- #32
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2
	DO .1 <- .3
	DO (57302) NEXT
	DO .1 <- .5
	DO .2 <- #32
	DO (1040) NEXT
	DO .1 <- .3
	DO (40001) NEXT DO NOTE .3 <- .1 MOD .2
	DO .1 <- .3
	DO (57302) NEXT
	DO RETRIEVE .1 + .2 + .3 + .4 + .5
	DO RESUME #1
	PLEASE STOP
(57302)	DO .2 <- #0
	DO (57303) NEXT
	DO .2 <- #29
	DO (57304) NEXT
	DO .2 <- #30
	DO (57305) NEXT
	DO .2 <- #31
	DO (57306) NEXT
	DO .2 <- #64
	DO (1000) NEXT
	DO .1 <- .3
	DO (50011) NEXT DO NOTE PUTCHAR
	DO RESUME #1
	PLEASE STOP
(57303)	DO (40011) NEXT DO NOTE .1=.2
	DO RESUME #2
	PLEASE STOP
(57304)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- #50
	DO (50011) NEXT DO NOTE PUTCHAR
	DO RESUME #2
	PLEASE STOP
(57305)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- #34
	DO (50011) NEXT DO NOTE PUTCHAR
	DO RESUME #2
	PLEASE STOP
(57306)	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- #63
	DO (50011) NEXT DO NOTE PUTCHAR
	DO RESUME #2
	PLEASE STOP



	PLEASE INITIALIZE DATABASE
	PLEASE NOTE ,60001 CONTAINS LONG FORM DESCRIPTIONS
	PLEASE NOTE  FIRST SUBSCRIPT IS LOCATION INDEX
	PLEASE NOTE  SECOND SUBSCRIPT IS CHARACTER INDEX
	PLEASE NOTE  ZERO TERMINATES DESCRIPTION
	PLEASE NOTE
	PLEASE NOTE ,60002 CONTAINS SHORT FORM DESCRIPTIONS
	PLEASE NOTE  FIRST SUBSCRIPT IS LOCATION INDEX
	PLEASE NOTE  SECOND SUBSCRIPT IS CHARACTER INDEX
	PLEASE NOTE    ZERO TERMINATES DESCRIPTION
	PLEASE NOTE    EMPTY MEANS USE LONG FORM DESCRIPTION
	PLEASE NOTE
	PLEASE NOTE ,60013 CONTAINS TRAVEL TABLE START INDEX INTO ,60003
	PLEASE NOTE   SUBSCRIPT IS LOCATION INDEX
	PLEASE NOTE ,60003 CONTAINS TRAVEL TABLE
	PLEASE NOTE   SUB #1 IS LOCATION INDEX
	PLEASE NOTE   SUB #2
	PLEASE NOTE     UPPER 8 BITS IS TAG
	PLEASE NOTE     LOWER 8 BITS IS VALUE
	PLEASE NOTE     WHEN TAG IS #1 VALUE IS DESTINATION LOCATION
	PLEASE NOTE     WHEN TAG IS #2 VALUE IS USED BY SPECIAL CODE SECTION
	PLEASE NOTE     WHEN TAG IS #3 VALUE IS INDEX INTO ARBITRARY MESSAGES
	PLEASE NOTE       TABLE ,60006
	PLEASE NOTE   SUB #3
	PLEASE NOTE     UPPER 8 BITS IS TAG
	PLEASE NOTE     LOWER 8 BITS IS VALUE
	PLEASE NOTE     WHEN TAG IS #1, MOTION IS UNCONDITIONAL
	PLEASE NOTE     WHEN TAG IS #2, MOTION IS UNCONDITIONAL
	PLEASE NOTE       BUT FORBIDDEN TO DWARVES
	PLEASE NOTE     WHEN TAG IS #3 VALUE IS THE % PROBABILITY OF MOTION
	PLEASE NOTE     WHEN TAG IS #4 VALUE IS INDEX OF OBJECT THAT MUST BE
	PLEASE NOTE       CARRIED FOR MOTION
	PLEASE NOTE     WHEN TAG IS #5 VALUE IS INDEX OF OBJECT THAT MUST BE
	PLEASE NOTE       CARRIED OR IN THE SAME ROOM FOR MOTION
	PLEASE NOTE     WHEN TAG IS #6 VALUE IS INDEX OF OBJECT WHOSE PROPERTY
	PLEASE NOTE       THAT MUST *NOT* BE #0 FOR MOTION
	PLEASE NOTE     WHEN TAG IS #7 VALUE IS INDEX OF OBJECT WHOSE PROPERTY
	PLEASE NOTE       THAT MUST *NOT* BE #1 FOR MOTION
	PLEASE NOTE     WHEN TAG IS #8 VALUE IS INDEX OF OBJECT WHOSE PROPERTY
	PLEASE NOTE       THAT MUST *NOT* BE #2 FOR MOTION
	PLEASE NOTE     WHEN TAG IS #9 VALUE IS INDEX OF OBJECT WHOSE PROPERTY
	PLEASE NOTE       THAT MUST *NOT* BE #3 FOR MOTION
	PLEASE NOTE     WHEN TAG IS #10 VALUE IS INDEX OF OBJECT WHOSE PROPERTY
	PLEASE NOTE       THAT MUST *NOT* BE #4 FOR MOTION
	PLEASE NOTE   SUB #4 OR MORE IS LIST OF MOTION VERBS
	PLEASE NOTE     #0 IS END OF LIST
	PLEASE NOTE     #1 IS FORCED MOTION TAKEN ON ENTERING LOCATION (STILL
	PLEASE NOTE       SUBJECT TO CONDITIONS OF ELEMENT #2)
	PLEASE NOTE
	PLEASE NOTE ,60004 CONTAINS VOCABULARY TABLE
	PLEASE NOTE   FIRST SUBSCRIPT IS THE TABLE INDEX
	PLEASE NOTE   THE ONLY NON-ALPHABETICAL CHARACTERS ARE 2, ", AND ?
	PLEASE NOTE     2 IS MAPPED TO #29
	PLEASE NOTE     " IS MAPPED TO #30
	PLEASE NOTE     ? IS MAPPED TO #31
	PLEASE NOTE   USE THE LOWER 5 BITS OF EACH CHARACTER
	PLEASE NOTE   SUB.I#1 IS FIRST THREE CHARACTERS
	PLEASE NOTE     FIRST CHARACTER IS BITS 1-5
	PLEASE NOTE     SECOND CHARACTER IS BITS 6-10
	PLEASE NOTE     THIRD CHARACTER IS BITS 11-15
	PLEASE NOTE   SUB.I#2 IS FIRST FOURTH AND FIFTH CHARACTERS
	PLEASE NOTE     FOURTH CHARACTER IS BITS 1-5
	PLEASE NOTE     FIFTH CHARACTER IS BITS 6-10
	PLEASE NOTE   SUB.I#3
	PLEASE NOTE     UPPER 8 BITS IS TAG
	PLEASE NOTE     LOWER 8 BITS IS VALUE
	PLEASE NOTE     WHEN TAG IS #1 VALUE IS MOTION VERB INDEX
	PLEASE NOTE     WHEN TAG IS #2 VALUE IS OBJECT INDEX
	PLEASE NOTE     WHEN TAG IS #3 VALUE IS ACTION VERB INDEX
	PLEASE NOTE     WHEN TAG IS #4 VALUE IS INDEX INTO ARBITRARY MESSAGES
	PLEASE NOTE       TABLE ,60006
	PLEASE NOTE   DO NOT KNOW YET WHAT DUPLICATE FEE FIE FOE FOO FUM MEANS
	PLEASE NOTE   DO NOT KNOW YET WHAT TO DO WITH DUPLICATE ROD PLANT TROLL
	PLEASE NOTE     BUT THEY MUST REFER THE ALTERNATE ROD, PLANT, AND TROLL
	PLEASE NOTE     OBJECTS -- THERE IS SPECIAL CASE CODE FOR ROD2, PLANT2,
	PLEASE NOTE	AND TROLL2
	PLEASE NOTE   INITIALLY CONSIDERED USING
	PLEASE NOTE     #31 BY #31 BY #31 BY #31 BY #31 TRIE FOR FAST LOOKUPS
	PLEASE NOTE     WITH #0 MAPPED TO #27 BUT THAT WASTES TOO MUCH MEMORY
	PLEASE NOTE
	PLEASE NOTE ,60005 CONTAINS OBJECT DESCRIPTION TABLE
	PLEASE NOTE   FIRST SUBSCRIPT IS OBJECT INDEX
	PLEASE NOTE   SECOND SUBSCRIPT IS OBJECT MESSAGE INDEX
	PLEASE NOTE     #1 IS INVENTORY MESSAGE
	PLEASE NOTE     #2 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #0
	PLEASE NOTE     #3 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #1
	PLEASE NOTE     #4 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #2
	PLEASE NOTE     #5 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #3
	PLEASE NOTE     #6 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #4
	PLEASE NOTE     #7 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #5
	PLEASE NOTE   THIRD SUBSCRIPT IS CHARACTER INDEX
	PLEASE NOTE     ZERO TERMINATES MESSAGE
	PLEASE NOTE
	PLEASE NOTE ,60006 CONTAINS ARBITRARY MESSAGES TABLE
	PLEASE NOTE   FIRST SUBSCRIPT IS ARBITRARY MESSAGE INDEX
	PLEASE NOTE   SECOND SUBSCRIPT IS CHARACTER INDEX
	PLEASE NOTE   ZERO TERMINATES MESSAGE
	PLEASE NOTE
	PLEASE NOTE ,60007 CONTAINS OBJECT LOCATION INDEX
	PLEASE NOTE   FIRST SUBSCRIPT IS OBJECT INDEX
	PLEASE NOTE   SECOND SUBSCRIPT IS MULTIPLE LOCATIONS INDEX
	PLEASE NOTE     #1 IS LOCATION INDEX, #0 MEANS NO LOCATION
	PLEASE NOTE	#2 IS SECOND LOCATION INDEX, NON-ZERO MEANS
	PLEASE NOTE	   OBJECT IS IMMOVABLE
	PLEASE NOTE
	PLEASE NOTE ,60008 CONTAINS ACTION VERB DEFAULTS
	PLEASE NOTE   SUBSCRIPT IS ACTION VERB INDEX
	PLEASE NOTE   VALUE IS ARBITRARY MESSAGE INDEX INTO ,60006
	PLEASE NOTE
	PLEASE NOTE ,60009 CONTAINS LOCATION FLAGS
	PLEASE NOTE   SUBSCRIPT IS LOCATION INDEX
	PLEASE NOTE   VALUE~#1 IS LIGHT FLAG
	PLEASE NOTE   VALUE~#2 IS #1 FOR OIL #0 FOR WATER IF VALUE~#4 IS #1
	PLEASE NOTE   VALUE~#4 IS LIQUID FLAG
	PLEASE NOTE   VALUE~#8 IS PIRATE OFF LIMITS UNLESS FOLLOWING PLAYER
	PLEASE NOTE   VALUE~#16 IS HINT FOR TRYING TO GET INTO CAVE
	PLEASE NOTE   VALUE~#32 IS HINT FOR TRYING TO CATCH BIRD
	PLEASE NOTE   VALUE~#64 IS HINT FOR TRYING TO DEAL WITH SNAKE
	PLEASE NOTE   VALUE~#128 IS HINT FOR LOST IN MAZE
	PLEASE NOTE   VALUE~#256 IS HINT FOR PONDERING DARK ROOM
	PLEASE NOTE   VALUE~#512 IS HINT FOR AT WITT'S END
	PLEASE NOTE   IF VALUE IS #2, MOVEMENT IS FORCED
	PLEASE NOTE
	PLEASE NOTE ,60010 CONTAINS CLASSIFICATION MESSAGES
	PLEASE NOTE   FIRST SUBSCRIPT CLASSIFICATION INDEX
	PLEASE NOTE   WHEN SECOND SUBSCRIPT IS #1
	PLEASE NOTE     VALUE IS MAXIMUM SCORE FOR THIS CLASSIFICATION
	PLEASE NOTE   SUBSEQUENT VALUES OF THE SECOND SUBSCRIPT ARE
	PLEASE NOTE     CHARACTERS IN THE CLASSIFICATION MESSAGE
	PLEASE NOTE     WHERE ZERO TERMINATES THE MESSAGE
	PLEASE NOTE
	PLEASE NOTE ,60011 CONTAINS HINT INFORMATION
	PLEASE NOTE   FIRST SUBSCRIPT IS HINT INDEX
	PLEASE NOTE     #2 IS READ CLUE IN REPOSITORY
	PLEASE NOTE     #3 IS ASKED FOR INSTRUCTIONS
	PLEASE NOTE     #4 IS HINT FOR TRYING TO GET INTO CAVE
	PLEASE NOTE     #5 IS HINT FOR TRYING TO CATCH BIRD
	PLEASE NOTE     #6 IS HINT FOR TRYING TO DEAL WITH SNAKE
	PLEASE NOTE     #7 IS HINT FOR LOST IN MAZE
	PLEASE NOTE     #8 IS HINT FOR PONDERING DARK ROOM
	PLEASE NOTE     #9 IS HINT FOR AT WITT'S END
	PLEASE NOTE   SECOND SUBSCRIPT
	PLEASE NOTE     #1 NUMBER OF TURNS TO TRIGGER HINT
	PLEASE NOTE     #2 POINTS DEDUCTED FOR TAKING HINT
	PLEASE NOTE     #3 ARBITRARY MESSAGE INDEX OF HINT QUESTION
	PLEASE NOTE     #4 ARBITRARY MESSAGE INDEX OF HINT TEXT
	PLEASE NOTE     #5 COND(LOC) SELECTOR FOR THIS HINT
	PLEASE NOTE
	PLEASE NOTE ,60012 CONTAINS MAGIC MESSAGES
	PLEASE NOTE   FIRST SUBSCRIPT IS MAGIC MESSAGE INDEX
	PLEASE NOTE   SECOND SUBSCRIPT IS CHARACTER INDEX
	PLEASE NOTE   ZERO TERMINATES MESSAGE
(60000)	DO ,60001 <- #140 BY #1294
	DO ,60002 <- #140 BY #50
	DO ,60013 <- #140
	DO ,60003 <- #500 BY #13
	DO ,60004 <- #296 BY #3
	DO ,60005 <- #64 BY #7 BY #138
	DO ,60006 <- #201 BY #1381
	DO ,60007 <- #64 BY #2
	DO ,60008 <- #31
	DO ,60009 <- #140
	DO ,60010 <- #9 BY #68
	DO ,60011 <- #9 BY #5
	DO ,60012 <- #32 BY #320

	DO,60006SUB#13#2<-#73  DO,60006SUB#13#3<-#78  DO,60006SUB#13#4<-#73
	DO,60006SUB#13#5<-#84  DO,60006SUB#13#6<-#73  DO,60006SUB#13#7<-#65
	DO,60006SUB#13#8<-#76  DO,60006SUB#13#9<-#73  DO,60006SUB#13#10<-#83
	DO,60006SUB#13#11<-#73 DO,60006SUB#13#12<-#78 DO,60006SUB#13#13<-#71
	DO,60006SUB#13#14<-#46 DO,60006SUB#13#15<-#46 DO,60006SUB#13#16<-#46
	DO,60006SUB#13#17<-#10 DO,60006SUB#13#18<-#0
	DO (57001) NEXT

	DO (65000) NEXT

	DO .3 <- #1
	DO (60001) NEXT
	PLEASE STOP
	PLEASE FOR .3 IN #1 .. #140, LOCFLAGS <- FORCED IF TRAVEL(LOC,VERB) EQ #1
(60001)	DO FORGET #1
	DO .1 <- ,60003SUB',60013SUB.3'#4
	DO .2 <- #1
	DO (60002) NEXT
	DO .1 <- .3
	DO .2 <- #140
	DO (60003) NEXT
	DO (1020) NEXT
	DO .3 <- .1
	DO (60001) NEXT
	PLEASE STOP
(60002)	DO (40011) NEXT DO NOTE .1=.2
	DO ,60009SUB.3 <- #2
	DO RESUME #1
	PLEASE STOP
(60003)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1

	DO .4 <- #1
	DO .5 <- #2
	DO (60004) NEXT
	PLEASE STOP
	PLEASE FOR .4 IN #1 .. #499, IF ,60003SUB.4#1 EQ ,60003SUB'.4+#1'#1
	       AND ,60003SUB.4#4 EQ ,60003SUB'.4+#1'#4, THEN
	       ,60003SUB.4'#5 .. #10' <- ,60003SUB'.4+#1''#5 .. #10'
	       COPY MOTION VERB SYNONYMS
(60004)	DO FORGET #1
	DO (60005) NEXT
	DO .1 <- .5
	DO .2 <- #500
	DO (60006) NEXT
	DO .4 <- .5
	DO (1020) NEXT
	DO .5 <- .1
	DO (60004) NEXT
	PLEASE STOP
(60005)	DO .1 <- ,60003SUB.4#1
	DO .2 <- ,60003SUB.5#1
	DO (40011) NEXT DO NOTE .1=.2
	DO .1 <- ,60003SUB.4#4
	DO .2 <- ,60003SUB.5#4
	DO (40011) NEXT DO NOTE .1=.2
	DO ,60003SUB.5#5 <- ,60003SUB.4#5
	DO ,60003SUB.5#6 <- ,60003SUB.4#6
	DO ,60003SUB.5#7 <- ,60003SUB.4#7
	DO ,60003SUB.5#8 <- ,60003SUB.4#8
	DO ,60003SUB.5#9 <- ,60003SUB.4#9
	DO ,60003SUB.5#10 <- ,60003SUB.4#10
	DO ,60003SUB.5#11 <- ,60003SUB.4#11
	DO ,60003SUB.5#12 <- ,60003SUB.4#12
	DO RESUME #1
	PLEASE STOP
(60006)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1

	DO ,60000 <- #5 BY #100
	DO,60000SUB#1#1<-#2    DO,60000SUB#1#2<-#0
	DO,60000SUB#1#3<-#72   DO,60000SUB#1#4<-#101  DO,60000SUB#1#5<-#108
	DO,60000SUB#1#6<-#108  DO,60000SUB#1#7<-#111
	DO,60000SUB#1#18<-#100 DO,60000SUB#1#19<-#97  DO,60000SUB#1#20<-#114
	DO,60000SUB#1#21<-#107 DO,60000SUB#1#22<-#110 DO,60000SUB#1#23<-#101
	DO,60000SUB#1#24<-#115 DO,60000SUB#1#25<-#115
	DO,60000SUB#1#33<-#84  DO,60000SUB#1#34<-#72  DO,60000SUB#1#35<-#69
	DO,60000SUB#1#36<-#32  DO,60000SUB#1#37<-#83  DO,60000SUB#1#38<-#79
	DO,60000SUB#1#39<-#85  DO,60000SUB#1#40<-#78  DO,60000SUB#1#41<-#68
	DO,60000SUB#1#42<-#32  DO,60000SUB#1#43<-#79  DO,60000SUB#1#44<-#70
	DO,60000SUB#1#45<-#32  DO,60000SUB#1#46<-#83  DO,60000SUB#1#47<-#73
	DO,60000SUB#1#48<-#76  DO,60000SUB#1#49<-#69  DO,60000SUB#1#50<-#78
	DO,60000SUB#1#51<-#67  DO,60000SUB#1#52<-#69  DO,60000SUB#1#53<-#32
	DO,60000SUB#1#54<-#71  DO,60000SUB#1#55<-#82  DO,60000SUB#1#56<-#69
	DO,60000SUB#1#57<-#69  DO,60000SUB#1#58<-#84  DO,60000SUB#1#59<-#83
	DO,60000SUB#1#60<-#32  DO,60000SUB#1#61<-#89  DO,60000SUB#1#62<-#79
	DO,60000SUB#1#63<-#85  DO,60000SUB#1#64<-#46  DO,60000SUB#1#65<-#10

	DO,60000SUB#2#1<-#8    DO,60000SUB#2#2<-#1
	DO,60000SUB#2#3<-#70   DO,60000SUB#2#4<-#114  DO,60000SUB#2#5<-#101
	DO,60000SUB#2#6<-#101
	DO,60000SUB#2#18<-#66  DO,60000SUB#2#19<-#105 DO,60000SUB#2#20<-#114
	DO,60000SUB#2#21<-#100
	DO,60000SUB#2#33<-#89  DO,60000SUB#2#34<-#79  DO,60000SUB#2#35<-#85
	DO,60000SUB#2#36<-#32  DO,60000SUB#2#37<-#72  DO,60000SUB#2#38<-#69
	DO,60000SUB#2#39<-#65  DO,60000SUB#2#40<-#82  DO,60000SUB#2#41<-#32
	DO,60000SUB#2#42<-#76  DO,60000SUB#2#43<-#89  DO,60000SUB#2#44<-#78
	DO,60000SUB#2#45<-#89  DO,60000SUB#2#46<-#82  DO,60000SUB#2#47<-#68
	DO,60000SUB#2#48<-#32  DO,60000SUB#2#49<-#83  DO,60000SUB#2#50<-#75
	DO,60000SUB#2#51<-#89  DO,60000SUB#2#52<-#78  DO,60000SUB#2#53<-#89
	DO,60000SUB#2#54<-#82  DO,60000SUB#2#55<-#68  DO,60000SUB#2#56<-#32
	DO,60000SUB#2#57<-#73  DO,60000SUB#2#58<-#78  DO,60000SUB#2#59<-#32
	DO,60000SUB#2#60<-#84  DO,60000SUB#2#61<-#72  DO,60000SUB#2#62<-#69
	DO,60000SUB#2#63<-#32  DO,60000SUB#2#64<-#68  DO,60000SUB#2#65<-#73
	DO,60000SUB#2#66<-#83  DO,60000SUB#2#67<-#84  DO,60000SUB#2#68<-#65
	DO,60000SUB#2#69<-#78  DO,60000SUB#2#70<-#67  DO,60000SUB#2#71<-#69
	DO,60000SUB#2#72<-#46  DO,60000SUB#2#73<-#10

	DO,60000SUB#3#1<-#35   DO,60000SUB#3#2<-#2
	DO,60000SUB#3#3<-#116  DO,60000SUB#3#4<-#97   DO,60000SUB#3#5<-#99
	DO,60000SUB#3#6<-#107  DO,60000SUB#3#7<-#108  DO,60000SUB#3#8<-#101
	DO,60000SUB#3#18<-#66  DO,60000SUB#3#19<-#101 DO,60000SUB#3#20<-#97
	DO,60000SUB#3#21<-#114
	DO,60000SUB#3#33<-#68  DO,60000SUB#3#34<-#65  DO,60000SUB#3#35<-#32
	DO,60000SUB#3#36<-#66  DO,60000SUB#3#37<-#69  DO,60000SUB#3#38<-#65
	DO,60000SUB#3#39<-#82  DO,60000SUB#3#40<-#32  DO,60000SUB#3#41<-#71
	DO,60000SUB#3#42<-#73  DO,60000SUB#3#43<-#86  DO,60000SUB#3#44<-#69
	DO,60000SUB#3#45<-#83  DO,60000SUB#3#46<-#32  DO,60000SUB#3#47<-#89
	DO,60000SUB#3#48<-#79  DO,60000SUB#3#49<-#85  DO,60000SUB#3#50<-#32
	DO,60000SUB#3#51<-#65  DO,60000SUB#3#52<-#32  DO,60000SUB#3#53<-#83
	DO,60000SUB#3#54<-#84  DO,60000SUB#3#55<-#73  DO,60000SUB#3#56<-#70
	DO,60000SUB#3#57<-#70  DO,60000SUB#3#58<-#45  DO,60000SUB#3#59<-#65
	DO,60000SUB#3#60<-#82  DO,60000SUB#3#61<-#77  DO,60000SUB#3#62<-#33
	DO,60000SUB#3#63<-#10

	DO,60000SUB#4#1<-#24   DO,60000SUB#4#2<-#4
	DO,60000SUB#4#3<-#99   DO,60000SUB#4#4<-#104  DO,60000SUB#4#5<-#111
	DO,60000SUB#4#6<-#112
	DO,60000SUB#4#18<-#98  DO,60000SUB#4#19<-#101 DO,60000SUB#4#20<-#97
	DO,60000SUB#4#21<-#110 DO,60000SUB#4#22<-#115 DO,60000SUB#4#23<-#116
	DO,60000SUB#4#24<-#97  DO,60000SUB#4#25<-#108 DO,60000SUB#4#26<-#107
	DO,60000SUB#4#33<-#65  DO,60000SUB#4#34<-#32  DO,60000SUB#4#35<-#86
	DO,60000SUB#4#36<-#79  DO,60000SUB#4#37<-#73  DO,60000SUB#4#38<-#67
	DO,60000SUB#4#39<-#69  DO,60000SUB#4#40<-#32  DO,60000SUB#4#41<-#70
	DO,60000SUB#4#42<-#82  DO,60000SUB#4#43<-#79  DO,60000SUB#4#44<-#77
	DO,60000SUB#4#45<-#32  DO,60000SUB#4#46<-#65  DO,60000SUB#4#47<-#66
	DO,60000SUB#4#48<-#79  DO,60000SUB#4#49<-#86  DO,60000SUB#4#50<-#69
	DO,60000SUB#4#51<-#32  DO,60000SUB#4#52<-#83  DO,60000SUB#4#53<-#73
	DO,60000SUB#4#54<-#78  DO,60000SUB#4#55<-#71  DO,60000SUB#4#56<-#83
	DO,60000SUB#4#57<-#44  DO,60000SUB#4#58<-#32  DO,60000SUB#4#59<-#34
	DO,60000SUB#4#60<-#72  DO,60000SUB#4#61<-#73  DO,60000SUB#4#62<-#84
	DO,60000SUB#4#63<-#32  DO,60000SUB#4#64<-#84  DO,60000SUB#4#65<-#72
	DO,60000SUB#4#66<-#69  DO,60000SUB#4#67<-#32  DO,60000SUB#4#68<-#82
	DO,60000SUB#4#69<-#79  DO,60000SUB#4#70<-#65  DO,60000SUB#4#71<-#68
	DO,60000SUB#4#72<-#44  DO,60000SUB#4#73<-#32  DO,60000SUB#4#74<-#74
	DO,60000SUB#4#75<-#65  DO,60000SUB#4#76<-#67  DO,60000SUB#4#77<-#75
	DO,60000SUB#4#78<-#33  DO,60000SUB#4#79<-#34  DO,60000SUB#4#80<-#10

	DO,60000SUB#5#1<-#61   DO,60000SUB#5#2<-#0
	DO,60000SUB#5#3<-#74   DO,60000SUB#5#4<-#97   DO,60000SUB#5#5<-#109
	DO,60000SUB#5#33<-#65  DO,60000SUB#5#34<-#32  DO,60000SUB#5#35<-#72
	DO,60000SUB#5#36<-#79  DO,60000SUB#5#37<-#76  DO,60000SUB#5#38<-#76
	DO,60000SUB#5#39<-#79  DO,60000SUB#5#40<-#87  DO,60000SUB#5#41<-#32
	DO,60000SUB#5#42<-#86  DO,60000SUB#5#43<-#79  DO,60000SUB#5#44<-#73
	DO,60000SUB#5#45<-#67  DO,60000SUB#5#46<-#69  DO,60000SUB#5#47<-#32
	DO,60000SUB#5#48<-#83  DO,60000SUB#5#49<-#65  DO,60000SUB#5#50<-#89
	DO,60000SUB#5#51<-#83  DO,60000SUB#5#52<-#44  DO,60000SUB#5#53<-#32
	DO,60000SUB#5#54<-#34  DO,60000SUB#5#55<-#77  DO,60000SUB#5#56<-#79
	DO,60000SUB#5#57<-#79  DO,60000SUB#5#58<-#75  DO,60000SUB#5#59<-#73
	DO,60000SUB#5#60<-#69  DO,60000SUB#5#61<-#32  DO,60000SUB#5#62<-#66
	DO,60000SUB#5#63<-#76  DO,60000SUB#5#64<-#65  DO,60000SUB#5#65<-#89
	DO,60000SUB#5#66<-#76  DO,60000SUB#5#67<-#79  DO,60000SUB#5#68<-#67
	DO,60000SUB#5#69<-#75  DO,60000SUB#5#70<-#34  DO,60000SUB#5#71<-#46
	DO,60000SUB#5#72<-#10

	DO,60006SUB#13#6<-#32  DO,60006SUB#13#7<-#68  DO,60006SUB#13#8<-#79
	DO,60006SUB#13#9<-#78  DO,60006SUB#13#10<-#69 DO,60006SUB#13#11<-#10
	DO,60006SUB#13#12<-#0
	DO (57001) NEXT
	DO RESUME #1
	PLEASE STOP

(60010)	DON'T RESUME #1
	DO .1 <- #1
	DO .2 <- '∀"',60009SUB",61000SUB#1"'~#1"¢#1'~#1
	DO (40011) NEXT
	DO .3 <- #1
	DO (60011) NEXT
	DO .3 <- #2
	DO (60011) NEXT
	DO .3 <- #3
	DO (60011) NEXT
	DO .3 <- #4
	DO (60011) NEXT
	DO .3 <- #5
	DO (60011) NEXT
	DO RESUME #1
	PLEASE STOP
(60011)	DO .1 <- ,60000SUB.3#1
	DO (30111) NEXT
	DO .2 <- #1
	DO (40011) NEXT
	DO .1 <- ,61003SUB',60000SUB.3#1'#4
	DO .2 <- ,60000SUB.3#2
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#3
	DO .2 <- ,51001SUB#1#1
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#4
	DO .2 <- ,51001SUB#1#2
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#5
	DO .2 <- ,51001SUB#1#3
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#6
	DO .2 <- ,51001SUB#1#4
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#7
	DO .2 <- ,51001SUB#1#5
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#8
	DO .2 <- ,51001SUB#1#6
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#9
	DO .2 <- ,51001SUB#1#7
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#10
	DO .2 <- ,51001SUB#1#8
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#11
	DO .2 <- ,51001SUB#1#9
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#12
	DO .2 <- ,51001SUB#1#10
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#13
	DO .2 <- ,51001SUB#1#11
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#14
	DO .2 <- ,51001SUB#1#12
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#15
	DO .2 <- ,51001SUB#1#13
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#16
	DO .2 <- ,51001SUB#1#14
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#17
	DO .2 <- ,51001SUB#1#15
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#18
	DO .2 <- ,51001SUB#2#1
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#19
	DO .2 <- ,51001SUB#2#2
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#20
	DO .2 <- ,51001SUB#2#3
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#21
	DO .2 <- ,51001SUB#2#4
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#22
	DO .2 <- ,51001SUB#2#5
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#23
	DO .2 <- ,51001SUB#2#6
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#24
	DO .2 <- ,51001SUB#2#7
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#25
	DO .2 <- ,51001SUB#2#8
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#26
	DO .2 <- ,51001SUB#2#9
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#27
	DO .2 <- ,51001SUB#2#10
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#28
	DO .2 <- ,51001SUB#2#11
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#29
	DO .2 <- ,51001SUB#2#12
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#30
	DO .2 <- ,51001SUB#2#13
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#31
	DO .2 <- ,51001SUB#2#14
	DO (40011) NEXT
	DO .1 <- ,60000SUB.3#32
	DO .2 <- ,51001SUB#2#15
	DO (40011) NEXT
	DO FORGET #1
	DO .2 <- #0
	DO .4 <- .3
	DO .5 <- #33
	DO (60012) NEXT
	PLEASE STOP
(60012)	DO FORGET #1
	DO .1 <- ,60000SUB.4.5
	DO .2 <- #0
	DO (40002) NEXT
	DO .1 <- .3
	DO (40011) NEXT
	DO .1 <- ,60000SUB.4.5
	DO (50011) NEXT
	DO ,60000SUB.4#33 <- #0
	DO .1 <- .5
	DO (1020) NEXT
	DO .5 <- .1
	DO (60012) NEXT
	PLEASE STOP

	PLEASE INITIALIZE STATE
	PLEASE NOTE FLAGS ARE #0 FOR FALSE AND #1 FOR TRUE
	PLEASE NOTE
	PLEASE NOTE ,61000SUB#1 IS LOC
	PLEASE NOTE ,61000SUB#2 IS NEWLOC
	PLEASE NOTE ,61000SUB#3 IS OLDLOC
	PLEASE NOTE ,61000SUB#4 IS TURNS TALLIES HOW MANY COMMANDS GIVEN
	PLEASE NOTE ,61000SUB#5 IS LIMIT LIFETIME OF LAMP
	PLEASE NOTE ,61000SUB#6 IS IWEST
	PLEASE NOTE ,61000SUB#7 IS KNFLOC
	PLEASE NOTE ,61000SUB#8 IS DETAIL
	PLEASE NOTE ,61000SUB#9 IS ABBNUM IS HOW OFTEN TO PRINT LONG DESCRIPTIONS
	PLEASE NOTE ,61000SUB#10 IS DFLAG
	PLEASE NOTE ,61000SUB#11 IS NUMDIE
	PLEASE NOTE ,61000SUB#12 IS HOLDNG
	PLEASE NOTE ,61000SUB#13 IS DKILL
	PLEASE NOTE ,61000SUB#14 IS FOOBAR
	PLEASE NOTE ,61000SUB#15 IS BONUS
	PLEASE NOTE ,61000SUB#16 IS CLOCK1
	PLEASE NOTE ,61000SUB#17 IS CLOCK2
	PLEASE NOTE ,61000SUB#18 IS OLDLC2
	PLEASE NOTE ,61000SUB#19 IS CLOSNG
	PLEASE NOTE ,61000SUB#20 IS WZDARK IS WHETHER THE LOC HE'S LEAVING WAS DARK
	PLEASE NOTE ,61000SUB#21 IS VERB
	PLEASE NOTE ,61000SUB#22 IS OBJ
	PLEASE NOTE ,61000SUB#23 IS OLD_OBJ
	PLEASE NOTE ,61000SUB#24 IS LMWARN
	PLEASE NOTE ,61000SUB#25 IS PANIC
	PLEASE NOTE ,61000SUB#26 IS CLOSED
	PLEASE NOTE ,61000SUB#27 IS GAVEUP
	PLEASE NOTE ,61000SUB#28 IS SCORNG
	PLEASE NOTE ,61000SUB#29 IS TALLY
	PLEASE NOTE ,61000SUB#30 IS TALLY2
	PLEASE NOTE
	PLEASE NOTE ,61001 IS LOCATION DATA
	PLEASE NOTE   FIRST SUBSCRIPT IS LOCATION INDEX
	PLEASE NOTE   SECOND SUBSCRIPT
	PLEASE NOTE     #1 IS LOC.FIRSTOBJ (#0 FOR NONE).
	PLEASE NOTE     #2 IS LOC.ABB
	PLEASE NOTE     #3 IS LOC.COND
	PLEASE NOTE
	PLEASE NOTE ,61002 IS DWARVES STATE
	PLEASE NOTE   FIRST SUBSCRIPT IS DWARF INDEX
	PLEASE NOTE   SECOND SUBSCRIPT
	PLEASE NOTE     #1 IS LOCATION
	PLEASE NOTE     #2 IS PRIOR LOCATION
	PLEASE NOTE     #3 IS SEEN FLAG
	PLEASE NOTE
	PLEASE NOTE ,61003 IS OBJECT DATA
	PLEASE NOTE   FIRST SUBSCRIPT IS OBJECT INDEX
	PLEASE NOTE   SECOND SUBSCRIPT
	PLEASE NOTE     #1 IS LOCATION INDEX
	PLEASE NOTE     #2 IS NEXT OBJECT (#0 FOR NONE).
	PLEASE NOTE     #3 IS PREVIOUS OBJECT (#0 FOR NONE).
	PLEASE NOTE     #4 IS PROP
	PLEASE NOTE     #5 IS PROP NEGATIVE FLAG
	PLEASE NOTE
	PLEASE NOTE ,61004 IS HINT STATE FLAG
	PLEASE NOTE   FIRST SUBSCRIPT IS HINT INDEX
	PLEASE NOTE   SECOND SUBSCRIPT
	PLEASE NOTE     #1 IS HINTED
	PLEASE NOTE     #2 IS HINTLC
	PLEASE NOTE
	PLEASE NOTE ,61005 IS TK, USED FOR RANDOM DWARF MOTION
(61000)	DO ,61000 <- #30
	DO ,61000SUB#1 <- #1 DO NOTE LOC
	DO ,61000SUB#2 <- #1 DO NOTE NEWLOC
	DO ,61000SUB#5 <- #330 DO NOTE LIMIT
	DO ,61000SUB#9 <- #5 DO NOTE ABBNUM
	DO ,61000SUB#16 <- #30 DO NOTE CLOCK1
	DO ,61000SUB#17 <- #50 DO NOTE CLOCK2
	DO ,61000SUB#29 <- #15 DO NOTE TALLY

	DO ,61001 <- #140 BY #4

	DO ,61002 <- #6 BY #3
	DO ,61002SUB#1#1 <- #19 DO NOTE DLOC
	DO ,61002SUB#2#1 <- #27
	DO ,61002SUB#3#1 <- #33
	DO ,61002SUB#4#1 <- #44
	DO ,61002SUB#5#1 <- #64
	DO ,61002SUB#6#1 <- #114

	DO ,61003 <- #64 BY #5
	DO .1 <- #64
	DO (61001) NEXT
	PLEASE STOP
	PLEASE LOOP OVER OBJECTS IN REVERSE ORDER TO MOVE TO INITIAL LOCATION
(61001)	DO FORGET #1
	DO .2 <- ,60007SUB.1#1
	DO (30001) NEXT DO NOTE MOVEOBJ
	DO .2 <- #1
	DO (1010) NEXT
	DO .1 <- .3
	DO .2 <- #0
	DO (61002) NEXT
	DO (61001) NEXT
	PLEASE STOP
(61002)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1
	DO .1 <- #50
	DO .2 <- #64
	DO (61003) NEXT
	PLEASE STOP
	PLEASE LOOP OVER TREASURES AND SET OBJ.PROPNEG <- #1
(61003)	DO FORGET #1
	DO ,61003SUB.1#5 <- #1
	DO (61004) NEXT
	DO (1020) NEXT
	DO (61003) NEXT
	PLEASE STOP
(61004)	DO (40011) NEXT DO NOTE .1=.2
	DO FORGET #1

	DO ,61004 <- #9 BY #2
	DO ,61005 <- #12

	DO RESUME #1
	PLEASE STOP
