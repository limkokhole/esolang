	DO (1) NEXT
	PLEASE INITIALIZE
(1)	DO (50000) NEXT
	DO (51000) NEXT
	DO (60000) NEXT
	DO FORGET #1
	DO (2) NEXT
	PLEASE STOP

	PLEASE START TEMPORARY CODE ...............................
(2)	DO FORGET #1
	DO (51001) NEXT
	DO [VERB] .1
	DO [OBJECT] .2
	DO (3) NEXT
	DO (5) NEXT
	DO (7) NEXT
	DO (9) NEXT
	DO (2) NEXT
	PLEASE STOP
(3)	DO (4) NEXT
	DO NOTE QUIT
	DO GIVE UP
	PLEASE STOP
(4)	DO RESUME '∀"'#511~"'∀#244¢.1'~'#0¢#511'"'~#1"¢#1'~#3
	PLEASE STOP
(5)	DO (6) NEXT
	DO NOTE LOOK
	DO FORGET #1
	DO WRITE IN .1
	DO (55201) NEXT
	DO (2) NEXT
	PLEASE STOP
(6)	DO RESUME '∀"'#511~"'∀#80¢.1'~'#0¢#511'"'~#1"¢#1'~#3
	PLEASE STOP
(7)	DO (8) NEXT
	DO NOTE VIEW
	DO FORGET #1
	DO WRITE IN .1
	DO (55101) NEXT
	DO (2) NEXT
	PLEASE STOP
(8)	DO RESUME '∀"'#511~"'∀#40¢.1'~'#0¢#511'"'~#1"¢#1'~#3
	PLEASE STOP
(9)	DO (10) NEXT
	DO NOTE EXAMI
	DO FORGET #1
	DO WRITE IN .1 + .2
	DO (55501) NEXT
	DO (2) NEXT
	PLEASE STOP
(10)	DO RESUME '∀"'#511~"'∀#81¢.1'~'#0¢#511'"'~#1"¢#1'~#3
	PLEASE STOP
	PLEASE END TEMPORARY CODE .................................



	PLEASE INITIALIZE INPUT/OUTPUT
	PLEASE CHOOSE PUKE BINARY I/O OR C-INTERCAL BINARY I/O
		BY ABSTAINING FROM (50000) FOR PUKE BINARY I/O OR
		REINSTATING (50000) FOR C-INTERCAL BINARY I/O
(50000)	DON'T (50001) NEXT
	DO ,50121 <- #2
	DO REINSTATE (50011)
	DO ABSTAIN FROM (50012)
	DO REINSTATE (50021)
	DO ABSTAIN FROM (50022)
	DO (50001) NEXT
	PLEASE STOP
(50001)	DO RESUME #2
	PLEASE STOP

	PLEASE READ OUT IN ONE BYTE FROM .1
	PLEASE RETAIN .50211 BETWEEN INVOCATIONS WHEN USING C-INTERCAL BINARY I/O
(50011)	DON'T (50111) NEXT
(50012) DO (50211) NEXT
	DO RESUME #1
	PLEASE STOP

	PLEASE WRITE IN ONE BYTE INTO .1
	PLEASE RETAIN ,50121 BETWEEN INVOCATIONS WHEN USING PUKE BINARY I/O
	PLEASE RETAIN .50221 BETWEEN INVOCATIONS WHEN USING C-INTERCAL BINARY I/O
(50021)	DON'T (50121) NEXT
(50022)	DO (50221) NEXT
	DO RESUME #1
	PLEASE STOP

	PLEASE READ OUT ONE BYTE FROM .1 USING PUKE BINARY I/O
(50111)	DO STASH ,8
	DO ,8 <- #8
	DO ,8SUB#1 <- .1~#1
	DO ,8SUB#2 <- !1~#2'¢#0
	DO ,8SUB#3 <- !1~#4'¢!1~#4'
	DO ,8SUB#4 <- #0¢"!1~#8'¢#0"
	DO ,8SUB#5 <- #0¢"!1~#16'¢!1~#16'"
	DO ,8SUB#6 <- !1~#32'¢"!1~#32'¢#0"
	DO ,8SUB#7 <- !1~#64'¢"!1~#64'¢!1~#64'"
	DO ,8SUB#8 <- "!1~#128'¢#0"¢#0
	DO READ OUT ,8
	DO RETRIEVE ,8
	DO RESUME #1
	PLEASE STOP

	PLEASE WRITE IN ONE BYTE TO .1 USING PUKE BINARY I/O
	PLEASE RETAIN ,50121 BETWEEN INVOCATIONS
(50121)	DO STASH .2 + .3 + .50120 + .50121
	DO .50120 <- #1
	DO .50121 <- #0
	DO (50122) NEXT
	PLEASE STOP
(50122)	DO FORGET #1
	DO (50123) NEXT
	DO (50130) NEXT
	PLEASE STOP
(50123)	DO (50124) NEXT
	DO .1 <- .50121
	DO RETRIEVE .2 + .3 + .50120 + .50121
	DO RESUME #2
	PLEASE STOP
(50124)	DO RESUME "∀'"#255~!50120~#255'"~#1'¢#1"~#3
	PLEASE STOP
(50130)	DO FORGET #1
	DO (50131) NEXT
	DO .1 <- ,50121SUB#1
	DO .2 <- #1
	DO (1010) NEXT
	DO ,50121SUB#1 <- .3
	DO .50120 <- !50120¢#0'~'#32767¢#1'
	DO (50122) NEXT
	PLEASE STOP
(50131)	DO (50132) NEXT
	DO FORGET #1
	DO (50140) NEXT
	PLEASE STOP
(50132)	DO RESUME "∀'"#65535~,50121SUB#1"~#1'¢#1"~#3
	PLEASE STOP
(50140)	DO FORGET #1
	DO (50141) NEXT
	DO .1 <- ,50121SUB#2
	DO .2 <- #1
	DO (1010) NEXT
	DO ,50121SUB#2 <- .3
	DO .1 <- .50120
	DO .2 <- .50121
	DO (1000) NEXT
	DO .50120 <- !50120¢#0'~'#32767¢#1'
	DO .50121 <- .3
	DO (50122) NEXT
	PLEASE STOP
(50141)	DO (50142) NEXT
	DO FORGET #1
	DO (50150) NEXT
	PLEASE STOP
(50142)	DO RESUME "∀'"#65535~,50121SUB#2"~#1'¢#1"~#3
	PLEASE STOP
(50150)	DO FORGET #1
	DO STASH ,50150
	DO ,50150 <- #1
	DO WRITE IN ,50150
	DO ,50121SUB#1 <- ,50150SUB#1
	DO ,50121SUB#2 <- #0
	DO RETRIEVE ,50150
	DO (50153) NEXT
	DO (50151) NEXT
	DO (50122) NEXT
	PLEASE STOP
(50151)	DO (50152) NEXT
	DO .1 <- .50121
	DO RETRIEVE .2 + .3 + .50120 + .50121
	DO RESUME #2
	PLEASE STOP
(50152)	DO RESUME '∀"'"#65535¢#65535"~"',50121SUB#1'¢',50121SUB#2'"'~#1"¢#1'~#3
	PLEASE STOP
(50153)	DO (50152) NEXT
	DO WRITE IN ,50121
	DO RESUME #1
	PLEASE STOP

	PLEASE READ OUT ONE BYTE FROM .1 USING C-INTERCAL BINARY I/O
	PLEASE RETAIN .50211 BETWEEN INVOCATIONS
(50211)	DO STASH .1 + .2 + .3 + ,50211
	DO ,50211 <- #1
	DO .2 <- '"!1~#1'¢!1~#16'"¢"!1~#4'¢!1~#64'"'¢'"!1~#2'¢!1~#32'"¢"!1~#8'¢!1~#128'"'
	DO .1 <- .50211
	DO .50211 <- .2
	DO (1010) NEXT
	DO ,50211SUB#1 <- .3~#255
	DO READ OUT ,50211
	DO RETRIEVE .1 + .2 + .3 + ,50211
	DO RESUME #1
	PLEASE STOP

	PLEASE WRITE IN ONE BYTE TO .1 USING C-INTERCAL BINARY I/O
	PLEASE RETAIN .50221 BETWEEN INVOCATIONS
(50221) DO STASH .2 + .3 + ,50221
	DO ,50221 <- #1
	DO WRITE IN ,50221
	DO (50222) NEXT
	DO (50224) NEXT
	PLEASE STOP
(50222)	DO (50223) NEXT
	DO .1 <- #0
	DO RETRIEVE .2 + .3 + ,50221
	DO RESUME #2
	PLEASE STOP
(50223)	DO RESUME '∀"'#511~"'∀#256¢,50221SUB#1'~'#0¢#511'"'~#1"¢#1'~#3
	PLEASE STOP
(50224)	DO .1 <- ,50221SUB#1
	DO .2 <- .50221
	DO (1000) NEXT
	DO .1 <- .3~#255
	DO .50221 <- .1
	DO RETRIEVE .2 + .3 + ,50221
	DO RESUME #2
	PLEASE STOP

	PLEASE DO LINE INPUT AND TOKENIZATION
	PLEASE USE (51000) FOR INITIALIZATION ENTRY POINT
	PLEASE USE (51001) FOR MAIN ENTRY POINT
	PLEASE NOTE ON RESUMPTION FROM (51001):
	PLEASE NOTE   .1 IS THE VOCABULARY TABLE INDEX OF THE FIRST WORD OR #0
	PLEASE NOTE   .2 IS THE VOCABULARY TABLE INDEX OF THE SECOND WORD OR #0
	PLEASE NOTE
	PLEASE NOTE ,51001 HOLDS THE INPUT
	PLEASE NOTE ,51001 SUB #1 .C CONTAINS UP TO #15 CHARACTERS OF THE
	PLEASE NOTE   FIRST WORD WITH #0 INDICATING THE END OF WORD
	PLEASE NOTE ,51001 SUB #2 .C CONTAINS UP TO #15 CHARACTERS OF THE
	PLEASE NOTE   SECOND WORD WITH #0 INDICATING THE END OF WORD
	PLEASE NOTE ,51001 SUB #3 #1 THE INDEX OF THE FIRST WORD'S ENTRY
	PLEASE NOTE   IN THE VOCABULARY TABLE ,60004 OR #0 IF NONE
	PLEASE NOTE ,51001 SUB #3 #2 THE INDEX OF THE SECOND WORD'S ENTRY
	PLEASE NOTE   IN THE VOCABULARY TABLE ,60004 OR #0 IF NONE
	PLEASE NOTE CHARACTERS BEYOND THE 15TH CHARACTER OF THE FIRST WORD,
	PLEASE NOTE   CHARACTERS BEYOND THE 15TH CHARACTER OF THE SECOND WORD,
	PLEASE NOTE   AND ALL CHARACTERS AFTER THE SECOND WORD ARE DISCARDED
	PLEASE NOTE   UNTIL THE END OF LINE
	PLEASE NOTE ONLY THE FIRST FIVE CHARACTERS OF THE FIRST AND SECOND
	PLEASE NOTE   WORDS ARE USED TO LOOK UP THE VOCABULARY TABLE INDEX
(51000)	DO ,51001 <- #3 BY #16
	DO RESUME #1
	PLEASE STOP
(51001)	DO ,51001SUB#1#1 <- #0  DO ,51001SUB#2#1 <- #0  DO ,51001SUB#3#1 <- #0
	DO ,51001SUB#1#2 <- #0  DO ,51001SUB#2#2 <- #0  DO ,51001SUB#3#2 <- #0
	DO ,51001SUB#1#3 <- #0  DO ,51001SUB#2#3 <- #0  DO ,51001SUB#3#3 <- #0
	DO ,51001SUB#1#4 <- #0  DO ,51001SUB#2#4 <- #0  DO ,51001SUB#3#4 <- #0
	DO ,51001SUB#1#5 <- #0  DO ,51001SUB#2#5 <- #0  DO ,51001SUB#3#5 <- #0
	DO ,51001SUB#1#6 <- #0  DO ,51001SUB#2#6 <- #0  DO ,51001SUB#3#6 <- #0
	DO ,51001SUB#1#7 <- #0  DO ,51001SUB#2#7 <- #0  DO ,51001SUB#3#7 <- #0
	DO ,51001SUB#1#8 <- #0  DO ,51001SUB#2#8 <- #0  DO ,51001SUB#3#8 <- #0
	DO ,51001SUB#1#9 <- #0  DO ,51001SUB#2#9 <- #0  DO ,51001SUB#3#9 <- #0
	DO ,51001SUB#1#10 <- #0 DO ,51001SUB#2#10 <- #0 DO ,51001SUB#3#10 <- #0
	DO ,51001SUB#1#11 <- #0 DO ,51001SUB#2#11 <- #0 DO ,51001SUB#3#11 <- #0
	DO ,51001SUB#1#12 <- #0 DO ,51001SUB#2#12 <- #0 DO ,51001SUB#3#12 <- #0
	DO ,51001SUB#1#13 <- #0 DO ,51001SUB#2#13 <- #0 DO ,51001SUB#3#13 <- #0
	DO ,51001SUB#1#14 <- #0 DO ,51001SUB#2#14 <- #0 DO ,51001SUB#3#14 <- #0
	DO ,51001SUB#1#15 <- #0 DO ,51001SUB#2#15 <- #0 DO ,51001SUB#3#15 <- #0
	DO ,51001SUB#1#16 <- #0 DO ,51001SUB#2#16 <- #0 DO ,51001SUB#3#16 <- #0
	DO ,51001SUB#3#4 <- #1
	DO ,51001SUB#3#5 <- #7
	DO ,51001SUB#3#6 <- #8
	DO (50021) NEXT
	DO (51002) NEXT
	PLEASE SKIP LEADING SPACES, FIRST CHECKING FOR END OF LINE
	PLEASE NOTE   ,51001SUB#3#3 IS CURRENT CHARACTER INDEX
	PLEASE NOTE   ,51001SUB#3#4 IS CURRENT WORD INDEX (#1 OR #2)
	PLEASE NOTE   ,51001SUB#3#5 IS CURRENT WORD INDEX FOR FIRST
	PLEASE NOTE      THREE CHARACTERS (#7 OR #9)
	PLEASE NOTE   ,51001SUB#3#6 IS CURRENT WORD INDEX FOR LAST
	PLEASE NOTE      TWO CHARACTERS (#8 OR #10)
	PLEASE NOTE   ,51001SUB#3#7 IS FIRST THREE CHARACTERS OF FIRST WORD
	PLEASE NOTE     AT FIVE BITS PER CHARACTER
	PLEASE NOTE   ,51001SUB#3#8 IS LAST TWO CHARACTERS OF FIRST WORD
	PLEASE NOTE   ,51001SUB#3#9 IS FIRST THREE CHARACTERS OF SECOND WORD
	PLEASE NOTE     AT FIVE BITS PER CHARACTER
	PLEASE NOTE   ,51001SUB#3#10 IS LAST TWO CHARACTERS OF SECOND WORD
(51002)	DO FORGET #1
	DO NOTE CHECK FOR END OF LINE
	DO (51003) NEXT
	DO NOTE NOT AT END OF LINE, CHECK FOR SPACE
	DO (51005) NEXT
	DO NOTE NOT SPACE, START WRITING IN FIRST OR SECOND WORD
	DO ,51001SUB#3#3 <- #1
	DO (51011) NEXT
	PLEASE STOP
(51003)	DO (51004) NEXT
	DO FORGET #1
	DO NOTE AT END OF LINE
	DO (51081) NEXT
(51004)	DO RESUME '∀"'#255~"'∀#10¢.1'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51005)	DO (51006) NEXT
	DO NOTE AT SPACE
	DO FORGET #1
	DO (50021) NEXT
	DO (51002) NEXT
	PLEASE STOP
(51006)	DO RESUME '∀"'#255~"'∀#32¢.1'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
	PLEASE WRITE IN CURRENT CHARACTER INTO CURRENT WORD
(51011)	DO FORGET #1
	DO ,51001SUB',51001SUB#3#4'',51001SUB#3#3' <- .1
	DO NOTE WRITE INTO FIVE BITS
	DO (51031) NEXT
	DO .1 <- ,51001SUB#3#3
	DO (1020) NEXT
	DO (51012) NEXT
	DO NOTE NOT AT 16TH CHARACTER, ADVANCE TO NEXT CHARACTER
	DO ,51001SUB#3#3 <- .1
	DO (50021) NEXT
	DO NOTE CHECK FOR END OF LINE
	DO (51003) NEXT
	DO NOTE NOT AT END OF LINE, CHECK FOR SPACE
	DO (51014) NEXT
	DO NOTE NOT SPACE, CONTINUE WRITING IN CURRENT WORD
	DO (51011) NEXT
	PLEASE STOP
(51012)	DO (51013) NEXT
	DO FORGET #1
	DO NOTE AT 16TH CHARACTER, DISCARD UNTIL SPACE OR END OF LINE
	DO (51018) NEXT
	PLEASE STOP
(51013)	DO RESUME '∀"'#255~"'∀#16¢.1'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51014)	DO (51006) NEXT
	DO FORGET #1
	DO NOTE AT END OF WORD, CHECK IF FIRST WORD OR SECOND WORD
	DO (51015) NEXT
	DO NOTE FINISHED SECOND WORD, DISCARD UNTIL END OF LINE
	DO (51017) NEXT
	PLEASE STOP
(51015)	DO (51016) NEXT
	DO FORGET #1
	DO NOTE FINISHED FIRST WORD, SKIP SPACES AND START SECOND WORD
	DO ,51001SUB#3#4 <- #2
	DO ,51001SUB#3#5 <- #9
	DO ,51001SUB#3#6 <- #10
	DO (51002) NEXT
	PLEASE STOP
(51016)	DO RESUME ,51001SUB#3#4
	PLEASE STOP
(51017)	DO FORGET #1
	DO NOTE DISCARD UNTIL END OF LINE
	DO (50021) NEXT
	DO (51003) NEXT
	DO (51017) NEXT
	PLEASE STOP
(51018)	DO FORGET #1
	DO NOTE DISCARD UNTIL SPACE OR END OF LINE
	DO (50021) NEXT
	DO (51003) NEXT
	DO (51005) NEXT
	DO (51018) NEXT
	PLEASE STOP

	PLEASE CONVERT .1 TO FIVE BIT REPRESENTATION WHERE 2 IS MAPPED TO #29
	       AND " IS MAPPED TO #30
(51021)	DO (51022) NEXT
	DO NOTE NOT 2
	DO (51024) NEXT
	DO NOTE NOT "
	DO .1 <- .1~#31
	DO RESUME #1
	PLEASE STOP
(51022)	DO (51023) NEXT
	DO .1 <- #29
	DO RESUME #2
	PLEASE STOP
(51023)	DO RESUME '∀"'#255~"'∀#50¢.1'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51024)	DO (51025) NEXT
	DO .1 <- #30
	DO RESUME #2
	PLEASE STOP
(51025)	DO RESUME '∀"'#255~"'∀#34¢.1'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP

	PLEASE ADD FIVE BIT REPRESENTATION TO ONE OF
	       ,51001SUB#3#7 ,51001SUB#3#8 ,51001SUB#3#9 ,51001SUB#3#10
	       AFTER MULTIPLYING BY ONE OF 1, 32, OR 1024
(51031)	DO STASH .1 + .2 + .3
	DO (51021) NEXT
	DO (51032) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 1
	DO (51034) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 2
	DO (51036) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 3
	DO (51038) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 4
	DO (51040) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS NOT 5
	DO (51042) NEXT
	PLEASE STOP
(51032)	DO (51033) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 1
	DO FORGET #1
	DO ,51001SUB#3",51001SUB#3#5" <- .1
	DO (51042) NEXT
	PLEASE STOP
(51033)	DO RESUME '∀"'#255~"'∀#1¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51034)	DO (51035) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 2
	DO FORGET #1
	DO .2 <- #32
	DO (1030) NEXT
	DO .1 <- .3
	DO .2 <- ,51001SUB#3",51001SUB#3#5"
	DO (1000) NEXT
	DO ,51001SUB#3",51001SUB#3#5" <- .3
	DO (51042) NEXT
	PLEASE STOP
(51035)	DO RESUME '∀"'#255~"'∀#2¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51036)	DO (51037) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 3
	DO FORGET #1
	DO .2 <- #1024
	DO (1030) NEXT
	DO .1 <- .3
	DO .2 <- ,51001SUB#3",51001SUB#3#5"
	DO (1000) NEXT
	DO ,51001SUB#3",51001SUB#3#5" <- .3
	DO (51042) NEXT
	PLEASE STOP
(51037)	DO RESUME '∀"'#255~"'∀#3¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51038)	DO (51039) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 4
	DO FORGET #1
	DO ,51001SUB#3",51001SUB#3#6" <- .1
	DO (51042) NEXT
	PLEASE STOP
(51039)	DO RESUME '∀"'#255~"'∀#4¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51040)	DO (51041) NEXT
	DO NOTE ,51001SUB#3#3, CURRENT CHARACTER INDEX, IS 5
	DO FORGET #1
	DO .2 <- #32
	DO (1030) NEXT
	DO .1 <- .3
	DO .2 <- ,51001SUB#3",51001SUB#3#6"
	DO (1000) NEXT
	DO ,51001SUB#3",51001SUB#3#6" <- .3
	DO (51042) NEXT
	PLEASE STOP
(51041)	DO RESUME '∀"'#255~"'∀#5¢",51001SUB#3#3"'~'#0¢#255'"'~#1"¢#1'~#3
	PLEASE STOP
(51042)	DO RETRIEVE .1 + .2 + .3
	DO RESUME #2
	PLEASE STOP

	PLEASE LOOK UP WORDS IN VOCABULARY TABLE, THEN
	       RETRIEVE STASHED VARS AND RESUME
(51081)	DO FORGET #1
	DO .1 <- #1
	DO ,51001SUB#3#1 <- #0
	DO ,51001SUB#3#2 <- #0
	DO (51082) NEXT
	PLEASE STOP
(51082)	DO FORGET #1
	DO (51085) NEXT
	DO (1020) NEXT
	DO (51083) NEXT
	DO (51082) NEXT
	PLEASE STOP
(51083)	DO (51084) NEXT
	DO FORGET #1
	DO .1 <- #1
	DO (51089) NEXT
	PLEASE STOP
(51084)	DO RESUME '∀"'#511~"'∀#296¢.1'~'#0¢#511'"'~#1"¢#1'~#3
	PLEASE STOP
(51085)	DO (51086) NEXT
	DO FORGET #1
	DO (51087) NEXT
	DO (1020) NEXT
	DO (51083) NEXT
	DO (51082) NEXT
	PLEASE STOP
(51086)	DO RESUME '∀"'#65535~"'∀",51001SUB#3#7"¢",60004SUB.1#1"'~'#0¢#65535'"'~#1"¢#1'~#3
	PLEASE STOP	
(51087)	DO (51088) NEXT
	DO FORGET #1
	DO ,51001SUB#3#1 <- .1
	DO .1 <- #1
	DO (51089) NEXT
	PLEASE STOP
(51088)	DO RESUME '∀"'#65535~"'∀",51001SUB#3#8"¢",60004SUB.1#2"'~'#0¢#65535'"'~#1"¢#1'~#3
	PLEASE STOP	
(51089)	DO FORGET #1
	DO (51091) NEXT
	DO (1020) NEXT
	DO (51090) NEXT
	DO (51089) NEXT
	PLEASE STOP
(51090)	DO (51084) NEXT
	DO FORGET #1
	DO (51095) NEXT
	PLEASE STOP
(51091)	DO (51092) NEXT
	DO FORGET #1
	DO (51093) NEXT
	DO (1020) NEXT
	DO (51090) NEXT
	DO (51089) NEXT
	PLEASE STOP
(51092)	DO RESUME '∀"'#65535~"'∀",51001SUB#3#9"¢",60004SUB.1#1"'~'#0¢#65535'"'~#1"¢#1'~#3
	PLEASE STOP
(51093)	DO (51094) NEXT
	DO FORGET #1
	DO ,51001SUB#3#2 <- .1
	DO (51095) NEXT
	PLEASE STOP
(51094)	DO RESUME '∀"'#65535~"'∀",51001SUB#3#10"¢",60004SUB.1#2"'~'#0¢#65535'"'~#1"¢#1'~#3
	PLEASE STOP
(51095)	DO FORGET #1
	DO .1 <- ,51001SUB#3#1
	DO .2 <- ,51001SUB#3#2
	DO RESUME #1
	PLEASE STOP

	PLEASE READ OUT LONG FORM DESCRIPTION
	       .1 IS OBJECT INDEX
(55101)	DO STASH .1 + .2 + .3
	DO .2 <- .1
	DO .3 <- #1
	DO (55102) NEXT
	PLEASE STOP
(55102)	DO FORGET #1
	DO .1 <- ,60001SUB.2.3
	DO (55103) NEXT
	DO (50011) NEXT
	DO .1 <- .3
	DO (1020) NEXT
	DO .3 <- .1
	DO (55102) NEXT
	PLEASE STOP
(55103)	DO (55104) NEXT
	DO RETRIEVE .1 + .2 + .3
	DO RESUME #2
	PLEASE STOP
(55104)	DO RESUME "∀'#255~.1'¢#1"~#3
	PLEASE STOP

	PLEASE READ OUT LOCATION SHORT FORM DESCRIPTION
	       .1 IS OBJECT INDEX
(55201)	DO (55202) NEXT
	DO NOTE HAVE SHORT FORM DESCRIPTION
	DO STASH .1 + .2 + .3
	DO .2 <- .1
	DO .3 <- #1
	DO (55204) NEXT
	PLEASE STOP
(55202)	DO (55203) NEXT
	DO NOTE NO SHORT FORM DESCRIPTION, READ OUT LONG FORM
	DO (55101) NEXT
	DO RESUME #2
	PLEASE STOP
(55203)	DO RESUME "∀'#255~,60002SUB.1#1'¢#1"~#3
	PLEASE STOP
(55204)	DO FORGET #1
	DO .1 <- ,60002SUB.2.3
	DO (55103) NEXT
	DO (50011) NEXT
	DO .1 <- .3
	DO (1020) NEXT
	DO .3 <- .1
	DO (55204) NEXT
	PLEASE STOP

	PLEASE READ OUT OBJECT DESCRIPTION
	       .1 IS OBJECT INDEX
	       .2 IS MESSAGE INDEX
(55501)	DO STASH .1 + .2 + .3 + .4
	DO .3 <- .1
	DO .4 <- #1
	DO (55502) NEXT
	PLEASE STOP
(55502)	DO FORGET #1
	DO .1 <- ,60005SUB.3.2.4
	DO (55503) NEXT
	DO (50011) NEXT
	DO .1 <- .4
	DO (1020) NEXT
	DO .4 <- .1
	DO (55502) NEXT
	PLEASE STOP
(55503)	DO (55104) NEXT
	DO RETRIEVE .1 + .2 + .3 + .4
	DO RESUME #2
	PLEASE STOP

	PLEASE INITIALIZE DATABASE
	PLEASE NOTE ,60001 CONTAINS LONG FORM DESCRIPTIONS
	PLEASE NOTE  FIRST SUBSCRIPT IS LOCATION INDEX
	PLEASE NOTE  SECOND SUBSCRIPT IS CHARACTER INDEX
	PLEASE NOTE  ZERO TERMINATES DESCRIPTION
	PLEASE NOTE
	PLEASE NOTE ,60002 CONTAINS SHORT FORM DESCRIPTIONS
	PLEASE NOTE  FIRST SUBSCRIPT IS LOCATION INDEX
	PLEASE NOTE  SECOND SUBSCRIPT IS CHARACTER INDEX
	PLEASE NOTE    ZERO TERMINATES DESCRIPTION
	PLEASE NOTE    EMPTY MEANS USE LONG FORM DESCRIPTION
	PLEASE NOTE
	PLEASE NOTE ,60013 CONTAINS TRAVEL TABLE START INDEX INTO ,60004
	PLEASE NOTE   SUBSCRIPT IS LOCATION INDEX
	PLEASE NOTE ,60003 CONTAINS TRAVEL TABLE
	PLEASE NOTE   SUB #1 IS LOCATION INDEX
	PLEASE NOTE   SUB #2
	PLEASE NOTE     UPPER 8 BITS IS TAG
	PLEASE NOTE     LOWER 8 BITS IS VALUE
	PLEASE NOTE     WHEN TAG IS #1 VALUE IS DESTINATION LOCATION
	PLEASE NOTE     WHEN TAG IS #2 VALUE IS USED BY SPECIAL CODE SECTION
	PLEASE NOTE     WHEN TAG IS #3 VALUE IS INDEX INTO ARBITRARY MESSAGES
	PLEASE NOTE       TABLE ,60006
	PLEASE NOTE   SUB #3
	PLEASE NOTE     UPPER 8 BITS IS TAG
	PLEASE NOTE     LOWER 8 BITS IS VALUE
	PLEASE NOTE     WHEN TAG IS #1, MOTION IS UNCONDITIONAL
	PLEASE NOTE     WHEN TAG IS #2, MOTION IS UNCONDITIONAL
	PLEASE NOTE       BUT FORBIDDEN TO DWARVES
	PLEASE NOTE     WHEN TAG IS #3 VALUE IS THE % PROBABILITY OF MOTION
	PLEASE NOTE     WHEN TAG IS #4 VALUE IS INDEX OF OBJECT THAT MUST BE
	PLEASE NOTE       CARRIED FOR MOTION
	PLEASE NOTE     WHEN TAG IS #5 VALUE IS INDEX OF OBJECT THAT MUST BE
	PLEASE NOTE       CARRIED OR IN THE SAME ROOM FOR MOTION
	PLEASE NOTE     WHEN TAG IS #6 VALUE IS INDEX OF PROPERTY THAT MUST
	PLEASE NOTE       *NOT* BE #0 FOR MOTION
	PLEASE NOTE     WHEN TAG IS #7 VALUE IS INDEX OF PROPERTY THAT MUST
	PLEASE NOTE       *NOT* BE #1 FOR MOTION
	PLEASE NOTE     WHEN TAG IS #8 VALUE IS INDEX OF PROPERTY THAT MUST
	PLEASE NOTE       *NOT* BE #2 FOR MOTION
	PLEASE NOTE     WHEN TAG IS #9 VALUE IS INDEX OF PROPERTY THAT MUST
	PLEASE NOTE       *NOT* BE #3 FOR MOTION
	PLEASE NOTE     WHEN TAG IS #10 VALUE IS INDEX OF PROPERTY THAT MUST
	PLEASE NOTE       *NOT* BE #4 FOR MOTION
	PLEASE NOTE   SUB #4 OR MORE IS LIST OF MOTION VERBS
	PLEASE NOTE     ZERO IS END OF LIST
	PLEASE NOTE     ONE IS SPECIAL ACTION TAKEN ON ENTERING LOCATION (STILL
	PLEASE NOTE       SUBJECT TO CONDITIONS OF ELEMENT #2)
	PLEASE NOTE
	PLEASE NOTE ,60004 CONTAINS VOCABULARY TABLE
	PLEASE NOTE   FIRST SUBSCRIPT IS THE TABLE INDEX
	PLEASE NOTE   THE ONLY NON-ALPHABETICAL CHARACTERS ARE 2, ", AND ?
	PLEASE NOTE     2 IS MAPPED TO #29
	PLEASE NOTE     " IS MAPPED TO #30
	PLEASE NOTE     ? IS MAPPED TO #31
	PLEASE NOTE   USE THE LOWER 5 BITS OF EACH CHARACTER
	PLEASE NOTE   SUB.I#1 IS FIRST THREE CHARACTERS
	PLEASE NOTE     FIRST CHARACTER IS BITS 1-5
	PLEASE NOTE     SECOND CHARACTER IS BITS 6-10
	PLEASE NOTE     THIRD CHARACTER IS BITS 11-15
	PLEASE NOTE   SUB.I#2 IS FIRST FOURTH AND FIFTH CHARACTERS
	PLEASE NOTE     FOURTH CHARACTER IS BITS 1-5
	PLEASE NOTE     FIFTH CHARACTER IS BITS 6-10
	PLEASE NOTE   SUB.I#3
	PLEASE NOTE     UPPER 8 BITS IS TAG
	PLEASE NOTE     LOWER 8 BITS IS VALUE
	PLEASE NOTE     WHEN TAG IS #1 MOTION VERB
	PLEASE NOTE     WHEN TAG IS #2 OBJECT
	PLEASE NOTE     WHEN TAG IS #3 ACTION VERB
	PLEASE NOTE     WHEN TAG IS #4 VALUE IS INDEX INTO ARBITRARY MESSAGES
	PLEASE NOTE       TABLE ,60006
	PLEASE NOTE   DO NOT KNOW YET WHAT DUPLICATE FEE FIE FOE FOO FUM MEANS
	PLEASE NOTE   DO NOT KNOW YET WHAT TO DO WITH DUPLICATE ROD PLANT TROLL
	PLEASE NOTE     BUT THEY MUST REFER THE ALTERNATE ROD, PLANT, AND TROLL
	PLEASE NOTE     OBJECTS
	PLEASE NOTE   INITIALLY CONSIDERED USING
	PLEASE NOTE     #32 BY #32 BY #32 BY #32 BY #32 TRIE FOR FAST LOOKUPS
	PLEASE NOTE     WITH #0 MAPPED TO #27 BUT THAT WASTES TOO MUCH MEMORY
	PLEASE NOTE
	PLEASE NOTE ,60005 CONTAINS OBJECT DESCRIPTION TABLE
	PLEASE NOTE   FIRST SUBSCRIPT IS OBJECT INDEX
	PLEASE NOTE   SECOND SUBSCRIPT IS OBJECT MESSAGE INDEX
	PLEASE NOTE     #1 IS INVENTORY MESSAGE
	PLEASE NOTE     #2 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #0
	PLEASE NOTE     #3 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #1
	PLEASE NOTE     #4 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #2
	PLEASE NOTE     #5 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #3
	PLEASE NOTE     #6 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #4
	PLEASE NOTE     #7 IS OBJECT DESCRIPTION WHEN OBJECT PROP VALUE IS #5
	PLEASE NOTE   THIRD SUBSCRIPT IS CHARACTER INDEX
	PLEASE NOTE     ZERO TERMINATES MESSAGE
(60000)	DO ,60001 <- #140 BY #1294
	DO ,60002 <- #140 BY #50
	DO ,60013 <- #140
	DO ,60003 <- #500 BY #10
	DO ,60004 <- #296 BY #3
	DO ,60005 <- #64 BY #7 BY #138
	DO (61000) NEXT
	DO RESUME #1
